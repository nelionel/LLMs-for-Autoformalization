import Mathlib.Algebra.Category.ModuleCat.ChangeOfRings
import Mathlib.Algebra.Category.Ring.Basic
universe v v‚ÇÅ u‚ÇÅ u
open CategoryTheory LinearMap Opposite
variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {R : C·µí·µñ ‚•§ RingCat.{u}}
variable (R) in
structure PresheafOfModules where
  obj (X : C·µí·µñ) : ModuleCat.{v} (R.obj X)
  map {X Y : C·µí·µñ} (f : X ‚ü∂ Y) : obj X ‚ü∂ (ModuleCat.restrictScalars (R.map f)).obj (obj Y)
  map_id (X : C·µí·µñ) :
    map (ùüô X) = (ModuleCat.restrictScalarsId' _ (R.map_id X)).inv.app _ := by aesop_cat
  map_comp {X Y Z : C·µí·µñ} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    map (f ‚â´ g) = map f ‚â´ (ModuleCat.restrictScalars _).map (map g) ‚â´
      (ModuleCat.restrictScalarsComp' _ _ _ (R.map_comp f g)).inv.app _ := by aesop_cat
namespace PresheafOfModules
attribute [simp] map_id map_comp
attribute [reassoc] map_comp
variable (M M‚ÇÅ M‚ÇÇ : PresheafOfModules.{v} R)
lemma map_smul {X Y : C·µí·µñ} (f : X ‚ü∂ Y) (r : R.obj X) (m : M.obj X) :
    M.map f (r ‚Ä¢ m) = R.map f r ‚Ä¢ M.map f m := by simp
lemma congr_map_apply {X Y : C·µí·µñ} {f g : X ‚ü∂ Y} (h : f = g) (m : M.obj X) :
    M.map f m = M.map g m := by rw [h]
@[ext]
structure Hom where
  app (X : C·µí·µñ) : M‚ÇÅ.obj X ‚ü∂ M‚ÇÇ.obj X
  naturality {X Y : C·µí·µñ} (f : X ‚ü∂ Y) :
      M‚ÇÅ.map f ‚â´ (ModuleCat.restrictScalars (R.map f)).map (app Y) =
        app X ‚â´ M‚ÇÇ.map f := by aesop_cat
attribute [reassoc (attr := simp)] Hom.naturality
instance : Category (PresheafOfModules.{v} R) where
  Hom := Hom
  id _ := { app := fun _ ‚Ü¶ ùüô _ }
  comp f g := { app := fun _ ‚Ü¶ f.app _ ‚â´ g.app _ }
variable {M‚ÇÅ M‚ÇÇ}
@[ext]
lemma hom_ext {f g : M‚ÇÅ ‚ü∂ M‚ÇÇ} (h : ‚àÄ (X : C·µí·µñ), f.app X = g.app X) :
    f = g := Hom.ext (by ext1; apply h)
@[simp]
lemma id_app (M : PresheafOfModules R) (X : C·µí·µñ) : Hom.app (ùüô M) X = ùüô _ := by
  rfl
@[simp]
lemma comp_app {M‚ÇÅ M‚ÇÇ M‚ÇÉ : PresheafOfModules R} (f : M‚ÇÅ ‚ü∂ M‚ÇÇ) (g : M‚ÇÇ ‚ü∂ M‚ÇÉ) (X : C·µí·µñ) :
    (f ‚â´ g).app X = f.app X ‚â´ g.app X := by
  rfl
lemma naturality_apply (f : M‚ÇÅ ‚ü∂ M‚ÇÇ) {X Y : C·µí·µñ} (g : X ‚ü∂ Y) (x : M‚ÇÅ.obj X) :
    Hom.app f Y (M‚ÇÅ.map g x) = M‚ÇÇ.map g (Hom.app f X x) :=
  congr_fun ((forget _).congr_map (Hom.naturality f g)) x
@[simps!]
def isoMk (app : ‚àÄ (X : C·µí·µñ), M‚ÇÅ.obj X ‚âÖ M‚ÇÇ.obj X)
    (naturality : ‚àÄ ‚¶ÉX Y : C·µí·µñ‚¶Ñ (f : X ‚ü∂ Y),
      M‚ÇÅ.map f ‚â´ (ModuleCat.restrictScalars (R.map f)).map (app Y).hom =
        (app X).hom ‚â´ M‚ÇÇ.map f := by aesop_cat) : M‚ÇÅ ‚âÖ M‚ÇÇ where
  hom := { app := fun X ‚Ü¶ (app X).hom }
  inv :=
    { app := fun X ‚Ü¶ (app X).inv
      naturality := fun {X Y} f ‚Ü¶ by
        rw [‚Üê cancel_epi (app X).hom, ‚Üê reassoc_of% (naturality f), Iso.map_hom_inv_id,
          Category.comp_id, Iso.hom_inv_id_assoc]}
def presheaf : C·µí·µñ ‚•§ Ab where
  obj X := (forget‚ÇÇ _ _).obj (M.obj X)
  map f := AddMonoidHom.mk' (M.map f) (by simp)
@[simp]
lemma presheaf_obj_coe (X : C·µí·µñ) :
    (M.presheaf.obj X : Type _) = M.obj X := rfl
@[simp]
lemma presheaf_map_apply_coe {X Y : C·µí·µñ} (f : X ‚ü∂ Y) (x : M.obj X) :
    DFunLike.coe (Œ± := M.obj X) (Œ≤ := fun _ ‚Ü¶ M.obj Y) (M.presheaf.map f) x = M.map f x := rfl
instance (M : PresheafOfModules R) (X : C·µí·µñ) :
    Module (R.obj X) (M.presheaf.obj X) :=
  inferInstanceAs (Module (R.obj X) (M.obj X))
variable (R) in
def toPresheaf : PresheafOfModules.{v} R ‚•§ C·µí·µñ ‚•§ Ab where
  obj M := M.presheaf
  map f :=
    { app := fun X ‚Ü¶ AddMonoidHom.mk' (Hom.app f X) (by simp)
      naturality := fun X Y g ‚Ü¶ by ext x; exact naturality_apply f g x }
@[simp]
lemma toPresheaf_obj_coe (X : C·µí·µñ) :
    (((toPresheaf R).obj M).obj X : Type _) = M.obj X := rfl
@[simp]
lemma toPresheaf_map_app_apply (f : M‚ÇÅ ‚ü∂ M‚ÇÇ) (X : C·µí·µñ) (x : M‚ÇÅ.obj X) :
    DFunLike.coe (Œ± := M‚ÇÅ.obj X) (Œ≤ := fun _ ‚Ü¶ M‚ÇÇ.obj X)
      (((toPresheaf R).map f).app X) x = f.app X x := rfl
instance : (toPresheaf R).Faithful where
  map_injective {_ _ f g} h := by
    ext X x
    exact congr_fun (((evaluation _ _).obj X ‚ãô forget _).congr_map h) x
section
variable (M : C·µí·µñ ‚•§ Ab.{v}) [‚àÄ X, Module (R.obj X) (M.obj X)]
  (map_smul : ‚àÄ ‚¶ÉX Y : C·µí·µñ‚¶Ñ (f : X ‚ü∂ Y) (r : R.obj X) (m : M.obj X),
    M.map f (r ‚Ä¢ m) = R.map f r ‚Ä¢ M.map f m)
@[simps]
def ofPresheaf : PresheafOfModules.{v} R where
  obj X := ModuleCat.of _ (M.obj X)
  map f :=
    { toFun := fun x ‚Ü¶ M.map f x
      map_add' := by simp
      map_smul' := fun r m ‚Ü¶ map_smul f r m }
@[simp]
lemma ofPresheaf_presheaf : (ofPresheaf M map_smul).presheaf = M := rfl
end
@[simps]
def homMk (œÜ : M‚ÇÅ.presheaf ‚ü∂ M‚ÇÇ.presheaf)
    (hœÜ : ‚àÄ (X : C·µí·µñ) (r : R.obj X) (m : M‚ÇÅ.obj X), œÜ.app X (r ‚Ä¢ m) = r ‚Ä¢ œÜ.app X m) :
    M‚ÇÅ ‚ü∂ M‚ÇÇ where
  app X :=
    { toFun := œÜ.app X
      map_add' := by simp
      map_smul' := hœÜ X }
  naturality := fun f ‚Ü¶ by
    ext x
    exact congr_fun ((forget _).congr_map (œÜ.naturality f)) x
instance : Zero (M‚ÇÅ ‚ü∂ M‚ÇÇ) where
  zero := { app := fun _ ‚Ü¶ 0 }
variable (M‚ÇÅ M‚ÇÇ) in
@[simp] lemma zero_app (X : C·µí·µñ) : (0 : M‚ÇÅ ‚ü∂ M‚ÇÇ).app X = 0 := rfl
instance : Neg (M‚ÇÅ ‚ü∂ M‚ÇÇ) where
  neg f :=
    { app := fun X ‚Ü¶ -f.app X
      naturality := fun {X Y} h ‚Ü¶ by
        ext x
        dsimp
        erw [map_neg]
        rw [‚Üê naturality_apply]
        rfl }
instance : Add (M‚ÇÅ ‚ü∂ M‚ÇÇ) where
  add f g :=
    { app := fun X ‚Ü¶ f.app X + g.app X
      naturality := fun {X Y} h ‚Ü¶ by
        ext x
        dsimp
        erw [map_add]
        rw [‚Üê naturality_apply, ‚Üê naturality_apply]
        rfl }
instance : Sub (M‚ÇÅ ‚ü∂ M‚ÇÇ) where
  sub f g :=
    { app := fun X ‚Ü¶ f.app X - g.app X
      naturality := fun {X Y} h ‚Ü¶ by
        ext x
        dsimp
        erw [map_sub]
        rw [‚Üê naturality_apply, ‚Üê naturality_apply]
        rfl }
@[simp] lemma neg_app (f : M‚ÇÅ ‚ü∂ M‚ÇÇ) (X : C·µí·µñ) : (-f).app X = -f.app X := rfl
@[simp] lemma add_app (f g : M‚ÇÅ ‚ü∂ M‚ÇÇ) (X : C·µí·µñ) : (f + g).app X = f.app X + g.app X := rfl
@[simp] lemma sub_app (f g : M‚ÇÅ ‚ü∂ M‚ÇÇ) (X : C·µí·µñ) : (f - g).app X = f.app X - g.app X := rfl
instance : AddCommGroup (M‚ÇÅ ‚ü∂ M‚ÇÇ) where
  add_assoc := by intros; ext1; simp only [add_app, add_assoc]
  zero_add := by intros; ext1; simp only [add_app, zero_app, zero_add]
  neg_add_cancel := by intros; ext1; simp only [add_app, neg_app, neg_add_cancel, zero_app]
  add_zero := by intros; ext1; simp only [add_app, zero_app, add_zero]
  add_comm := by intros; ext1; simp only [add_app]; apply add_comm
  sub_eq_add_neg := by intros; ext1; simp only [add_app, sub_app, neg_app, sub_eq_add_neg]
  nsmul := nsmulRec
  zsmul := zsmulRec
instance : Preadditive (PresheafOfModules R) where
instance : (toPresheaf R).Additive where
lemma zsmul_app (n : ‚Ñ§) (f : M‚ÇÅ ‚ü∂ M‚ÇÇ) (X : C·µí·µñ) : (n ‚Ä¢ f).app X = n ‚Ä¢ f.app X := by
  ext x
  change (toPresheaf R ‚ãô (evaluation _ _).obj X).map (n ‚Ä¢ f) x = _
  rw [Functor.map_zsmul]
  rfl
variable (R)
@[simps]
def evaluation (X : C·µí·µñ) : PresheafOfModules.{v} R ‚•§ ModuleCat (R.obj X) where
  obj M := M.obj X
  map f := f.app X
instance (X : C·µí·µñ) : (evaluation.{v} R X).Additive where
@[simps]
noncomputable def restriction {X Y : C·µí·µñ} (f : X ‚ü∂ Y) :
    evaluation R X ‚ü∂ evaluation R Y ‚ãô ModuleCat.restrictScalars (R.map f) where
  app M := M.map f
def unit : PresheafOfModules R where
  obj X := ModuleCat.of _ (R.obj X)
  map {X Y } f :=
    { toFun := fun x ‚Ü¶ R.map f x
      map_add' := by simp
      map_smul' := by aesop_cat }
lemma unit_map_one {X Y : C·µí·µñ} (f : X ‚ü∂ Y) : (unit R).map f (1 : R.obj X) = (1 : R.obj Y) :=
  (R.map f).map_one
variable {R}
def sections (M : PresheafOfModules.{v} R) : Type _ := (M.presheaf ‚ãô forget _).sections
abbrev sections.eval {M : PresheafOfModules.{v} R} (s : M.sections) (X : C·µí·µñ) : M.obj X := s.1 X
@[simp]
lemma sections_property {M : PresheafOfModules.{v} R} (s : M.sections)
    {X Y : C·µí·µñ} (f : X ‚ü∂ Y) : M.map f (s.1 X) = s.1 Y := s.2 f
@[simps]
def sectionsMk {M : PresheafOfModules.{v} R} (s : ‚àÄ X, M.obj X)
    (hs : ‚àÄ ‚¶ÉX Y : C·µí·µñ‚¶Ñ (f : X ‚ü∂ Y), M.map f (s X) = s Y) : M.sections where
  val := s
  property f := hs f
@[ext]
lemma sections_ext {M : PresheafOfModules.{v} R} (s t : M.sections)
    (h : ‚àÄ (X : C·µí·µñ), s.val X = t.val X) : s = t :=
  Subtype.ext (by ext; apply h)
@[simps!]
def sectionsMap {M N : PresheafOfModules.{v} R} (f : M ‚ü∂ N) (s : M.sections) : N.sections :=
  N.sectionsMk (fun X ‚Ü¶ f.app X (s.1 _))
    (fun X Y g ‚Ü¶ by rw [‚Üê naturality_apply, sections_property])
@[simp]
lemma sectionsMap_comp {M N P : PresheafOfModules.{v} R} (f : M ‚ü∂ N) (g : N ‚ü∂ P) (s : M.sections) :
    sectionsMap (f ‚â´ g) s = sectionsMap g (sectionsMap f s) := rfl
@[simp]
lemma sectionsMap_id {M : PresheafOfModules.{v} R} (s : M.sections) :
    sectionsMap (ùüô M) s = s := rfl
@[simps! apply_coe]
def unitHomEquiv (M : PresheafOfModules R) :
    (unit R ‚ü∂ M) ‚âÉ M.sections where
  toFun f := sectionsMk (fun X ‚Ü¶ Hom.app f X (1 : R.obj X))
    (by intros; rw [‚Üê naturality_apply, unit_map_one])
  invFun s :=
    { app := fun X ‚Ü¶ (LinearMap.ringLmapEquivSelf (R.obj X) ‚Ñ§ (M.obj X)).symm (s.val X)
      naturality := fun {X Y} f ‚Ü¶ by
        ext (x : R.obj X)
        change R.map f x ‚Ä¢ s.eval Y = M.map f (x ‚Ä¢ s.eval X)
        simp }
  left_inv f := by
    ext1 X
    exact (LinearMap.ringLmapEquivSelf (R.obj X) ‚Ñ§ (M.obj X)).symm_apply_apply (f.app X)
  right_inv s := by
    ext X
    exact (LinearMap.ringLmapEquivSelf (R.obj X) ‚Ñ§ (M.obj X)).apply_symm_apply (s.val X)
section module_over_initial
variable (X : C·µí·µñ) (hX : Limits.IsInitial X)
section
variable (M : PresheafOfModules.{v} R)
noncomputable abbrev forgetToPresheafModuleCatObjObj (Y : C·µí·µñ) : ModuleCat (R.obj X) :=
  (ModuleCat.restrictScalars (R.map (hX.to Y))).obj (M.obj Y)
@[simp]
lemma forgetToPresheafModuleCatObjObj_coe (Y : C·µí·µñ) :
    (forgetToPresheafModuleCatObjObj X hX M Y : Type _) = M.obj Y := rfl
def forgetToPresheafModuleCatObjMap {Y Z : C·µí·µñ} (f : Y ‚ü∂ Z) :
    forgetToPresheafModuleCatObjObj X hX M Y ‚ü∂
      forgetToPresheafModuleCatObjObj X hX M Z where
  toFun x := M.map f x
  map_add' := by simp
  map_smul' r x := by
    simp only [ModuleCat.restrictScalars.smul_def, AddHom.toFun_eq_coe, AddHom.coe_mk,
      RingHom.id_apply, M.map_smul]
    rw [‚Üê CategoryTheory.comp_apply, ‚Üê R.map_comp]
    congr
    apply hX.hom_ext
@[simp]
lemma forgetToPresheafModuleCatObjMap_apply {Y Z : C·µí·µñ} (f : Y ‚ü∂ Z) (m : M.obj Y) :
    DFunLike.coe (Œ± := M.obj Y) (Œ≤ := fun _ ‚Ü¶ M.obj Z)
      (forgetToPresheafModuleCatObjMap X hX M f) m = M.map f m := rfl
@[simps]
noncomputable def forgetToPresheafModuleCatObj
    (X : C·µí·µñ) (hX : Limits.IsInitial X) (M : PresheafOfModules.{v} R) :
    C·µí·µñ ‚•§ ModuleCat (R.obj X) where
  obj Y := forgetToPresheafModuleCatObjObj X hX M Y
  map f := forgetToPresheafModuleCatObjMap X hX M f
end
noncomputable def forgetToPresheafModuleCatMap
    (X : C·µí·µñ) (hX : Limits.IsInitial X) {M N : PresheafOfModules.{v} R} (f : M ‚ü∂ N) :
    forgetToPresheafModuleCatObj X hX M ‚ü∂ forgetToPresheafModuleCatObj X hX N where
  app Y :=
    { toFun := f.app Y
      map_add' := by simp
      map_smul' := fun r ‚Ü¶ (f.app Y).map_smul (R.1.map (hX.to Y) _) }
  naturality Y Z g := by
    ext x
    exact naturality_apply f g x
@[simps]
noncomputable def forgetToPresheafModuleCat (X : C·µí·µñ) (hX : Limits.IsInitial X) :
    PresheafOfModules.{v} R ‚•§ C·µí·µñ ‚•§ ModuleCat (R.obj X) where
  obj M := forgetToPresheafModuleCatObj X hX M
  map f := forgetToPresheafModuleCatMap X hX f
end module_over_initial
end PresheafOfModules