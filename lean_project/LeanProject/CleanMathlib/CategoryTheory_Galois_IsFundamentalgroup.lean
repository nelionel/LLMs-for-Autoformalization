import Mathlib.CategoryTheory.Galois.Basic
import Mathlib.CategoryTheory.Galois.Topology
import Mathlib.CategoryTheory.Galois.Prorepresentability
import Mathlib.Topology.Algebra.OpenSubgroup
universe u‚ÇÅ u‚ÇÇ w
namespace CategoryTheory
namespace PreGaloisCategory
open Limits Functor
variable {C : Type u‚ÇÅ} [Category.{u‚ÇÇ} C] (F : C ‚•§ FintypeCat.{w})
section
variable (G : Type*) [Group G] [‚àÄ X, MulAction G (F.obj X)]
class IsNaturalSMul : Prop where
  naturality (g : G) {X Y : C} (f : X ‚ü∂ Y) (x : F.obj X) : F.map f (g ‚Ä¢ x) = g ‚Ä¢ F.map f x
variable {G} in
@[simps!]
private def isoOnObj (g : G) (X : C) : F.obj X ‚âÖ F.obj X :=
  FintypeCat.equivEquivIso <| {
    toFun := fun x ‚Ü¶ g ‚Ä¢ x
    invFun := fun x ‚Ü¶ g‚Åª¬π ‚Ä¢ x
    left_inv := fun _ ‚Ü¶ by simp
    right_inv := fun _ ‚Ü¶ by simp
  }
variable [IsNaturalSMul F G]
def toAut : G ‚Üí* Aut F where
  toFun g := NatIso.ofComponents (isoOnObj F g) <| by
    intro X Y f
    ext
    simp [IsNaturalSMul.naturality]
  map_one' := by
    ext
    simp only [NatIso.ofComponents_hom_app, isoOnObj_hom, one_smul]
    rfl
  map_mul' := by
    intro g h
    ext X x
    simp only [NatIso.ofComponents_hom_app, isoOnObj_hom, mul_smul]
    rfl
variable {G} in
@[simp]
lemma toAut_hom_app_apply (g : G) {X : C} (x : F.obj X) : (toAut F G g).hom.app X x = g ‚Ä¢ x :=
  rfl
lemma toAut_injective_of_non_trivial (h : ‚àÄ (g : G), (‚àÄ (X : C) (x : F.obj X), g ‚Ä¢ x = x) ‚Üí g = 1) :
    Function.Injective (toAut F G) := by
  rw [‚Üê MonoidHom.ker_eq_bot_iff, eq_bot_iff]
  intro g (hg : toAut F G g = 1)
  refine h g (fun X x ‚Ü¶ ?_)
  have : (toAut F G g).hom.app X = ùüô (F.obj X) := by
    rw [hg]
    rfl
  rw [‚Üê toAut_hom_app_apply, this, FintypeCat.id_apply]
variable [GaloisCategory C] [FiberFunctor F]
lemma toAut_continuous [TopologicalSpace G] [TopologicalGroup G]
    [‚àÄ (X : C), ContinuousSMul G (F.obj X)] :
    Continuous (toAut F G) := by
  apply continuous_of_continuousAt_one
  rw [continuousAt_def, map_one]
  intro A hA
  obtain ‚ü®X, _, hX‚ü© := ((nhds_one_has_basis_stabilizers F).mem_iff' A).mp hA
  rw [mem_nhds_iff]
  exact ‚ü®MulAction.stabilizer G X.pt, Set.preimage_mono (f := toAut F G) hX,
    stabilizer_isOpen G X.pt, one_mem _‚ü©
variable {G}
lemma action_ext_of_isGalois {t : F ‚ü∂ F} {X : C} [IsGalois X] {g : G} (x : F.obj X)
    (hg : g ‚Ä¢ x = t.app X x) (y : F.obj X) : g ‚Ä¢ y = t.app X y := by
  obtain ‚ü®œÜ, (rfl : F.map œÜ.hom y = x)‚ü© := MulAction.exists_smul_eq (Aut X) y x
  have : Function.Injective (F.map œÜ.hom) :=
    ConcreteCategory.injective_of_mono_of_preservesPullback (F.map œÜ.hom)
  apply this
  rw [IsNaturalSMul.naturality, hg, FunctorToFintypeCat.naturality]
variable (G)
lemma toAut_surjective_isGalois (t : Aut F) (X : C) [IsGalois X]
    [MulAction.IsPretransitive G (F.obj X)] :
    ‚àÉ (g : G), ‚àÄ (x : F.obj X), g ‚Ä¢ x = t.hom.app X x := by
  obtain ‚ü®a‚ü© := nonempty_fiber_of_isConnected F X
  obtain ‚ü®g, hg‚ü© := MulAction.exists_smul_eq G a (t.hom.app X a)
  exact ‚ü®g, action_ext_of_isGalois F _ hg‚ü©
lemma toAut_surjective_isGalois_finite_family (t : Aut F) {Œπ : Type*} [Finite Œπ] (X : Œπ ‚Üí C)
    [‚àÄ i, IsGalois (X i)] (h : ‚àÄ (X : C) [IsGalois X], MulAction.IsPretransitive G (F.obj X)) :
    ‚àÉ (g : G), ‚àÄ (i : Œπ) (x : F.obj (X i)), g ‚Ä¢ x = t.hom.app (X i) x := by
  let x (i : Œπ) : F.obj (X i) := (nonempty_fiber_of_isConnected F (X i)).some
  let P : C := ‚àè·∂ú X
  letI : Fintype Œπ := Fintype.ofFinite Œπ
  let is‚ÇÅ : F.obj P ‚âÖ ‚àè·∂ú fun i ‚Ü¶ (F.obj (X i)) := PreservesProduct.iso F X
  let is‚ÇÇ : (‚àè·∂ú fun i ‚Ü¶ F.obj (X i) : FintypeCat) ‚âÉ ‚àÄ i, F.obj (X i) :=
    Limits.FintypeCat.productEquiv (fun i ‚Ü¶ (F.obj (X i)))
  let px : F.obj P := is‚ÇÅ.inv (is‚ÇÇ.symm x)
  have hpx (i : Œπ) : F.map (Pi.œÄ X i) px = x i := by
    simp only [px, is‚ÇÅ, is‚ÇÇ, ‚Üê piComparison_comp_œÄ, ‚Üê PreservesProduct.iso_hom]
    simp only [FintypeCat.comp_apply, FintypeCat.inv_hom_id_apply,
      FintypeCat.productEquiv_symm_comp_œÄ_apply]
  obtain ‚ü®A, f, a, _, hfa‚ü© := exists_hom_from_galois_of_fiber F P px
  obtain ‚ü®g, hg‚ü© := toAut_surjective_isGalois F G t A
  refine ‚ü®g, fun i y ‚Ü¶ action_ext_of_isGalois F (x i) ?_ _‚ü©
  rw [‚Üê hpx i, ‚Üê IsNaturalSMul.naturality, FunctorToFintypeCat.naturality,
    ‚Üê hfa, FunctorToFintypeCat.naturality, ‚Üê IsNaturalSMul.naturality, hg]
open Pointwise
lemma toAut_surjective_of_isPretransitive [TopologicalSpace G] [TopologicalGroup G] [CompactSpace G]
    [‚àÄ (X : C), ContinuousSMul G (F.obj X)]
    (h : ‚àÄ (X : C) [IsGalois X], MulAction.IsPretransitive G (F.obj X)) :
    Function.Surjective (toAut F G) := by
  intro t
  choose gi hgi using (fun X : PointedGaloisObject F ‚Ü¶ toAut_surjective_isGalois F G t X)
  let cl (X : PointedGaloisObject F) : Set G := gi X ‚Ä¢ MulAction.stabilizer G X.pt
  let c : Set G := ‚ãÇ i, cl i
  have hne : c.Nonempty := by
    rw [‚Üê Set.univ_inter c]
    apply CompactSpace.isCompact_univ.inter_iInter_nonempty
    ¬∑ intro X
      apply IsClosed.leftCoset
      exact Subgroup.isClosed_of_isOpen _ (stabilizer_isOpen G X.pt)
    ¬∑ intro s
      rw [Set.univ_inter]
      obtain ‚ü®gs, hgs‚ü© :=
        toAut_surjective_isGalois_finite_family F G t (fun X : s ‚Ü¶ X.val.obj) h
      use gs
      simp only [Set.mem_iInter]
      intro X hXmem
      rw [mem_leftCoset_iff, SetLike.mem_coe, MulAction.mem_stabilizer_iff, mul_smul,
        hgs ‚ü®X, hXmem‚ü©, ‚Üê hgi X, inv_smul_smul]
  obtain ‚ü®g, hg‚ü© := hne
  refine ‚ü®g, Iso.ext <| natTrans_ext_of_isGalois _ <| fun X _ ‚Ü¶ ?_‚ü©
  ext x
  simp only [toAut_hom_app_apply]
  have : g ‚àà (gi ‚ü®X, x, inferInstance‚ü© ‚Ä¢ MulAction.stabilizer G x : Set G) := by
    simp only [Set.mem_iInter, c] at hg
    exact hg _
  obtain ‚ü®s, (hsmem : s ‚Ä¢ x = x), (rfl : gi ‚ü®X, x, inferInstance‚ü© ‚Ä¢ s = _)‚ü© := this
  rw [smul_eq_mul, mul_smul, hsmem]
  exact hgi ‚ü®X, x, inferInstance‚ü© x
lemma isPretransitive_of_surjective (h : Function.Surjective (toAut F G)) (X : C)
    [IsConnected X] : MulAction.IsPretransitive G (F.obj X) where
  exists_smul_eq x y := by
    obtain ‚ü®t, ht‚ü© := MulAction.exists_smul_eq (Aut F) x y
    obtain ‚ü®g, rfl‚ü© := h t
    exact ‚ü®g, ht‚ü©
end
section
variable [GaloisCategory C]
variable (G : Type*) [Group G] [‚àÄ (X : C), MulAction G (F.obj X)]
class IsFundamentalGroup [TopologicalSpace G] [TopologicalGroup G] [CompactSpace G]
    extends IsNaturalSMul F G : Prop where
  transitive_of_isGalois (X : C) [IsGalois X] : MulAction.IsPretransitive G (F.obj X)
  continuous_smul (X : C) : ContinuousSMul G (F.obj X)
  non_trivial' (g : G) : (‚àÄ (X : C) (x : F.obj X), g ‚Ä¢ x = x) ‚Üí g = 1
namespace IsFundamentalGroup
attribute [instance] continuous_smul transitive_of_isGalois
variable {G} [TopologicalSpace G] [TopologicalGroup G] [CompactSpace G] [IsFundamentalGroup F G]
lemma non_trivial (g : G) (h : ‚àÄ (X : C) (x : F.obj X), g ‚Ä¢ x = x) : g = 1 :=
  IsFundamentalGroup.non_trivial' g h
end IsFundamentalGroup
variable [FiberFunctor F]
instance : IsFundamentalGroup F (Aut F) where
  naturality g _ _ f x := (FunctorToFintypeCat.naturality F F g.hom f x).symm
  transitive_of_isGalois X := FiberFunctor.isPretransitive_of_isConnected F X
  continuous_smul X := continuousSMul_aut_fiber F X
  non_trivial' g h := by
    ext X x
    exact h X x
variable [TopologicalSpace G] [TopologicalGroup G] [CompactSpace G] [IsFundamentalGroup F G]
lemma toAut_bijective : Function.Bijective (toAut F G) where
  left := toAut_injective_of_non_trivial F G IsFundamentalGroup.non_trivial'
  right := toAut_surjective_of_isPretransitive F G IsFundamentalGroup.transitive_of_isGalois
instance (X : C) [IsConnected X] : MulAction.IsPretransitive G (F.obj X) :=
  isPretransitive_of_surjective F G (toAut_bijective F G).surjective X
noncomputable def toAutMulEquiv : G ‚âÉ* Aut F :=
  MulEquiv.ofBijective (toAut F G) (toAut_bijective F G)
lemma toAut_isHomeomorph : IsHomeomorph (toAut F G) := by
  rw [isHomeomorph_iff_continuous_bijective]
  exact ‚ü®toAut_continuous F G, toAut_bijective F G‚ü©
lemma toAutMulEquiv_isHomeomorph : IsHomeomorph (toAutMulEquiv F G) :=
  toAut_isHomeomorph F G
noncomputable def toAutHomeo : G ‚âÉ‚Çú Aut F := (toAut_isHomeomorph F G).homeomorph
variable {G}
@[simp]
lemma toAutMulEquiv_apply (g : G) : toAutMulEquiv F G g = toAut F G g := rfl
@[simp]
lemma toAutHomeo_apply (g : G) : toAutHomeo F G g = toAut F G g := rfl
end
end PreGaloisCategory
end CategoryTheory