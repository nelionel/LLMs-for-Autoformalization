import Mathlib.LinearAlgebra.Matrix.Gershgorin
import Mathlib.NumberTheory.NumberField.CanonicalEmbedding.ConvexBody
import Mathlib.NumberTheory.NumberField.Units.Basic
open scoped NumberField
noncomputable section
open NumberField NumberField.InfinitePlace NumberField.Units
variable (K : Type*) [Field K]
namespace NumberField.Units.dirichletUnitTheorem
open scoped Classical
open Finset
variable {K}
section NumberField
variable [NumberField K]
def wâ‚€ : InfinitePlace K := (inferInstance : Nonempty (InfinitePlace K)).some
variable (K)
def _root_.NumberField.Units.logEmbedding :
    Additive ((ğ“ K)Ë£) â†’+ ({w : InfinitePlace K // w â‰  wâ‚€} â†’ â„) :=
{ toFun := fun x w => mult w.val * Real.log (w.val â†‘x.toMul)
  map_zero' := by simp; rfl
  map_add' := fun _ _ => by simp [Real.log_mul, mul_add]; rfl }
variable {K}
@[simp]
theorem logEmbedding_component (x : (ğ“ K)Ë£) (w : {w : InfinitePlace K // w â‰  wâ‚€}) :
    (logEmbedding K (Additive.ofMul x)) w = mult w.val * Real.log (w.val x) := rfl
theorem sum_logEmbedding_component (x : (ğ“ K)Ë£) :
    âˆ‘ w, logEmbedding K (Additive.ofMul x) w =
      - mult (wâ‚€ : InfinitePlace K) * Real.log (wâ‚€ (x : K)) := by
  have h := congr_arg Real.log (prod_eq_abs_norm (x : K))
  rw [Units.norm, Rat.cast_one, Real.log_one, Real.log_prod] at h
  Â· simp_rw [Real.log_pow] at h
    rw [â† insert_erase (mem_univ wâ‚€), sum_insert (not_mem_erase wâ‚€ univ), add_comm,
      add_eq_zero_iff_eq_neg] at h
    convert h using 1
    Â· refine (sum_subtype _ (fun w => ?_) (fun w => (mult w) * (Real.log (w (x : K))))).symm
      exact âŸ¨ne_of_mem_erase, fun h => mem_erase_of_ne_of_mem h (mem_univ w)âŸ©
    Â· norm_num
  Â· exact fun w _ => pow_ne_zero _ (AbsoluteValue.ne_zero _ (coe_ne_zero x))
end NumberField
theorem mult_log_place_eq_zero {x : (ğ“ K)Ë£} {w : InfinitePlace K} :
    mult w * Real.log (w x) = 0 â†” w x = 1 := by
  rw [mul_eq_zero, or_iff_right, Real.log_eq_zero, or_iff_right, or_iff_left]
  Â· linarith [(apply_nonneg _ _ : 0 â‰¤ w x)]
  Â· simp only [ne_eq, map_eq_zero, coe_ne_zero x, not_false_eq_true]
  Â· refine (ne_of_gt ?_)
    rw [mult]; split_ifs <;> norm_num
variable [NumberField K]
theorem logEmbedding_eq_zero_iff {x : (ğ“ K)Ë£} :
    logEmbedding K (Additive.ofMul x) = 0 â†” x âˆˆ torsion K := by
  rw [mem_torsion]
  refine âŸ¨fun h w => ?_, fun h => ?_âŸ©
  Â· by_cases hw : w = wâ‚€
    Â· suffices -mult wâ‚€ * Real.log (wâ‚€ (x : K)) = 0 by
        rw [neg_mul, neg_eq_zero, â† hw] at this
        exact mult_log_place_eq_zero.mp this
      rw [â† sum_logEmbedding_component, sum_eq_zero]
      exact fun w _ => congrFun h w
    Â· exact mult_log_place_eq_zero.mp (congrFun h âŸ¨w, hwâŸ©)
  Â· ext w
    rw [logEmbedding_component, h w.val, Real.log_one, mul_zero, Pi.zero_apply]
theorem logEmbedding_component_le {r : â„} {x : (ğ“ K)Ë£} (hr : 0 â‰¤ r) (h : â€–logEmbedding K xâ€– â‰¤ r)
    (w : {w : InfinitePlace K // w â‰  wâ‚€}) : |logEmbedding K (Additive.ofMul x) w| â‰¤ r := by
  lift r to NNReal using hr
  simp_rw [Pi.norm_def, NNReal.coe_le_coe, Finset.sup_le_iff, â† NNReal.coe_le_coe] at h
  exact h w (mem_univ _)
theorem log_le_of_logEmbedding_le {r : â„} {x : (ğ“ K)Ë£} (hr : 0 â‰¤ r)
    (h : â€–logEmbedding K (Additive.ofMul x)â€– â‰¤ r) (w : InfinitePlace K) :
    |Real.log (w x)| â‰¤ (Fintype.card (InfinitePlace K)) * r := by
  have tool : âˆ€ x : â„, 0 â‰¤ x â†’ x â‰¤ mult w * x := fun x hx => by
    nth_rw 1 [â† one_mul x]
    refine mul_le_mul ?_ le_rfl hx ?_
    all_goals { rw [mult]; split_ifs <;> norm_num }
  by_cases hw : w = wâ‚€
  Â· have hyp := congr_arg (â€–Â·â€–) (sum_logEmbedding_component x).symm
    replace hyp := (le_of_eq hyp).trans (norm_sum_le _ _)
    simp_rw [norm_mul, norm_neg, Real.norm_eq_abs, Nat.abs_cast] at hyp
    refine (le_trans ?_ hyp).trans ?_
    Â· rw [â† hw]
      exact tool _ (abs_nonneg _)
    Â· refine (sum_le_card_nsmul univ _ _
        (fun w _ => logEmbedding_component_le hr h w)).trans ?_
      rw [nsmul_eq_mul]
      refine mul_le_mul ?_ le_rfl hr (Fintype.card (InfinitePlace K)).cast_nonneg
      simp [card_univ]
  Â· have hyp := logEmbedding_component_le hr h âŸ¨w, hwâŸ©
    rw [logEmbedding_component, abs_mul, Nat.abs_cast] at hyp
    refine (le_trans ?_ hyp).trans ?_
    Â· exact tool _ (abs_nonneg _)
    Â· nth_rw 1 [â† one_mul r]
      exact mul_le_mul (Nat.one_le_cast.mpr Fintype.card_pos) (le_of_eq rfl) hr (Nat.cast_nonneg _)
variable (K)
noncomputable def _root_.NumberField.Units.unitLattice :
    Submodule â„¤ ({w : InfinitePlace K // w â‰  wâ‚€} â†’ â„) :=
  Submodule.map (logEmbedding K).toIntLinearMap âŠ¤
theorem unitLattice_inter_ball_finite (r : â„) :
    ((unitLattice K : Set ({ w : InfinitePlace K // w â‰  wâ‚€} â†’ â„)) âˆ©
      Metric.closedBall 0 r).Finite := by
  obtain hr | hr := lt_or_le r 0
  Â· convert Set.finite_empty
    rw [Metric.closedBall_eq_empty.mpr hr]
    exact Set.inter_empty _
  Â· suffices {x : (ğ“ K)Ë£ | IsIntegral â„¤ (x : K) âˆ§
        âˆ€ (Ï† : K â†’+* â„‚), â€–Ï† xâ€– â‰¤ Real.exp ((Fintype.card (InfinitePlace K)) * r)}.Finite by
      refine (Set.Finite.image (logEmbedding K) this).subset ?_
      rintro _ âŸ¨âŸ¨x, âŸ¨_, rflâŸ©âŸ©, hxâŸ©
      refine âŸ¨x, âŸ¨x.val.prop, (le_iff_le _ _).mp (fun w => (Real.log_le_iff_le_exp ?_).mp ?_)âŸ©, rflâŸ©
      Â· exact pos_iff.mpr (coe_ne_zero x)
      Â· rw [mem_closedBall_zero_iff] at hx
        exact (le_abs_self _).trans (log_le_of_logEmbedding_le hr hx w)
    refine Set.Finite.of_finite_image ?_ (coe_injective K).injOn
    refine (Embeddings.finite_of_norm_le K â„‚
        (Real.exp ((Fintype.card (InfinitePlace K)) * r))).subset ?_
    rintro _ âŸ¨x, âŸ¨âŸ¨h_int, h_leâŸ©, rflâŸ©âŸ©
    exact âŸ¨h_int, h_leâŸ©
section span_top
open NumberField.mixedEmbedding NNReal
variable (wâ‚ : InfinitePlace K) {B : â„•} (hB : minkowskiBound K 1 < (convexBodyLTFactor K) * B)
include hB in
theorem seq_next {x : ğ“ K} (hx : x â‰  0) :
    âˆƒ y : ğ“ K, y â‰  0 âˆ§
      (âˆ€ w, w â‰  wâ‚ â†’ w y < w x) âˆ§
      |Algebra.norm â„š (y : K)| â‰¤ B := by
  have hx' := RingOfIntegers.coe_ne_zero_iff.mpr hx
  let f : InfinitePlace K â†’ â„â‰¥0 :=
    fun w => âŸ¨(w x) / 2, div_nonneg (AbsoluteValue.nonneg _ _) (by norm_num)âŸ©
  suffices âˆ€ w, w â‰  wâ‚ â†’ f w â‰  0 by
    obtain âŸ¨g, h_geqf, h_gprodâŸ© := adjust_f K B this
    obtain âŸ¨y, h_ynz, h_yleâŸ© := exists_ne_zero_mem_ringOfIntegers_lt K (f := g)
      (by rw [convexBodyLT_volume]; convert hB; exact congr_arg ((â†‘) : NNReal â†’ ENNReal) h_gprod)
    refine âŸ¨y, h_ynz, fun w hw => (h_geqf w hw â–¸ h_yle w).trans ?_, ?_âŸ©
    Â· rw [â† Rat.cast_le (K := â„), Rat.cast_natCast]
      calc
        _ = âˆ w : InfinitePlace K, w (algebraMap _ K y) ^ mult w :=
          (prod_eq_abs_norm (algebraMap _ K y)).symm
        _ â‰¤ âˆ w : InfinitePlace K, (g w : â„) ^ mult w := by gcongr with w; exact (h_yle w).le
        _ â‰¤ (B : â„) := by
          simp_rw [â† NNReal.coe_pow, â† NNReal.coe_prod]
          exact le_of_eq (congr_arg toReal h_gprod)
    Â· refine div_lt_self ?_ (by norm_num)
      exact pos_iff.mpr hx'
  intro _ _
  rw [ne_eq, Nonneg.mk_eq_zero, div_eq_zero_iff, map_eq_zero, not_or]
  exact âŸ¨hx', by norm_numâŸ©
def seq : â„• â†’ { x : ğ“ K // x â‰  0 }
  | 0 => âŸ¨1, by norm_numâŸ©
  | n + 1 =>
    âŸ¨(seq_next K wâ‚ hB (seq n).prop).choose, (seq_next K wâ‚ hB (seq n).prop).choose_spec.1âŸ©
theorem seq_ne_zero (n : â„•) : algebraMap (ğ“ K) K (seq K wâ‚ hB n) â‰  0 :=
  RingOfIntegers.coe_ne_zero_iff.mpr (seq K wâ‚ hB n).prop
theorem seq_decreasing {n m : â„•} (h : n < m) (w : InfinitePlace K) (hw : w â‰  wâ‚) :
    w (algebraMap (ğ“ K) K (seq K wâ‚ hB m)) < w (algebraMap (ğ“ K) K (seq K wâ‚ hB n)) := by
  induction m with
  | zero =>
      exfalso
      exact Nat.not_succ_le_zero n h
  | succ m m_ih =>
      cases eq_or_lt_of_le (Nat.le_of_lt_succ h) with
      | inl hr =>
          rw [hr]
          exact (seq_next K wâ‚ hB (seq K wâ‚ hB m).prop).choose_spec.2.1 w hw
      | inr hr =>
          refine lt_trans ?_ (m_ih hr)
          exact (seq_next K wâ‚ hB (seq K wâ‚ hB m).prop).choose_spec.2.1 w hw
theorem seq_norm_le (n : â„•) :
    Int.natAbs (Algebra.norm â„¤ (seq K wâ‚ hB n : ğ“ K)) â‰¤ B := by
  cases n with
  | zero =>
      have : 1 â‰¤ B := by
        contrapose! hB
        simp only [Nat.lt_one_iff.mp hB, CharP.cast_eq_zero, mul_zero, zero_le]
      simp only [ne_eq, seq, map_one, Int.natAbs_one, this]
  | succ n =>
      rw [â† Nat.cast_le (Î± := â„š), Int.cast_natAbs, Int.cast_abs, Algebra.coe_norm_int]
      exact (seq_next K wâ‚ hB (seq K wâ‚ hB n).prop).choose_spec.2.2
theorem exists_unit (wâ‚ : InfinitePlace K) :
    âˆƒ u : (ğ“ K)Ë£, âˆ€ w : InfinitePlace K, w â‰  wâ‚ â†’ Real.log (w u) < 0 := by
  obtain âŸ¨B, hBâŸ© : âˆƒ B : â„•, minkowskiBound K 1 < (convexBodyLTFactor K) * B := by
    conv => congr; ext; rw [mul_comm]
    exact ENNReal.exists_nat_mul_gt (ENNReal.coe_ne_zero.mpr (convexBodyLTFactor_ne_zero K))
      (ne_of_lt (minkowskiBound_lt_top K 1))
  rsuffices âŸ¨n, m, hnm, hâŸ© : âˆƒ n m, n < m âˆ§
      (Ideal.span ({ (seq K wâ‚ hB n : ğ“ K) }) = Ideal.span ({ (seq K wâ‚ hB m : ğ“ K) }))
  Â· have hu := Ideal.span_singleton_eq_span_singleton.mp h
    refine âŸ¨hu.choose, fun w hw => Real.log_neg ?_ ?_âŸ©
    Â· exact pos_iff.mpr (coe_ne_zero _)
    Â· calc
        _ = w (algebraMap (ğ“ K) K (seq K wâ‚ hB m) * (algebraMap (ğ“ K) K (seq K wâ‚ hB n))â»Â¹) := by
          rw [â† congr_arg (algebraMap (ğ“ K) K) hu.choose_spec, mul_comm, map_mul (algebraMap _ _),
          â† mul_assoc, inv_mul_cancelâ‚€ (seq_ne_zero K wâ‚ hB n), one_mul]
      _ = w (algebraMap (ğ“ K) K (seq K wâ‚ hB m)) * w (algebraMap (ğ“ K) K (seq K wâ‚ hB n))â»Â¹ :=
        _root_.map_mul _ _ _
      _ < 1 := by
        rw [map_invâ‚€, mul_inv_lt_iffâ‚€' (pos_iff.mpr (seq_ne_zero K wâ‚ hB n)), mul_one]
        exact seq_decreasing K wâ‚ hB hnm w hw
  refine Set.Finite.exists_lt_map_eq_of_forall_mem (t := {I : Ideal (ğ“ K) | Ideal.absNorm I â‰¤ B})
    (fun n â†¦ ?_) (Ideal.finite_setOf_absNorm_le B)
  rw [Set.mem_setOf_eq, Ideal.absNorm_span_singleton]
  exact seq_norm_le K wâ‚ hB n
theorem unitLattice_span_eq_top :
    Submodule.span â„ (unitLattice K : Set ({w : InfinitePlace K // w â‰  wâ‚€} â†’ â„)) = âŠ¤ := by
  refine le_antisymm le_top ?_
  let B := Pi.basisFun â„ {w : InfinitePlace K // w â‰  wâ‚€}
  let v := fun w : { w : InfinitePlace K // w â‰  wâ‚€ } =>
    logEmbedding K (Additive.ofMul (exists_unit K w).choose)
  suffices B.det v â‰  0 by
    rw [â† isUnit_iff_ne_zero, â† is_basis_iff_det] at this
    rw [â† this.2]
    refine  Submodule.span_monotone fun _ âŸ¨w, hwâŸ© â†¦ âŸ¨(exists_unit K w).choose, trivial, hwâŸ©
  rw [Basis.det_apply]
  refine det_ne_zero_of_sum_col_lt_diag (fun w => ?_)
  simp_rw [Real.norm_eq_abs, B, Basis.coePiBasisFun.toMatrix_eq_transpose, Matrix.transpose_apply]
  rw [â† sub_pos, sum_congr rfl (fun x hx => abs_of_neg ?_), sum_neg_distrib, sub_neg_eq_add,
    sum_erase_eq_sub (mem_univ _), â† add_comm_sub]
  Â· refine add_pos_of_nonneg_of_pos ?_ ?_
    Â· rw [sub_nonneg]
      exact le_abs_self _
    Â· rw [sum_logEmbedding_component (exists_unit K w).choose]
      refine mul_pos_of_neg_of_neg ?_ ((exists_unit K w).choose_spec _ w.prop.symm)
      rw [mult]; split_ifs <;> norm_num
  Â· refine mul_neg_of_pos_of_neg ?_ ((exists_unit K w).choose_spec x ?_)
    Â· rw [mult]; split_ifs <;> norm_num
    Â· exact Subtype.ext_iff_val.not.mp (ne_of_mem_erase hx)
end span_top
end dirichletUnitTheorem
section statements
variable [NumberField K]
open scoped Classical
open dirichletUnitTheorem Module
def rank : â„• := Fintype.card (InfinitePlace K) - 1
instance instDiscrete_unitLattice : DiscreteTopology (unitLattice K) := by
  refine discreteTopology_of_isOpen_singleton_zero ?_
  refine isOpen_singleton_of_finite_mem_nhds 0 (s := Metric.closedBall 0 1) ?_ ?_
  Â· exact Metric.closedBall_mem_nhds _ (by norm_num)
  Â· refine Set.Finite.of_finite_image ?_ (Set.injOn_of_injective Subtype.val_injective)
    convert unitLattice_inter_ball_finite K 1
    ext x
    refine âŸ¨?_, fun âŸ¨hx1, hx2âŸ© => âŸ¨âŸ¨x, hx1âŸ©, hx2, rflâŸ©âŸ©
    rintro âŸ¨x, hx, rflâŸ©
    exact âŸ¨Subtype.mem x, hxâŸ©
instance instZLattice_unitLattice : IsZLattice â„ (unitLattice K) where
  span_top := unitLattice_span_eq_top K
protected theorem finrank_eq_rank :
    finrank â„ ({w : InfinitePlace K // w â‰  wâ‚€} â†’ â„) = Units.rank K := by
  simp only [finrank_fintype_fun_eq_card, Fintype.card_subtype_compl,
    Fintype.card_ofSubsingleton, rank]
@[simp]
theorem unitLattice_rank :
    finrank â„¤ (unitLattice K) = Units.rank K := by
  rw [â† Units.finrank_eq_rank, ZLattice.rank â„]
def logEmbeddingQuot :
    Additive ((ğ“ K)Ë£ â§¸ (torsion K)) â†’+ ({w : InfinitePlace K // w â‰  wâ‚€} â†’ â„) :=
  MonoidHom.toAdditive' <|
    (QuotientGroup.kerLift (AddMonoidHom.toMultiplicative' (logEmbedding K))).comp
      (QuotientGroup.quotientMulEquivOfEq (by
        ext
        rw [MonoidHom.mem_ker, AddMonoidHom.toMultiplicative'_apply_apply, ofAdd_eq_one,
          â† logEmbedding_eq_zero_iff])).toMonoidHom
@[simp]
theorem logEmbeddingQuot_apply (x : (ğ“ K)Ë£) :
    logEmbeddingQuot K (Additive.ofMul (QuotientGroup.mk x)) =
      logEmbedding K (Additive.ofMul x) := rfl
theorem logEmbeddingQuot_injective :
    Function.Injective (logEmbeddingQuot K) := by
  unfold logEmbeddingQuot
  intro _ _ h
  simp_rw [MonoidHom.toAdditive'_apply_apply, MonoidHom.coe_comp, MulEquiv.coe_toMonoidHom,
    Function.comp_apply, EmbeddingLike.apply_eq_iff_eq] at h
  exact (EmbeddingLike.apply_eq_iff_eq _).mp <| (QuotientGroup.kerLift_injective _).eq_iff.mp h
#adaptation_note
set_option maxSynthPendingDepth 2 
def logEmbeddingEquiv :
    Additive ((ğ“ K)Ë£ â§¸ (torsion K)) â‰ƒâ‚—[â„¤] (unitLattice K) :=
  LinearEquiv.ofBijective ((logEmbeddingQuot K).codRestrict (unitLattice K)
    (Quotient.ind fun _ â†¦ logEmbeddingQuot_apply K _ â–¸
      Submodule.mem_map_of_mem trivial)).toIntLinearMap
    âŸ¨fun _ _ â†¦ by
      rw [AddMonoidHom.coe_toIntLinearMap, AddMonoidHom.codRestrict_apply,
        AddMonoidHom.codRestrict_apply, Subtype.mk.injEq]
      apply logEmbeddingQuot_injective K, fun âŸ¨a, âŸ¨b, _, haâŸ©âŸ© â†¦ âŸ¨âŸ¦bâŸ§, by simpa using haâŸ©âŸ©
@[simp]
theorem logEmbeddingEquiv_apply (x : (ğ“ K)Ë£) :
    logEmbeddingEquiv K (Additive.ofMul (QuotientGroup.mk x)) =
      logEmbedding K (Additive.ofMul x) := rfl
instance : Module.Free â„¤ (Additive ((ğ“ K)Ë£ â§¸ (torsion K))) :=
  Module.Free.of_equiv (logEmbeddingEquiv K).symm
instance : Module.Finite â„¤ (Additive ((ğ“ K)Ë£ â§¸ (torsion K))) :=
  Module.Finite.equiv (logEmbeddingEquiv K).symm
instance : Module.Finite â„¤ (Additive (ğ“ K)Ë£) := by
  rw [Module.finite_def]
  refine Submodule.fg_of_fg_map_of_fg_inf_ker
    (MonoidHom.toAdditive (QuotientGroup.mk' (torsion K))).toIntLinearMap ?_ ?_
  Â· rw [Submodule.map_top, LinearMap.range_eq_top.mpr
      (by exact QuotientGroup.mk'_surjective (torsion K)), â† Module.finite_def]
    infer_instance
  Â· rw [inf_of_le_right le_top, AddMonoidHom.coe_toIntLinearMap_ker, MonoidHom.coe_toAdditive_ker,
      QuotientGroup.ker_mk', Submodule.fg_iff_add_subgroup_fg,
      AddSubgroup.toIntSubmodule_toAddSubgroup, â† AddGroup.fg_iff_addSubgroup_fg]
    have : Finite (Subgroup.toAddSubgroup (torsion K)) := (inferInstance : Finite (torsion K))
    exact AddGroup.fg_of_finite
instance : Monoid.FG (ğ“ K)Ë£ := by
  rw [Monoid.fg_iff_add_fg, â† AddGroup.fg_iff_addMonoid_fg, â† Module.Finite.iff_addGroup_fg]
  infer_instance
theorem rank_modTorsion :
    Module.finrank â„¤ (Additive ((ğ“ K)Ë£ â§¸ (torsion K))) = rank K := by
  rw [â† LinearEquiv.finrank_eq (logEmbeddingEquiv K).symm, unitLattice_rank]
def basisModTorsion : Basis (Fin (rank K)) â„¤ (Additive ((ğ“ K)Ë£ â§¸ (torsion K))) :=
  Basis.reindex (Module.Free.chooseBasis â„¤ _) (Fintype.equivOfCardEq <| by
    rw [â† Module.finrank_eq_card_chooseBasisIndex, rank_modTorsion, Fintype.card_fin])
def basisUnitLattice : Basis (Fin (rank K)) â„¤ (unitLattice K) :=
  (basisModTorsion K).map (logEmbeddingEquiv K)
def fundSystem : Fin (rank K) â†’ (ğ“ K)Ë£ :=
  fun i => Quotient.out ((basisModTorsion K i).toMul:)
theorem fundSystem_mk (i : Fin (rank K)) :
    Additive.ofMul (QuotientGroup.mk (fundSystem K i)) = (basisModTorsion K i) := by
  simp_rw [fundSystem, Equiv.apply_eq_iff_eq_symm_apply, Additive.ofMul_symm_eq, Quotient.out_eq']
theorem logEmbedding_fundSystem (i : Fin (rank K)) :
    logEmbedding K (Additive.ofMul (fundSystem K i)) = basisUnitLattice K i := by
  rw [basisUnitLattice, Basis.map_apply, â† fundSystem_mk, logEmbeddingEquiv_apply]
theorem fun_eq_repr {x Î¶ : (ğ“ K)Ë£} {f : Fin (rank K) â†’ â„¤} (hÎ¶ : Î¶ âˆˆ torsion K)
    (h : x = Î¶ * âˆ i, (fundSystem K i) ^ (f i)) :
    f = (basisModTorsion K).repr (Additive.ofMul â†‘x) := by
  suffices Additive.ofMul â†‘x = âˆ‘ i, (f i) â€¢ (basisModTorsion K i) by
    rw [â† (basisModTorsion K).repr_sum_self f, â† this]
  calc
    Additive.ofMul â†‘x
    _ = âˆ‘ i, (f i) â€¢ Additive.ofMul â†‘(fundSystem K i) := by
          rw [h, QuotientGroup.mk_mul, (QuotientGroup.eq_one_iff _).mpr hÎ¶, one_mul,
            QuotientGroup.mk_prod, ofMul_prod]; rfl
    _ = âˆ‘ i, (f i) â€¢ (basisModTorsion K i) := by
          simp_rw [fundSystem, QuotientGroup.out_eq', ofMul_toMul]
theorem exist_unique_eq_mul_prod (x : (ğ“ K)Ë£) : âˆƒ! Î¶e : torsion K Ã— (Fin (rank K) â†’ â„¤),
    x = Î¶e.1 * âˆ i, (fundSystem K i) ^ (Î¶e.2 i) := by
  let Î¶ := x * (âˆ i, (fundSystem K i) ^ ((basisModTorsion K).repr (Additive.ofMul â†‘x) i))â»Â¹
  have h_tors : Î¶ âˆˆ torsion K := by
    rw [â† QuotientGroup.eq_one_iff, QuotientGroup.mk_mul, QuotientGroup.mk_inv, â† ofMul_eq_zero,
      ofMul_mul, ofMul_inv, QuotientGroup.mk_prod, ofMul_prod]
    simp_rw [QuotientGroup.mk_zpow, ofMul_zpow, fundSystem, QuotientGroup.out_eq']
    rw [add_eq_zero_iff_eq_neg, neg_neg]
    exact ((basisModTorsion K).sum_repr (Additive.ofMul â†‘x)).symm
  refine âŸ¨âŸ¨âŸ¨Î¶, h_torsâŸ©, ((basisModTorsion K).repr (Additive.ofMul â†‘x) : Fin (rank K) â†’ â„¤)âŸ©, ?_, ?_âŸ©
  Â· simp only [Î¶, _root_.inv_mul_cancel_right]
  Â· rintro âŸ¨âŸ¨Î¶', h_tors'âŸ©, Î·âŸ© hf
    simp only [Î¶, â† fun_eq_repr K h_tors' hf, Prod.mk.injEq, Subtype.mk.injEq, and_true]
    nth_rewrite 1 [hf]
    rw [_root_.mul_inv_cancel_right]
end statements
end NumberField.Units