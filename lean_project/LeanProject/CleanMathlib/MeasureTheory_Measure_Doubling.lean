import Mathlib.Analysis.SpecialFunctions.Log.Base
import Mathlib.MeasureTheory.Measure.MeasureSpaceDef
noncomputable section
open Set Filter Metric MeasureTheory TopologicalSpace ENNReal NNReal Topology
class IsUnifLocDoublingMeasure {Œ± : Type*} [PseudoMetricSpace Œ±] [MeasurableSpace Œ±]
  (Œº : Measure Œ±) : Prop where
  exists_measure_closedBall_le_mul'' :
    ‚àÉ C : ‚Ñù‚â•0, ‚àÄ·∂† Œµ in ùìù[>] 0, ‚àÄ x, Œº (closedBall x (2 * Œµ)) ‚â§ C * Œº (closedBall x Œµ)
namespace IsUnifLocDoublingMeasure
variable {Œ± : Type*} [PseudoMetricSpace Œ±] [MeasurableSpace Œ±] (Œº : Measure Œ±)
  [IsUnifLocDoublingMeasure Œº]
theorem exists_measure_closedBall_le_mul :
    ‚àÉ C : ‚Ñù‚â•0, ‚àÄ·∂† Œµ in ùìù[>] 0, ‚àÄ x, Œº (closedBall x (2 * Œµ)) ‚â§ C * Œº (closedBall x Œµ) :=
  exists_measure_closedBall_le_mul''
def doublingConstant : ‚Ñù‚â•0 :=
  Classical.choose <| exists_measure_closedBall_le_mul Œº
theorem exists_measure_closedBall_le_mul' :
    ‚àÄ·∂† Œµ in ùìù[>] 0, ‚àÄ x, Œº (closedBall x (2 * Œµ)) ‚â§ doublingConstant Œº * Œº (closedBall x Œµ) :=
  Classical.choose_spec <| exists_measure_closedBall_le_mul Œº
theorem exists_eventually_forall_measure_closedBall_le_mul (K : ‚Ñù) :
    ‚àÉ C : ‚Ñù‚â•0, ‚àÄ·∂† Œµ in ùìù[>] 0, ‚àÄ x, ‚àÄ t ‚â§ K, Œº (closedBall x (t * Œµ)) ‚â§ C * Œº (closedBall x Œµ) := by
  let C := doublingConstant Œº
  have hŒº :
    ‚àÄ n : ‚Ñï, ‚àÄ·∂† Œµ in ùìù[>] 0, ‚àÄ x,
      Œº (closedBall x ((2 : ‚Ñù) ^ n * Œµ)) ‚â§ ‚Üë(C ^ n) * Œº (closedBall x Œµ) := by
    intro n
    induction' n with n ih
    ¬∑ simp
    replace ih := eventually_nhdsWithin_pos_mul_left (two_pos : 0 < (2 : ‚Ñù)) ih
    refine (ih.and (exists_measure_closedBall_le_mul' Œº)).mono fun Œµ hŒµ x => ?_
    calc
      Œº (closedBall x ((2 : ‚Ñù) ^ (n + 1) * Œµ)) = Œº (closedBall x ((2 : ‚Ñù) ^ n * (2 * Œµ))) := by
        rw [pow_succ, mul_assoc]
      _ ‚â§ ‚Üë(C ^ n) * Œº (closedBall x (2 * Œµ)) := hŒµ.1 x
      _ ‚â§ ‚Üë(C ^ n) * (C * Œº (closedBall x Œµ)) := by gcongr; exact hŒµ.2 x
      _ = ‚Üë(C ^ (n + 1)) * Œº (closedBall x Œµ) := by rw [‚Üê mul_assoc, pow_succ, ENNReal.coe_mul]
  rcases lt_or_le K 1 with (hK | hK)
  ¬∑ refine ‚ü®1, ?_‚ü©
    simp only [ENNReal.coe_one, one_mul]
    refine eventually_mem_nhdsWithin.mono fun Œµ hŒµ x t ht ‚Ü¶ ?_
    gcongr
    nlinarith [mem_Ioi.mp hŒµ]
  ¬∑ use C ^ ‚åàReal.logb 2 K‚åâ‚Çä
    filter_upwards [hŒº ‚åàReal.logb 2 K‚åâ‚Çä, eventually_mem_nhdsWithin] with Œµ hŒµ hŒµ‚ÇÄ x t ht
    refine le_trans ?_ (hŒµ x)
    gcongr
    ¬∑ exact (mem_Ioi.mp hŒµ‚ÇÄ).le
    ¬∑ refine ht.trans ?_
      rw [‚Üê Real.rpow_natCast, ‚Üê Real.logb_le_iff_le_rpow]
      exacts [Nat.le_ceil _, by norm_num, by linarith]
def scalingConstantOf (K : ‚Ñù) : ‚Ñù‚â•0 :=
  max (Classical.choose <| exists_eventually_forall_measure_closedBall_le_mul Œº K) 1
@[simp]
theorem one_le_scalingConstantOf (K : ‚Ñù) : 1 ‚â§ scalingConstantOf Œº K :=
  le_max_of_le_right <| le_refl 1
theorem eventually_measure_mul_le_scalingConstantOf_mul (K : ‚Ñù) :
    ‚àÉ R : ‚Ñù,
      0 < R ‚àß
        ‚àÄ x t r, t ‚àà Ioc 0 K ‚Üí r ‚â§ R ‚Üí
          Œº (closedBall x (t * r)) ‚â§ scalingConstantOf Œº K * Œº (closedBall x r) := by
  have h := Classical.choose_spec (exists_eventually_forall_measure_closedBall_le_mul Œº K)
  rcases mem_nhdsWithin_Ioi_iff_exists_Ioc_subset.1 h with ‚ü®R, Rpos, hR‚ü©
  refine ‚ü®R, Rpos, fun x t r ht hr => ?_‚ü©
  rcases lt_trichotomy r 0 with (rneg | rfl | rpos)
  ¬∑ have : t * r < 0 := mul_neg_of_pos_of_neg ht.1 rneg
    simp only [closedBall_eq_empty.2 this, measure_empty, zero_le']
  ¬∑ simp only [mul_zero, closedBall_zero]
    refine le_mul_of_one_le_of_le ?_ le_rfl
    apply ENNReal.one_le_coe_iff.2 (le_max_right _ _)
  ¬∑ apply (hR ‚ü®rpos, hr‚ü© x t ht.2).trans
    gcongr
    apply le_max_left
theorem eventually_measure_le_scaling_constant_mul (K : ‚Ñù) :
    ‚àÄ·∂† r in ùìù[>] 0, ‚àÄ x, Œº (closedBall x (K * r)) ‚â§ scalingConstantOf Œº K * Œº (closedBall x r) := by
  filter_upwards [Classical.choose_spec
      (exists_eventually_forall_measure_closedBall_le_mul Œº K)] with r hr x
  exact (hr x K le_rfl).trans (mul_le_mul_right' (ENNReal.coe_le_coe.2 (le_max_left _ _)) _)
theorem eventually_measure_le_scaling_constant_mul' (K : ‚Ñù) (hK : 0 < K) :
    ‚àÄ·∂† r in ùìù[>] 0, ‚àÄ x,
      Œº (closedBall x r) ‚â§ scalingConstantOf Œº K‚Åª¬π * Œº (closedBall x (K * r)) := by
  convert eventually_nhdsWithin_pos_mul_left hK (eventually_measure_le_scaling_constant_mul Œº K‚Åª¬π)
  simp [inv_mul_cancel_left‚ÇÄ hK.ne']
def scalingScaleOf (K : ‚Ñù) : ‚Ñù :=
  (eventually_measure_mul_le_scalingConstantOf_mul Œº K).choose
theorem scalingScaleOf_pos (K : ‚Ñù) : 0 < scalingScaleOf Œº K :=
  (eventually_measure_mul_le_scalingConstantOf_mul Œº K).choose_spec.1
theorem measure_mul_le_scalingConstantOf_mul {K : ‚Ñù} {x : Œ±} {t r : ‚Ñù} (ht : t ‚àà Ioc 0 K)
    (hr : r ‚â§ scalingScaleOf Œº K) :
    Œº (closedBall x (t * r)) ‚â§ scalingConstantOf Œº K * Œº (closedBall x r) :=
  (eventually_measure_mul_le_scalingConstantOf_mul Œº K).choose_spec.2 x t r ht hr
end IsUnifLocDoublingMeasure