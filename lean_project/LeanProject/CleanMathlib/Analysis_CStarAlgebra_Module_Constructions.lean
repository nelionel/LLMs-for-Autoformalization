import Mathlib.Analysis.CStarAlgebra.Module.Defs
import Mathlib.Analysis.CStarAlgebra.Module.Synonym
import Mathlib.Topology.MetricSpace.Bilipschitz
open CStarModule CStarRing
namespace WithCStarModule
variable {A : Type*} [NonUnitalCStarAlgebra A] [PartialOrder A]
section Self
variable [StarOrderedRing A]
instance : CStarModule A A where
  inner x y := star x * y
  inner_add_right := mul_add ..
  inner_self_nonneg := star_mul_self_nonneg _
  inner_self := CStarRing.star_mul_self_eq_zero_iff _
  inner_op_smul_right := mul_assoc .. |>.symm
  inner_smul_right_complex := mul_smul_comm ..
  star_inner x y := by simp
  norm_eq_sqrt_norm_inner_self {x} := by
    rw [‚Üê sq_eq_sq‚ÇÄ (norm_nonneg _) (by positivity)]
    simpa [sq] using Eq.symm <| CStarRing.norm_star_mul_self
open scoped InnerProductSpace in
lemma inner_def (x y : A) : ‚ü™x, y‚ü´_A = star x * y := rfl
end Self
section Prod
open scoped InnerProductSpace
variable {E F : Type*}
variable [NormedAddCommGroup E] [Module ‚ÑÇ E] [SMul A·µê·µí·µñ E]
variable [NormedAddCommGroup F] [Module ‚ÑÇ F] [SMul A·µê·µí·µñ F]
variable [CStarModule A E] [CStarModule A F]
noncomputable instance : Norm (C‚ãÜ·µê·µí·µà (E √ó F)) where
  norm x := ‚àö‚Äñ‚ü™x.1, x.1‚ü´_A + ‚ü™x.2, x.2‚ü´_A‚Äñ
lemma prod_norm (x : C‚ãÜ·µê·µí·µà (E √ó F)) : ‚Äñx‚Äñ = ‚àö‚Äñ‚ü™x.1, x.1‚ü´_A + ‚ü™x.2, x.2‚ü´_A‚Äñ := rfl
lemma prod_norm_sq (x : C‚ãÜ·µê·µí·µà (E √ó F)) : ‚Äñx‚Äñ ^ 2 = ‚Äñ‚ü™x.1, x.1‚ü´_A + ‚ü™x.2, x.2‚ü´_A‚Äñ := by
  simp [prod_norm]
lemma prod_norm_le_norm_add (x : C‚ãÜ·µê·µí·µà (E √ó F)) : ‚Äñx‚Äñ ‚â§ ‚Äñx.1‚Äñ + ‚Äñx.2‚Äñ := by
  refine abs_le_of_sq_le_sq' ?_ (by positivity) |>.2
  calc ‚Äñx‚Äñ ^ 2 ‚â§ ‚Äñ‚ü™x.1, x.1‚ü´_A‚Äñ + ‚Äñ‚ü™x.2, x.2‚ü´_A‚Äñ := prod_norm_sq x ‚ñ∏ norm_add_le _ _
    _ = ‚Äñx.1‚Äñ ^ 2 + 0 + ‚Äñx.2‚Äñ ^ 2 := by simp [norm_sq_eq]
    _ ‚â§ ‚Äñx.1‚Äñ ^ 2 + 2 * ‚Äñx.1‚Äñ * ‚Äñx.2‚Äñ + ‚Äñx.2‚Äñ ^ 2 := by gcongr; positivity
    _ = (‚Äñx.1‚Äñ + ‚Äñx.2‚Äñ) ^ 2 := by ring
variable [StarOrderedRing A]
noncomputable instance : CStarModule A (C‚ãÜ·µê·µí·µà (E √ó F)) where
  inner x y := inner x.1 y.1 + inner x.2 y.2
  inner_add_right {x y z} := by simpa using add_add_add_comm ..
  inner_self_nonneg := add_nonneg CStarModule.inner_self_nonneg CStarModule.inner_self_nonneg
  inner_self {x} := by
    refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ by simp [h, CStarModule.inner_zero_left]‚ü©
    apply equiv (E √ó F) |>.injective
    ext
    ¬∑ refine inner_self.mp <| le_antisymm ?_ inner_self_nonneg
      exact le_add_of_nonneg_right CStarModule.inner_self_nonneg |>.trans_eq h
    ¬∑ refine inner_self.mp <| le_antisymm ?_ inner_self_nonneg
      exact le_add_of_nonneg_left CStarModule.inner_self_nonneg |>.trans_eq h
  inner_op_smul_right := by simp [add_mul]
  inner_smul_right_complex := by simp [smul_add]
  star_inner x y := by simp
  norm_eq_sqrt_norm_inner_self {x} := by with_reducible_and_instances rfl
lemma prod_inner (x y : C‚ãÜ·µê·µí·µà (E √ó F)) : ‚ü™x, y‚ü´_A = ‚ü™x.1, y.1‚ü´_A + ‚ü™x.2, y.2‚ü´_A := rfl
lemma max_le_prod_norm (x : C‚ãÜ·µê·µí·µà (E √ó F)) : max ‚Äñx.1‚Äñ ‚Äñx.2‚Äñ ‚â§ ‚Äñx‚Äñ := by
  rw [prod_norm]
  simp only [equiv_fst, norm_eq_sqrt_norm_inner_self (E := E),
    norm_eq_sqrt_norm_inner_self (E := F), equiv_snd, max_le_iff, norm_nonneg,
    Real.sqrt_le_sqrt_iff]
  constructor
  all_goals
    apply CStarAlgebra.norm_le_norm_of_nonneg_of_le
    all_goals
      aesop (add safe apply CStarModule.inner_self_nonneg)
lemma norm_equiv_le_norm_prod (x : C‚ãÜ·µê·µí·µà (E √ó F)) : ‚Äñequiv (E √ó F) x‚Äñ ‚â§ ‚Äñx‚Äñ :=
  max_le_prod_norm x
section Aux
attribute [-instance] WithCStarModule.instUniformSpace WithCStarModule.instBornology
attribute [local instance] CStarModule.normedAddCommGroup
open Filter Uniformity Bornology
private lemma antilipschitzWith_two_equiv_prod_aux : AntilipschitzWith 2 (equiv (E √ó F)) :=
  AddMonoidHomClass.antilipschitz_of_bound (linearEquiv ‚ÑÇ (E √ó F)) fun x ‚Ü¶ by
    apply prod_norm_le_norm_add x |>.trans
    simp only [NNReal.coe_ofNat, linearEquiv_apply, two_mul]
    gcongr
    ¬∑ exact norm_fst_le x
    ¬∑ exact norm_snd_le x
private lemma lipschitzWith_one_equiv_prod_aux : LipschitzWith 1 (equiv (E √ó F)) :=
  AddMonoidHomClass.lipschitz_of_bound_nnnorm (linearEquiv ‚ÑÇ (E √ó F)) 1 <| by
    simpa using norm_equiv_le_norm_prod
private lemma uniformity_prod_eq_aux :
    ùì§[(inferInstance : UniformSpace (E √ó F)).comap <| equiv _] = ùì§ (C‚ãÜ·µê·µí·µà (E √ó F)) :=
  uniformity_eq_of_bilipschitz antilipschitzWith_two_equiv_prod_aux lipschitzWith_one_equiv_prod_aux
private lemma isBounded_prod_iff_aux (s : Set (C‚ãÜ·µê·µí·µà (E √ó F))) :
    @IsBounded _ (induced <| equiv (E √ó F)) s ‚Üî IsBounded s :=
  isBounded_iff_of_bilipschitz antilipschitzWith_two_equiv_prod_aux
    lipschitzWith_one_equiv_prod_aux s
end Aux
noncomputable instance : NormedAddCommGroup (C‚ãÜ·µê·µí·µà (E √ó F)) :=
  .ofCoreReplaceAll normedSpaceCore uniformity_prod_eq_aux isBounded_prod_iff_aux
instance : NormedSpace ‚ÑÇ (C‚ãÜ·µê·µí·µà (E √ó F)) := .ofCore normedSpaceCore
end Prod
section Pi
open scoped InnerProductSpace
variable {Œπ : Type*} {E : Œπ ‚Üí Type*} [Fintype Œπ]
variable [‚àÄ i, NormedAddCommGroup (E i)] [‚àÄ i, Module ‚ÑÇ (E i)] [‚àÄ i, SMul A·µê·µí·µñ (E i)]
variable [‚àÄ i, CStarModule A (E i)]
noncomputable instance : Norm (C‚ãÜ·µê·µí·µà (Œ† i, E i)) where
  norm x := ‚àö‚Äñ‚àë i, ‚ü™x i, x i‚ü´_A‚Äñ
lemma pi_norm (x : C‚ãÜ·µê·µí·µà (Œ† i, E i)) : ‚Äñx‚Äñ = ‚àö‚Äñ‚àë i, ‚ü™x i, x i‚ü´_A‚Äñ := by
  with_reducible_and_instances rfl
lemma pi_norm_sq (x : C‚ãÜ·µê·µí·µà (Œ† i, E i)) : ‚Äñx‚Äñ ^ 2 = ‚Äñ‚àë i, ‚ü™x i, x i‚ü´_A‚Äñ := by
  simp [pi_norm]
open Finset in
lemma pi_norm_le_sum_norm (x : C‚ãÜ·µê·µí·µà (Œ† i, E i)) : ‚Äñx‚Äñ ‚â§ ‚àë i, ‚Äñx i‚Äñ := by
  refine abs_le_of_sq_le_sq' ?_ (by positivity) |>.2
  calc ‚Äñx‚Äñ ^ 2 ‚â§ ‚àë i, ‚Äñ‚ü™x i, x i‚ü´_A‚Äñ := pi_norm_sq x ‚ñ∏ norm_sum_le _ _
    _ = ‚àë i, ‚Äñx i‚Äñ ^ 2 := by simp only [norm_sq_eq]
    _ ‚â§ (‚àë i, ‚Äñx i‚Äñ) ^ 2 := sum_sq_le_sq_sum_of_nonneg (fun _ _ ‚Ü¶ norm_nonneg _)
variable [StarOrderedRing A]
open Finset in
noncomputable instance : CStarModule A (C‚ãÜ·µê·µí·µà (Œ† i, E i)) where
  inner x y := ‚àë i, inner (x i) (y i)
  inner_add_right {x y z} := by simp [inner_sum_right, sum_add_distrib]
  inner_self_nonneg := sum_nonneg <| fun _ _ ‚Ü¶ CStarModule.inner_self_nonneg
  inner_self {x} := by
    refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ by simp [h, CStarModule.inner_zero_left]‚ü©
    ext i
    refine inner_self.mp <| le_antisymm (le_of_le_of_eq ?_ h) inner_self_nonneg
    exact single_le_sum (fun i _ ‚Ü¶ CStarModule.inner_self_nonneg (x := x i)) (mem_univ _)
  inner_op_smul_right := by simp [sum_mul]
  inner_smul_right_complex := by simp [smul_sum]
  star_inner x y := by simp
  norm_eq_sqrt_norm_inner_self {x} := by with_reducible_and_instances rfl
lemma pi_inner (x y : C‚ãÜ·µê·µí·µà (Œ† i, E i)) : ‚ü™x, y‚ü´_A = ‚àë i, ‚ü™x i, y i‚ü´_A := rfl
@[simp]
lemma inner_single_left [DecidableEq Œπ] (x : C‚ãÜ·µê·µí·µà (Œ† i, E i)) {i : Œπ} (y : E i) :
    ‚ü™equiv _ |>.symm <| Pi.single i y, x‚ü´_A = ‚ü™y, x i‚ü´_A := by
  simp only [pi_inner, equiv_symm_pi_apply]
  rw [Finset.sum_eq_single i]
  all_goals simp_all
@[simp]
lemma inner_single_right [DecidableEq Œπ] (x : C‚ãÜ·µê·µí·µà (Œ† i, E i)) {i : Œπ} (y : E i) :
    ‚ü™x, equiv _ |>.symm <| Pi.single i y‚ü´_A = ‚ü™x i, y‚ü´_A := by
  simp only [pi_inner, equiv_symm_pi_apply]
  rw [Finset.sum_eq_single i]
  all_goals simp_all
@[simp]
lemma norm_single [DecidableEq Œπ] (i : Œπ) (y : E i) :
    ‚Äñequiv _ |>.symm <| Pi.single i y‚Äñ = ‚Äñy‚Äñ := by
  let _ : NormedAddCommGroup (C‚ãÜ·µê·µí·µà (Œ† i, E i)) := normedAddCommGroup
  rw [‚Üê sq_eq_sq‚ÇÄ (by positivity) (by positivity)]
  simp [norm_sq_eq]
lemma norm_apply_le_norm (x : C‚ãÜ·µê·µí·µà (Œ† i, E i)) (i : Œπ) : ‚Äñx i‚Äñ ‚â§ ‚Äñx‚Äñ := by
  let _ : NormedAddCommGroup (C‚ãÜ·µê·µí·µà (Œ† i, E i)) := normedAddCommGroup
  refine abs_le_of_sq_le_sq' ?_ (by positivity) |>.2
  rw [pi_norm_sq, norm_sq_eq]
  refine CStarAlgebra.norm_le_norm_of_nonneg_of_le inner_self_nonneg ?_
  exact Finset.single_le_sum (fun j _ ‚Ü¶ inner_self_nonneg (x := x j)) (Finset.mem_univ i)
open Finset in
lemma norm_equiv_le_norm_pi (x : C‚ãÜ·µê·µí·µà (Œ† i, E i)) : ‚Äñequiv _ x‚Äñ ‚â§ ‚Äñx‚Äñ := by
  let _ : NormedAddCommGroup (C‚ãÜ·µê·µí·µà (Œ† i, E i)) := normedAddCommGroup
  rw [pi_norm_le_iff_of_nonneg (by positivity)]
  simpa using norm_apply_le_norm x
section Aux
attribute [-instance] WithCStarModule.instUniformSpace WithCStarModule.instBornology
attribute [local instance] CStarModule.normedAddCommGroup
open Uniformity Bornology
private lemma antilipschitzWith_card_equiv_pi_aux :
    AntilipschitzWith (Fintype.card Œπ) (equiv (Œ† i, E i)) :=
  AddMonoidHomClass.antilipschitz_of_bound (linearEquiv ‚ÑÇ (Œ† i, E i)) fun x ‚Ü¶ by
    simp only [NNReal.coe_natCast, linearEquiv_apply]
    calc ‚Äñx‚Äñ ‚â§ ‚àë i, ‚Äñx i‚Äñ := pi_norm_le_sum_norm x
      _ ‚â§ ‚àë _, ‚Äñ‚áëx‚Äñ := Finset.sum_le_sum fun _ _ ‚Ü¶ norm_le_pi_norm ..
      _ ‚â§ Fintype.card Œπ * ‚Äñ‚áëx‚Äñ := by simp
private lemma lipschitzWith_one_equiv_pi_aux : LipschitzWith 1 (equiv (Œ† i, E i)) :=
  AddMonoidHomClass.lipschitz_of_bound_nnnorm (linearEquiv ‚ÑÇ (Œ† i, E i)) 1 <| by
    simpa using norm_equiv_le_norm_pi
private lemma uniformity_pi_eq_aux :
    ùì§[(inferInstance : UniformSpace (Œ† i, E i)).comap <| equiv _] = ùì§ (C‚ãÜ·µê·µí·µà (Œ† i, E i)) :=
  uniformity_eq_of_bilipschitz antilipschitzWith_card_equiv_pi_aux lipschitzWith_one_equiv_pi_aux
private lemma isBounded_pi_iff_aux (s : Set (C‚ãÜ·µê·µí·µà (Œ† i, E i))) :
    @IsBounded _ (induced <| equiv (Œ† i, E i)) s ‚Üî IsBounded s :=
  isBounded_iff_of_bilipschitz antilipschitzWith_card_equiv_pi_aux lipschitzWith_one_equiv_pi_aux s
end Aux
noncomputable instance : NormedAddCommGroup (C‚ãÜ·µê·µí·µà (Œ† i, E i)) :=
  .ofCoreReplaceAll normedSpaceCore uniformity_pi_eq_aux isBounded_pi_iff_aux
instance : NormedSpace ‚ÑÇ (C‚ãÜ·µê·µí·µà (Œ† i, E i)) := .ofCore normedSpaceCore
end Pi
section InnerProductSpace
open ComplexOrder
variable {E : Type*}
variable [NormedAddCommGroup E] [InnerProductSpace ‚ÑÇ E]
variable [instSMulOp : SMul ‚ÑÇ·µê·µí·µñ E] [instCentral : IsCentralScalar ‚ÑÇ E]
open scoped InnerProductSpace in
instance instCStarModuleComplex : CStarModule ‚ÑÇ E where
  inner x y := ‚ü™x, y‚ü´_‚ÑÇ
  inner_add_right := _root_.inner_add_right ..
  inner_self_nonneg {x} := by
    simp only
    rw [‚Üê inner_self_ofReal_re, RCLike.ofReal_nonneg]
    exact inner_self_nonneg
  inner_self := inner_self_eq_zero
  inner_op_smul_right := by simp [inner_smul_right, mul_comm]
  inner_smul_right_complex := inner_smul_right ..
  star_inner _ _ := inner_conj_symm ..
  norm_eq_sqrt_norm_inner_self {x} := by
    simpa only [‚Üê inner_self_re_eq_norm] using norm_eq_sqrt_inner x
example : instCStarModule (A := ‚ÑÇ) = instCStarModuleComplex := by with_reducible_and_instances rfl
example : (toInner : Inner ‚ÑÇ ‚ÑÇ) = WithCStarModule.instCStarModule.toInner := rfl
end InnerProductSpace
end WithCStarModule