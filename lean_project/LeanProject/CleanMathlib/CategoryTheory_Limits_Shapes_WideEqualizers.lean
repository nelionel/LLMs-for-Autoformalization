import Mathlib.CategoryTheory.Limits.HasLimits
import Mathlib.CategoryTheory.Limits.Shapes.Equalizers
noncomputable section
namespace CategoryTheory.Limits
open CategoryTheory
universe w v u u‚ÇÇ
variable {J : Type w}
inductive WalkingParallelFamily (J : Type w) : Type w
  | zero : WalkingParallelFamily J
  | one : WalkingParallelFamily J
open WalkingParallelFamily
instance : DecidableEq (WalkingParallelFamily J)
  | zero, zero => isTrue rfl
  | zero, one => isFalse fun t => WalkingParallelFamily.noConfusion t
  | one, zero => isFalse fun t => WalkingParallelFamily.noConfusion t
  | one, one => isTrue rfl
instance : Inhabited (WalkingParallelFamily J) :=
  ‚ü®zero‚ü©
inductive WalkingParallelFamily.Hom (J : Type w) :
  WalkingParallelFamily J ‚Üí WalkingParallelFamily J ‚Üí Type w
  | id : ‚àÄ X : WalkingParallelFamily.{w} J, WalkingParallelFamily.Hom J X X
  | line : J ‚Üí WalkingParallelFamily.Hom J zero one
  deriving DecidableEq
instance (J : Type v) : Inhabited (WalkingParallelFamily.Hom J zero zero) where default := Hom.id _
open WalkingParallelFamily.Hom
def WalkingParallelFamily.Hom.comp :
    ‚àÄ {X Y Z : WalkingParallelFamily J} (_ : WalkingParallelFamily.Hom J X Y)
      (_ : WalkingParallelFamily.Hom J Y Z), WalkingParallelFamily.Hom J X Z
  | _, _, _, id _, h => h
  | _, _, _, line j, id one => line j
instance WalkingParallelFamily.category : SmallCategory (WalkingParallelFamily J) where
  Hom := WalkingParallelFamily.Hom J
  id := WalkingParallelFamily.Hom.id
  comp := WalkingParallelFamily.Hom.comp
  assoc f g h := by cases f <;> cases g <;> cases h <;> aesop_cat
  comp_id f := by cases f <;> aesop_cat
@[simp]
theorem WalkingParallelFamily.hom_id (X : WalkingParallelFamily J) :
    WalkingParallelFamily.Hom.id X = ùüô X :=
  rfl
variable {C : Type u} [Category.{v} C]
variable {X Y : C} (f : J ‚Üí (X ‚ü∂ Y))
def parallelFamily : WalkingParallelFamily J ‚•§ C where
  obj x := WalkingParallelFamily.casesOn x X Y
  map {x y} h :=
    match x, y, h with
    | _, _, Hom.id _ => ùüô _
    | _, _, line j => f j
  map_comp := by
    rintro _ _ _ ‚ü®‚ü© ‚ü®‚ü© <;>
      ¬∑ aesop_cat
@[simp]
theorem parallelFamily_obj_zero : (parallelFamily f).obj zero = X :=
  rfl
@[simp]
theorem parallelFamily_obj_one : (parallelFamily f).obj one = Y :=
  rfl
@[simp]
theorem parallelFamily_map_left {j : J} : (parallelFamily f).map (line j) = f j :=
  rfl
@[simps!]
def diagramIsoParallelFamily (F : WalkingParallelFamily J ‚•§ C) :
    F ‚âÖ parallelFamily fun j => F.map (line j) :=
  NatIso.ofComponents (fun j => eqToIso <| by cases j <;> aesop_cat) <| by
    rintro _ _ (_|_) <;> aesop_cat
@[simps!]
def walkingParallelFamilyEquivWalkingParallelPair :
    WalkingParallelFamily.{w} (ULift Bool) ‚âå WalkingParallelPair where
  functor :=
    parallelFamily fun p => cond p.down WalkingParallelPairHom.left WalkingParallelPairHom.right
  inverse := parallelPair (line (ULift.up true)) (line (ULift.up false))
  unitIso := NatIso.ofComponents (fun X => eqToIso (by cases X <;> rfl)) (by
    rintro _ _ (_|‚ü®_|_‚ü©) <;> aesop_cat)
  counitIso := NatIso.ofComponents (fun X => eqToIso (by cases X <;> rfl)) (by
    rintro _ _ (_|_|_) <;> aesop_cat)
  functor_unitIso_comp := by rintro (_|_) <;> aesop_cat
abbrev Trident :=
  Cone (parallelFamily f)
abbrev Cotrident :=
  Cocone (parallelFamily f)
variable {f}
abbrev Trident.Œπ (t : Trident f) :=
  t.œÄ.app zero
abbrev Cotrident.œÄ (t : Cotrident f) :=
  t.Œπ.app one
@[simp]
theorem Trident.Œπ_eq_app_zero (t : Trident f) : t.Œπ = t.œÄ.app zero :=
  rfl
@[simp]
theorem Cotrident.œÄ_eq_app_one (t : Cotrident f) : t.œÄ = t.Œπ.app one :=
  rfl
@[reassoc (attr := simp)]
theorem Trident.app_zero (s : Trident f) (j : J) : s.œÄ.app zero ‚â´ f j = s.œÄ.app one := by
  rw [‚Üê s.w (line j), parallelFamily_map_left]
@[reassoc (attr := simp)]
theorem Cotrident.app_one (s : Cotrident f) (j : J) : f j ‚â´ s.Œπ.app one = s.Œπ.app zero := by
  rw [‚Üê s.w (line j), parallelFamily_map_left]
@[simps]
def Trident.ofŒπ [Nonempty J] {P : C} (Œπ : P ‚ü∂ X) (w : ‚àÄ j‚ÇÅ j‚ÇÇ, Œπ ‚â´ f j‚ÇÅ = Œπ ‚â´ f j‚ÇÇ) :
    Trident f where
  pt := P
  œÄ :=
    { app := fun X => WalkingParallelFamily.casesOn X Œπ (Œπ ‚â´ f (Classical.arbitrary J))
      naturality := fun i j f => by
        dsimp
        cases' f with _ k
        ¬∑ simp
        ¬∑ simp [w (Classical.arbitrary J) k] }
@[simps]
def Cotrident.ofœÄ [Nonempty J] {P : C} (œÄ : Y ‚ü∂ P) (w : ‚àÄ j‚ÇÅ j‚ÇÇ, f j‚ÇÅ ‚â´ œÄ = f j‚ÇÇ ‚â´ œÄ) :
    Cotrident f where
  pt := P
  Œπ :=
    { app := fun X => WalkingParallelFamily.casesOn X (f (Classical.arbitrary J) ‚â´ œÄ) œÄ
      naturality := fun i j f => by
        dsimp
        cases' f with _ k
        ¬∑ simp
        ¬∑ simp [w (Classical.arbitrary J) k] }
theorem Trident.Œπ_ofŒπ [Nonempty J] {P : C} (Œπ : P ‚ü∂ X) (w : ‚àÄ j‚ÇÅ j‚ÇÇ, Œπ ‚â´ f j‚ÇÅ = Œπ ‚â´ f j‚ÇÇ) :
    (Trident.ofŒπ Œπ w).Œπ = Œπ :=
  rfl
theorem Cotrident.œÄ_ofœÄ [Nonempty J] {P : C} (œÄ : Y ‚ü∂ P) (w : ‚àÄ j‚ÇÅ j‚ÇÇ, f j‚ÇÅ ‚â´ œÄ = f j‚ÇÇ ‚â´ œÄ) :
    (Cotrident.ofœÄ œÄ w).œÄ = œÄ :=
  rfl
@[reassoc]
theorem Trident.condition (j‚ÇÅ j‚ÇÇ : J) (t : Trident f) : t.Œπ ‚â´ f j‚ÇÅ = t.Œπ ‚â´ f j‚ÇÇ := by
  rw [t.app_zero, t.app_zero]
@[reassoc]
theorem Cotrident.condition (j‚ÇÅ j‚ÇÇ : J) (t : Cotrident f) : f j‚ÇÅ ‚â´ t.œÄ = f j‚ÇÇ ‚â´ t.œÄ := by
  rw [t.app_one, t.app_one]
theorem Trident.equalizer_ext [Nonempty J] (s : Trident f) {W : C} {k l : W ‚ü∂ s.pt}
    (h : k ‚â´ s.Œπ = l ‚â´ s.Œπ) : ‚àÄ j : WalkingParallelFamily J, k ‚â´ s.œÄ.app j = l ‚â´ s.œÄ.app j
  | zero => h
  | one => by rw [‚Üê s.app_zero (Classical.arbitrary J), reassoc_of% h]
theorem Cotrident.coequalizer_ext [Nonempty J] (s : Cotrident f) {W : C} {k l : s.pt ‚ü∂ W}
    (h : s.œÄ ‚â´ k = s.œÄ ‚â´ l) : ‚àÄ j : WalkingParallelFamily J, s.Œπ.app j ‚â´ k = s.Œπ.app j ‚â´ l
  | zero => by rw [‚Üê s.app_one (Classical.arbitrary J), Category.assoc, Category.assoc, h]
  | one => h
theorem Trident.IsLimit.hom_ext [Nonempty J] {s : Trident f} (hs : IsLimit s) {W : C}
    {k l : W ‚ü∂ s.pt} (h : k ‚â´ s.Œπ = l ‚â´ s.Œπ) : k = l :=
  hs.hom_ext <| Trident.equalizer_ext _ h
theorem Cotrident.IsColimit.hom_ext [Nonempty J] {s : Cotrident f} (hs : IsColimit s) {W : C}
    {k l : s.pt ‚ü∂ W} (h : s.œÄ ‚â´ k = s.œÄ ‚â´ l) : k = l :=
  hs.hom_ext <| Cotrident.coequalizer_ext _ h
def Trident.IsLimit.lift' [Nonempty J] {s : Trident f} (hs : IsLimit s) {W : C} (k : W ‚ü∂ X)
    (h : ‚àÄ j‚ÇÅ j‚ÇÇ, k ‚â´ f j‚ÇÅ = k ‚â´ f j‚ÇÇ) : { l : W ‚ü∂ s.pt // l ‚â´ Trident.Œπ s = k } :=
  ‚ü®hs.lift <| Trident.ofŒπ _ h, hs.fac _ _‚ü©
def Cotrident.IsColimit.desc' [Nonempty J] {s : Cotrident f} (hs : IsColimit s) {W : C} (k : Y ‚ü∂ W)
    (h : ‚àÄ j‚ÇÅ j‚ÇÇ, f j‚ÇÅ ‚â´ k = f j‚ÇÇ ‚â´ k) : { l : s.pt ‚ü∂ W // Cotrident.œÄ s ‚â´ l = k } :=
  ‚ü®hs.desc <| Cotrident.ofœÄ _ h, hs.fac _ _‚ü©
def Trident.IsLimit.mk [Nonempty J] (t : Trident f) (lift : ‚àÄ s : Trident f, s.pt ‚ü∂ t.pt)
    (fac : ‚àÄ s : Trident f, lift s ‚â´ t.Œπ = s.Œπ)
    (uniq :
      ‚àÄ (s : Trident f) (m : s.pt ‚ü∂ t.pt)
        (_ : ‚àÄ j : WalkingParallelFamily J, m ‚â´ t.œÄ.app j = s.œÄ.app j), m = lift s) :
    IsLimit t :=
  { lift
    fac := fun s j =>
      WalkingParallelFamily.casesOn j (fac s)
        (by rw [‚Üê t.w (line (Classical.arbitrary J)), reassoc_of% fac, s.w])
    uniq := uniq }
def Trident.IsLimit.mk' [Nonempty J] (t : Trident f)
    (create : ‚àÄ s : Trident f, { l // l ‚â´ t.Œπ = s.Œπ ‚àß ‚àÄ {m}, m ‚â´ t.Œπ = s.Œπ ‚Üí m = l }) :
    IsLimit t :=
  Trident.IsLimit.mk t (fun s => (create s).1) (fun s => (create s).2.1) fun s _ w =>
    (create s).2.2 (w zero)
def Cotrident.IsColimit.mk [Nonempty J] (t : Cotrident f) (desc : ‚àÄ s : Cotrident f, t.pt ‚ü∂ s.pt)
    (fac : ‚àÄ s : Cotrident f, t.œÄ ‚â´ desc s = s.œÄ)
    (uniq :
      ‚àÄ (s : Cotrident f) (m : t.pt ‚ü∂ s.pt)
        (_ : ‚àÄ j : WalkingParallelFamily J, t.Œπ.app j ‚â´ m = s.Œπ.app j), m = desc s) :
    IsColimit t :=
  { desc
    fac := fun s j =>
      WalkingParallelFamily.casesOn j (by rw [‚Üê t.w_assoc (line (Classical.arbitrary J)), fac, s.w])
        (fac s)
    uniq := uniq }
def Cotrident.IsColimit.mk' [Nonempty J] (t : Cotrident f)
    (create :
      ‚àÄ s : Cotrident f, { l : t.pt ‚ü∂ s.pt // t.œÄ ‚â´ l = s.œÄ ‚àß ‚àÄ {m}, t.œÄ ‚â´ m = s.œÄ ‚Üí m = l }) :
    IsColimit t :=
  Cotrident.IsColimit.mk t (fun s => (create s).1) (fun s => (create s).2.1) fun s _ w =>
    (create s).2.2 (w one)
@[simps]
def Trident.IsLimit.homIso [Nonempty J] {t : Trident f} (ht : IsLimit t) (Z : C) :
    (Z ‚ü∂ t.pt) ‚âÉ { h : Z ‚ü∂ X // ‚àÄ j‚ÇÅ j‚ÇÇ, h ‚â´ f j‚ÇÅ = h ‚â´ f j‚ÇÇ } where
  toFun k := ‚ü®k ‚â´ t.Œπ, by simp‚ü©
  invFun h := (Trident.IsLimit.lift' ht _ h.prop).1
  left_inv _ := Trident.IsLimit.hom_ext ht (Trident.IsLimit.lift' _ _ _).prop
  right_inv _ := Subtype.ext (Trident.IsLimit.lift' ht _ _).prop
theorem Trident.IsLimit.homIso_natural [Nonempty J] {t : Trident f} (ht : IsLimit t) {Z Z' : C}
    (q : Z' ‚ü∂ Z) (k : Z ‚ü∂ t.pt) :
    (Trident.IsLimit.homIso ht _ (q ‚â´ k) : Z' ‚ü∂ X) =
      q ‚â´ (Trident.IsLimit.homIso ht _ k : Z ‚ü∂ X) :=
  Category.assoc _ _ _
@[simps]
def Cotrident.IsColimit.homIso [Nonempty J] {t : Cotrident f} (ht : IsColimit t) (Z : C) :
    (t.pt ‚ü∂ Z) ‚âÉ { h : Y ‚ü∂ Z // ‚àÄ j‚ÇÅ j‚ÇÇ, f j‚ÇÅ ‚â´ h = f j‚ÇÇ ‚â´ h } where
  toFun k := ‚ü®t.œÄ ‚â´ k, by simp‚ü©
  invFun h := (Cotrident.IsColimit.desc' ht _ h.prop).1
  left_inv _ := Cotrident.IsColimit.hom_ext ht (Cotrident.IsColimit.desc' _ _ _).prop
  right_inv _ := Subtype.ext (Cotrident.IsColimit.desc' ht _ _).prop
theorem Cotrident.IsColimit.homIso_natural [Nonempty J] {t : Cotrident f} {Z Z' : C} (q : Z ‚ü∂ Z')
    (ht : IsColimit t) (k : t.pt ‚ü∂ Z) :
    (Cotrident.IsColimit.homIso ht _ (k ‚â´ q) : Y ‚ü∂ Z') =
      (Cotrident.IsColimit.homIso ht _ k : Y ‚ü∂ Z) ‚â´ q :=
  (Category.assoc _ _ _).symm
def Cone.ofTrident {F : WalkingParallelFamily J ‚•§ C} (t : Trident fun j => F.map (line j)) :
    Cone F where
  pt := t.pt
  œÄ :=
    { app := fun X => t.œÄ.app X ‚â´ eqToHom (by cases X <;> aesop_cat)
      naturality := fun j j' g => by cases g <;> aesop_cat }
def Cocone.ofCotrident {F : WalkingParallelFamily J ‚•§ C} (t : Cotrident fun j => F.map (line j)) :
    Cocone F where
  pt := t.pt
  Œπ :=
    { app := fun X => eqToHom (by cases X <;> aesop_cat) ‚â´ t.Œπ.app X
      naturality := fun j j' g => by cases g <;> dsimp <;> simp [Cotrident.app_one t] }
@[simp]
theorem Cone.ofTrident_œÄ {F : WalkingParallelFamily J ‚•§ C} (t : Trident fun j => F.map (line j))
    (j) : (Cone.ofTrident t).œÄ.app j = t.œÄ.app j ‚â´ eqToHom (by cases j <;> aesop_cat) :=
  rfl
@[simp]
theorem Cocone.ofCotrident_Œπ {F : WalkingParallelFamily J ‚•§ C}
    (t : Cotrident fun j => F.map (line j)) (j) :
    (Cocone.ofCotrident t).Œπ.app j = eqToHom (by cases j <;> aesop_cat) ‚â´ t.Œπ.app j :=
  rfl
def Trident.ofCone {F : WalkingParallelFamily J ‚•§ C} (t : Cone F) :
    Trident fun j => F.map (line j) where
  pt := t.pt
  œÄ :=
    { app := fun X => t.œÄ.app X ‚â´ eqToHom (by cases X <;> aesop_cat)
      naturality := by rintro _ _ (_|_) <;> aesop_cat }
def Cotrident.ofCocone {F : WalkingParallelFamily J ‚•§ C} (t : Cocone F) :
    Cotrident fun j => F.map (line j) where
  pt := t.pt
  Œπ :=
    { app := fun X => eqToHom (by cases X <;> aesop_cat) ‚â´ t.Œπ.app X
      naturality := by rintro _ _ (_|_) <;> aesop_cat }
@[simp]
theorem Trident.ofCone_œÄ {F : WalkingParallelFamily J ‚•§ C} (t : Cone F) (j) :
    (Trident.ofCone t).œÄ.app j = t.œÄ.app j ‚â´ eqToHom (by cases j <;> aesop_cat) :=
  rfl
@[simp]
theorem Cotrident.ofCocone_Œπ {F : WalkingParallelFamily J ‚•§ C} (t : Cocone F) (j) :
    (Cotrident.ofCocone t).Œπ.app j = eqToHom (by cases j <;> aesop_cat) ‚â´ t.Œπ.app j :=
  rfl
@[simps]
def Trident.mkHom [Nonempty J] {s t : Trident f} (k : s.pt ‚ü∂ t.pt)
    (w : k ‚â´ t.Œπ = s.Œπ := by aesop_cat) : s ‚ü∂ t where
  hom := k
  w := by
    rintro ‚ü®_ | _‚ü©
    ¬∑ exact w
    ¬∑ simpa using w =‚â´ f (Classical.arbitrary J)
@[simps]
def Trident.ext [Nonempty J] {s t : Trident f} (i : s.pt ‚âÖ t.pt)
    (w : i.hom ‚â´ t.Œπ = s.Œπ := by aesop_cat) : s ‚âÖ t where
  hom := Trident.mkHom i.hom w
  inv := Trident.mkHom i.inv (by rw [‚Üê w, Iso.inv_hom_id_assoc])
@[simps]
def Cotrident.mkHom [Nonempty J] {s t : Cotrident f} (k : s.pt ‚ü∂ t.pt)
    (w : s.œÄ ‚â´ k = t.œÄ := by aesop_cat) : s ‚ü∂ t where
  hom := k
  w := by
    rintro ‚ü®_ | _‚ü©
    ¬∑ simpa using f (Classical.arbitrary J) ‚â´= w
    ¬∑ exact w
def Cotrident.ext [Nonempty J] {s t : Cotrident f} (i : s.pt ‚âÖ t.pt)
    (w : s.œÄ ‚â´ i.hom = t.œÄ := by aesop_cat) : s ‚âÖ t where
  hom := Cotrident.mkHom i.hom w
  inv := Cotrident.mkHom i.inv (by rw [Iso.comp_inv_eq, w])
variable (f)
section
abbrev HasWideEqualizer :=
  HasLimit (parallelFamily f)
variable [HasWideEqualizer f]
abbrev wideEqualizer : C :=
  limit (parallelFamily f)
abbrev wideEqualizer.Œπ : wideEqualizer f ‚ü∂ X :=
  limit.œÄ (parallelFamily f) zero
abbrev wideEqualizer.trident : Trident f :=
  limit.cone (parallelFamily f)
@[simp]
theorem wideEqualizer.trident_Œπ : (wideEqualizer.trident f).Œπ = wideEqualizer.Œπ f :=
  rfl
@[simp 1100]
theorem wideEqualizer.trident_œÄ_app_zero :
    (wideEqualizer.trident f).œÄ.app zero = wideEqualizer.Œπ f :=
  rfl
@[reassoc]
theorem wideEqualizer.condition (j‚ÇÅ j‚ÇÇ : J) : wideEqualizer.Œπ f ‚â´ f j‚ÇÅ = wideEqualizer.Œπ f ‚â´ f j‚ÇÇ :=
  Trident.condition j‚ÇÅ j‚ÇÇ <| limit.cone <| parallelFamily f
def wideEqualizerIsWideEqualizer [Nonempty J] :
    IsLimit (Trident.ofŒπ (wideEqualizer.Œπ f) (wideEqualizer.condition f)) :=
  IsLimit.ofIsoLimit (limit.isLimit _) (Trident.ext (Iso.refl _))
variable {f}
abbrev wideEqualizer.lift [Nonempty J] {W : C} (k : W ‚ü∂ X) (h : ‚àÄ j‚ÇÅ j‚ÇÇ, k ‚â´ f j‚ÇÅ = k ‚â´ f j‚ÇÇ) :
    W ‚ü∂ wideEqualizer f :=
  limit.lift (parallelFamily f) (Trident.ofŒπ k h)
@[reassoc (attr := simp 1100)]
theorem wideEqualizer.lift_Œπ [Nonempty J] {W : C} (k : W ‚ü∂ X)
    (h : ‚àÄ j‚ÇÅ j‚ÇÇ, k ‚â´ f j‚ÇÅ = k ‚â´ f j‚ÇÇ) :
    wideEqualizer.lift k h ‚â´ wideEqualizer.Œπ f = k :=
  limit.lift_œÄ _ _
def wideEqualizer.lift' [Nonempty J] {W : C} (k : W ‚ü∂ X) (h : ‚àÄ j‚ÇÅ j‚ÇÇ, k ‚â´ f j‚ÇÅ = k ‚â´ f j‚ÇÇ) :
    { l : W ‚ü∂ wideEqualizer f // l ‚â´ wideEqualizer.Œπ f = k } :=
  ‚ü®wideEqualizer.lift k h, wideEqualizer.lift_Œπ _ _‚ü©
@[ext]
theorem wideEqualizer.hom_ext [Nonempty J] {W : C} {k l : W ‚ü∂ wideEqualizer f}
    (h : k ‚â´ wideEqualizer.Œπ f = l ‚â´ wideEqualizer.Œπ f) : k = l :=
  Trident.IsLimit.hom_ext (limit.isLimit _) h
instance wideEqualizer.Œπ_mono [Nonempty J] : Mono (wideEqualizer.Œπ f) where
  right_cancellation _ _ w := wideEqualizer.hom_ext w
end
section
variable {f}
theorem mono_of_isLimit_parallelFamily [Nonempty J] {c : Cone (parallelFamily f)} (i : IsLimit c) :
    Mono (Trident.Œπ c) where
  right_cancellation _ _ w := Trident.IsLimit.hom_ext i w
end
section
abbrev HasWideCoequalizer :=
  HasColimit (parallelFamily f)
variable [HasWideCoequalizer f]
abbrev wideCoequalizer : C :=
  colimit (parallelFamily f)
abbrev wideCoequalizer.œÄ : Y ‚ü∂ wideCoequalizer f :=
  colimit.Œπ (parallelFamily f) one
abbrev wideCoequalizer.cotrident : Cotrident f :=
  colimit.cocone (parallelFamily f)
@[simp]
theorem wideCoequalizer.cotrident_œÄ : (wideCoequalizer.cotrident f).œÄ = wideCoequalizer.œÄ f :=
  rfl
@[simp 1100]
theorem wideCoequalizer.cotrident_Œπ_app_one :
    (wideCoequalizer.cotrident f).Œπ.app one = wideCoequalizer.œÄ f :=
  rfl
@[reassoc]
theorem wideCoequalizer.condition (j‚ÇÅ j‚ÇÇ : J) :
    f j‚ÇÅ ‚â´ wideCoequalizer.œÄ f = f j‚ÇÇ ‚â´ wideCoequalizer.œÄ f :=
  Cotrident.condition j‚ÇÅ j‚ÇÇ <| colimit.cocone <| parallelFamily f
def wideCoequalizerIsWideCoequalizer [Nonempty J] :
    IsColimit (Cotrident.ofœÄ (wideCoequalizer.œÄ f) (wideCoequalizer.condition f)) :=
  IsColimit.ofIsoColimit (colimit.isColimit _) (Cotrident.ext (Iso.refl _))
variable {f}
abbrev wideCoequalizer.desc [Nonempty J] {W : C} (k : Y ‚ü∂ W) (h : ‚àÄ j‚ÇÅ j‚ÇÇ, f j‚ÇÅ ‚â´ k = f j‚ÇÇ ‚â´ k) :
    wideCoequalizer f ‚ü∂ W :=
  colimit.desc (parallelFamily f) (Cotrident.ofœÄ k h)
@[reassoc (attr := simp 1100)]
theorem wideCoequalizer.œÄ_desc [Nonempty J] {W : C} (k : Y ‚ü∂ W)
    (h : ‚àÄ j‚ÇÅ j‚ÇÇ, f j‚ÇÅ ‚â´ k = f j‚ÇÇ ‚â´ k) :
    wideCoequalizer.œÄ f ‚â´ wideCoequalizer.desc k h = k :=
  colimit.Œπ_desc _ _
def wideCoequalizer.desc' [Nonempty J] {W : C} (k : Y ‚ü∂ W) (h : ‚àÄ j‚ÇÅ j‚ÇÇ, f j‚ÇÅ ‚â´ k = f j‚ÇÇ ‚â´ k) :
    { l : wideCoequalizer f ‚ü∂ W // wideCoequalizer.œÄ f ‚â´ l = k } :=
  ‚ü®wideCoequalizer.desc k h, wideCoequalizer.œÄ_desc _ _‚ü©
@[ext]
theorem wideCoequalizer.hom_ext [Nonempty J] {W : C} {k l : wideCoequalizer f ‚ü∂ W}
    (h : wideCoequalizer.œÄ f ‚â´ k = wideCoequalizer.œÄ f ‚â´ l) : k = l :=
  Cotrident.IsColimit.hom_ext (colimit.isColimit _) h
instance wideCoequalizer.œÄ_epi [Nonempty J] : Epi (wideCoequalizer.œÄ f) where
  left_cancellation _ _ w := wideCoequalizer.hom_ext w
end
section
variable {f}
theorem epi_of_isColimit_parallelFamily [Nonempty J] {c : Cocone (parallelFamily f)}
    (i : IsColimit c) : Epi (c.Œπ.app one) where
  left_cancellation _ _ w := Cotrident.IsColimit.hom_ext i w
end
variable (C)
abbrev HasWideEqualizers :=
  ‚àÄ J, HasLimitsOfShape (WalkingParallelFamily.{w} J) C
abbrev HasWideCoequalizers :=
  ‚àÄ J, HasColimitsOfShape (WalkingParallelFamily.{w} J) C
theorem hasWideEqualizers_of_hasLimit_parallelFamily
    [‚àÄ {J : Type w} {X Y : C} {f : J ‚Üí (X ‚ü∂ Y)}, HasLimit (parallelFamily f)] :
    HasWideEqualizers.{w} C := fun _ =>
  { has_limit := fun F => hasLimitOfIso (diagramIsoParallelFamily F).symm }
theorem hasWideCoequalizers_of_hasColimit_parallelFamily
    [‚àÄ {J : Type w} {X Y : C} {f : J ‚Üí (X ‚ü∂ Y)}, HasColimit (parallelFamily f)] :
    HasWideCoequalizers.{w} C := fun _ =>
  { has_colimit := fun F => hasColimitOfIso (diagramIsoParallelFamily F) }
instance (priority := 10) hasEqualizers_of_hasWideEqualizers [HasWideEqualizers.{w} C] :
    HasEqualizers C :=
  hasLimitsOfShape_of_equivalence.{w} walkingParallelFamilyEquivWalkingParallelPair
instance (priority := 10) hasCoequalizers_of_hasWideCoequalizers [HasWideCoequalizers.{w} C] :
    HasCoequalizers C :=
  hasColimitsOfShape_of_equivalence.{w} walkingParallelFamilyEquivWalkingParallelPair
end CategoryTheory.Limits
attribute [nolint simpNF] CategoryTheory.Limits.WalkingParallelFamily.Hom.id.sizeOf_spec