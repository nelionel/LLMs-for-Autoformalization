import Mathlib.CategoryTheory.Limits.Shapes.Pullback.HasPullback
import Mathlib.CategoryTheory.Limits.Shapes.StrongEpi
import Mathlib.CategoryTheory.Limits.Shapes.Equalizers
import Mathlib.Lean.Expr.Basic
noncomputable section
namespace CategoryTheory
open CategoryTheory.Limits
universe v‚ÇÅ u‚ÇÅ u‚ÇÇ
variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]
variable {X Y : C}
class RegularMono (f : X ‚ü∂ Y) where
  Z : C 
  left : Y ‚ü∂ Z
  right : Y ‚ü∂ Z
  w : f ‚â´ left = f ‚â´ right := by aesop_cat
  isLimit : IsLimit (Fork.ofŒπ f w)
attribute [reassoc] RegularMono.w
instance (priority := 100) RegularMono.mono (f : X ‚ü∂ Y) [RegularMono f] : Mono f :=
  mono_of_isLimit_fork RegularMono.isLimit
instance equalizerRegular (g h : X ‚ü∂ Y) [HasLimit (parallelPair g h)] :
    RegularMono (equalizer.Œπ g h) where
  Z := Y
  left := g
  right := h
  w := equalizer.condition g h
  isLimit :=
    Fork.IsLimit.mk _ (fun s => limit.lift _ s) (by simp) fun s m w => by
      apply equalizer.hom_ext
      simp [‚Üê w]
instance (priority := 100) RegularMono.ofIsSplitMono (f : X ‚ü∂ Y) [IsSplitMono f] :
    RegularMono f where
  Z := Y
  left := ùüô Y
  right := retraction f ‚â´ f
  isLimit := isSplitMonoEqualizes f
def RegularMono.lift' {W : C} (f : X ‚ü∂ Y) [RegularMono f] (k : W ‚ü∂ Y)
    (h : k ‚â´ (RegularMono.left : Y ‚ü∂ @RegularMono.Z _ _ _ _ f _) = k ‚â´ RegularMono.right) :
    { l : W ‚ü∂ X // l ‚â´ f = k } :=
  Fork.IsLimit.lift' RegularMono.isLimit _ h
def regularOfIsPullbackSndOfRegular {P Q R S : C} {f : P ‚ü∂ Q} {g : P ‚ü∂ R} {h : Q ‚ü∂ S} {k : R ‚ü∂ S}
    [hr : RegularMono h] (comm : f ‚â´ h = g ‚â´ k) (t : IsLimit (PullbackCone.mk _ _ comm)) :
    RegularMono g where
  Z := hr.Z
  left := k ‚â´ hr.left
  right := k ‚â´ hr.right
  w := by
    repeat (rw [‚Üê Category.assoc, ‚Üê eq_whisker comm])
    simp only [Category.assoc, hr.w]
  isLimit := by
    apply Fork.IsLimit.mk' _ _
    intro s
    have l‚ÇÅ : (Fork.Œπ s ‚â´ k) ‚â´ RegularMono.left = (Fork.Œπ s ‚â´ k) ‚â´ hr.right := by
      rw [Category.assoc, s.condition, Category.assoc]
    obtain ‚ü®l, hl‚ü© := Fork.IsLimit.lift' hr.isLimit _ l‚ÇÅ
    obtain ‚ü®p, _, hp‚ÇÇ‚ü© := PullbackCone.IsLimit.lift' t _ _ hl
    refine ‚ü®p, hp‚ÇÇ, ?_‚ü©
    intro m w
    have z : m ‚â´ g = p ‚â´ g := w.trans hp‚ÇÇ.symm
    apply t.hom_ext
    apply (PullbackCone.mk f g comm).equalizer_ext
    ¬∑ erw [‚Üê cancel_mono h, Category.assoc, Category.assoc, comm]
      simp only [‚Üê Category.assoc, eq_whisker z]
    ¬∑ exact z
def regularOfIsPullbackFstOfRegular {P Q R S : C} {f : P ‚ü∂ Q} {g : P ‚ü∂ R} {h : Q ‚ü∂ S} {k : R ‚ü∂ S}
    [RegularMono k] (comm : f ‚â´ h = g ‚â´ k) (t : IsLimit (PullbackCone.mk _ _ comm)) :
    RegularMono f :=
  regularOfIsPullbackSndOfRegular comm.symm (PullbackCone.flipIsLimit t)
instance (priority := 100) strongMono_of_regularMono (f : X ‚ü∂ Y) [RegularMono f] : StrongMono f :=
  StrongMono.mk' (by
      intro A B z hz u v sq
      have : v ‚â´ (RegularMono.left : Y ‚ü∂ RegularMono.Z f) = v ‚â´ RegularMono.right := by
        apply (cancel_epi z).1
        repeat (rw [‚Üê Category.assoc, ‚Üê eq_whisker sq.w])
        simp only [Category.assoc, RegularMono.w]
      obtain ‚ü®t, ht‚ü© := RegularMono.lift' _ _ this
      refine CommSq.HasLift.mk' ‚ü®t, (cancel_mono f).1 ?_, ht‚ü©
      simp only [Arrow.mk_hom, Arrow.homMk'_left, Category.assoc, ht, sq.w])
theorem isIso_of_regularMono_of_epi (f : X ‚ü∂ Y) [RegularMono f] [Epi f] : IsIso f :=
  isIso_of_epi_of_strongMono _
section
variable (C)
class RegularMonoCategory where
  regularMonoOfMono : ‚àÄ {X Y : C} (f : X ‚ü∂ Y) [Mono f], RegularMono f
end
def regularMonoOfMono [RegularMonoCategory C] (f : X ‚ü∂ Y) [Mono f] : RegularMono f :=
  RegularMonoCategory.regularMonoOfMono _
instance (priority := 100) regularMonoCategoryOfSplitMonoCategory [SplitMonoCategory C] :
    RegularMonoCategory C where
  regularMonoOfMono f _ := by
    haveI := isSplitMono_of_mono f
    infer_instance
instance (priority := 100) strongMonoCategory_of_regularMonoCategory [RegularMonoCategory C] :
    StrongMonoCategory C where
  strongMono_of_mono f _ := by
    haveI := regularMonoOfMono f
    infer_instance
class RegularEpi (f : X ‚ü∂ Y) where
  W : C 
  (left right : W ‚ü∂ X)
  w : left ‚â´ f = right ‚â´ f := by aesop_cat
  isColimit : IsColimit (Cofork.ofœÄ f w)
attribute [reassoc] RegularEpi.w
instance (priority := 100) RegularEpi.epi (f : X ‚ü∂ Y) [RegularEpi f] : Epi f :=
  epi_of_isColimit_cofork RegularEpi.isColimit
instance coequalizerRegular (g h : X ‚ü∂ Y) [HasColimit (parallelPair g h)] :
    RegularEpi (coequalizer.œÄ g h) where
  W := X
  left := g
  right := h
  w := coequalizer.condition g h
  isColimit :=
    Cofork.IsColimit.mk _ (fun s => colimit.desc _ s) (by simp) fun s m w => by
      apply coequalizer.hom_ext
      simp [‚Üê w]
noncomputable def regularEpiOfKernelPair {B X : C} (f : X ‚ü∂ B) [HasPullback f f]
    (hc : IsColimit (Cofork.ofœÄ f pullback.condition)) : RegularEpi f where
  W := pullback f f
  left := pullback.fst f f
  right := pullback.snd f f
  w := pullback.condition
  isColimit := hc
instance (priority := 100) RegularEpi.ofSplitEpi (f : X ‚ü∂ Y) [IsSplitEpi f] : RegularEpi f where
  W := X
  left := ùüô X
  right := f ‚â´ section_ f
  isColimit := isSplitEpiCoequalizes f
def RegularEpi.desc' {W : C} (f : X ‚ü∂ Y) [RegularEpi f] (k : X ‚ü∂ W)
    (h : (RegularEpi.left : RegularEpi.W f ‚ü∂ X) ‚â´ k = RegularEpi.right ‚â´ k) :
    { l : Y ‚ü∂ W // f ‚â´ l = k } :=
  Cofork.IsColimit.desc' RegularEpi.isColimit _ h
def regularOfIsPushoutSndOfRegular {P Q R S : C} {f : P ‚ü∂ Q} {g : P ‚ü∂ R} {h : Q ‚ü∂ S} {k : R ‚ü∂ S}
    [gr : RegularEpi g] (comm : f ‚â´ h = g ‚â´ k) (t : IsColimit (PushoutCocone.mk _ _ comm)) :
    RegularEpi h where
  W := gr.W
  left := gr.left ‚â´ f
  right := gr.right ‚â´ f
  w := by rw [Category.assoc, Category.assoc, comm]; simp only [‚Üê Category.assoc, eq_whisker gr.w]
  isColimit := by
    apply Cofork.IsColimit.mk' _ _
    intro s
    have l‚ÇÅ : gr.left ‚â´ f ‚â´ s.œÄ = gr.right ‚â´ f ‚â´ s.œÄ := by
      rw [‚Üê Category.assoc, ‚Üê Category.assoc, s.condition]
    obtain ‚ü®l, hl‚ü© := Cofork.IsColimit.desc' gr.isColimit (f ‚â´ Cofork.œÄ s) l‚ÇÅ
    obtain ‚ü®p, hp‚ÇÅ, _‚ü© := PushoutCocone.IsColimit.desc' t _ _ hl.symm
    refine ‚ü®p, hp‚ÇÅ, ?_‚ü©
    intro m w
    have z := w.trans hp‚ÇÅ.symm
    apply t.hom_ext
    apply (PushoutCocone.mk _ _ comm).coequalizer_ext
    ¬∑ exact z
    ¬∑ erw [‚Üê cancel_epi g, ‚Üê Category.assoc, ‚Üê eq_whisker comm]
      erw [‚Üê Category.assoc, ‚Üê eq_whisker comm]
      dsimp at z; simp only [Category.assoc, z]
def regularOfIsPushoutFstOfRegular {P Q R S : C} {f : P ‚ü∂ Q} {g : P ‚ü∂ R} {h : Q ‚ü∂ S} {k : R ‚ü∂ S}
    [RegularEpi f] (comm : f ‚â´ h = g ‚â´ k) (t : IsColimit (PushoutCocone.mk _ _ comm)) :
    RegularEpi k :=
  regularOfIsPushoutSndOfRegular comm.symm (PushoutCocone.flipIsColimit t)
instance (priority := 100) strongEpi_of_regularEpi (f : X ‚ü∂ Y) [RegularEpi f] : StrongEpi f :=
  StrongEpi.mk'
    (by
      intro A B z hz u v sq
      have : (RegularEpi.left : RegularEpi.W f ‚ü∂ X) ‚â´ u = RegularEpi.right ‚â´ u := by
        apply (cancel_mono z).1
        simp only [Category.assoc, sq.w, RegularEpi.w_assoc]
      obtain ‚ü®t, ht‚ü© := RegularEpi.desc' f u this
      exact
        CommSq.HasLift.mk'
          ‚ü®t, ht,
            (cancel_epi f).1
              (by simp only [‚Üê Category.assoc, ht, ‚Üê sq.w, Arrow.mk_hom, Arrow.homMk'_right])‚ü©)
theorem isIso_of_regularEpi_of_mono (f : X ‚ü∂ Y) [RegularEpi f] [Mono f] : IsIso f :=
  isIso_of_mono_of_strongEpi _
section
variable (C)
class RegularEpiCategory where
  regularEpiOfEpi : ‚àÄ {X Y : C} (f : X ‚ü∂ Y) [Epi f], RegularEpi f
end
def regularEpiOfEpi [RegularEpiCategory C] (f : X ‚ü∂ Y) [Epi f] : RegularEpi f :=
  RegularEpiCategory.regularEpiOfEpi _
instance (priority := 100) regularEpiCategoryOfSplitEpiCategory [SplitEpiCategory C] :
    RegularEpiCategory C where
  regularEpiOfEpi f _ := by
    haveI := isSplitEpi_of_epi f
    infer_instance
instance (priority := 100) strongEpiCategory_of_regularEpiCategory [RegularEpiCategory C] :
    StrongEpiCategory C where
  strongEpi_of_epi f _ := by
    haveI := regularEpiOfEpi f
    infer_instance
end CategoryTheory