import Mathlib.CategoryTheory.Sites.Sieves
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.Mono
universe w vâ‚ vâ‚‚ uâ‚ uâ‚‚
namespace CategoryTheory
open Opposite CategoryTheory Category Limits Sieve
namespace Presieve
variable {C : Type uâ‚} [Category.{vâ‚} C]
variable {P Q U : Cáµ’áµ– â¥¤ Type w}
variable {X Y : C} {S : Sieve X} {R : Presieve X}
def FamilyOfElements (P : Cáµ’áµ– â¥¤ Type w) (R : Presieve X) :=
  âˆ€ â¦ƒY : Câ¦„ (f : Y âŸ¶ X), R f â†’ P.obj (op Y)
instance : Inhabited (FamilyOfElements P (âŠ¥ : Presieve X)) :=
  âŸ¨fun _ _ => False.elimâŸ©
def FamilyOfElements.restrict {Râ‚ Râ‚‚ : Presieve X} (h : Râ‚ â‰¤ Râ‚‚) :
    FamilyOfElements P Râ‚‚ â†’ FamilyOfElements P Râ‚ := fun x _ f hf => x f (h _ hf)
def FamilyOfElements.map (p : FamilyOfElements P R) (Ï† : P âŸ¶ Q) :
    FamilyOfElements Q R :=
  fun _ f hf => Ï†.app _ (p f hf)
@[simp]
lemma FamilyOfElements.map_apply
    (p : FamilyOfElements P R) (Ï† : P âŸ¶ Q) {Y : C} (f : Y âŸ¶ X) (hf : R f) :
    p.map Ï† f hf = Ï†.app _ (p f hf) := rfl
lemma FamilyOfElements.restrict_map
    (p : FamilyOfElements P R) (Ï† : P âŸ¶ Q) {R' : Presieve X} (h : R' â‰¤ R) :
    (p.restrict h).map Ï† = (p.map Ï†).restrict h := rfl
def FamilyOfElements.Compatible (x : FamilyOfElements P R) : Prop :=
  âˆ€ â¦ƒYâ‚ Yâ‚‚ Zâ¦„ (gâ‚ : Z âŸ¶ Yâ‚) (gâ‚‚ : Z âŸ¶ Yâ‚‚) â¦ƒfâ‚ : Yâ‚ âŸ¶ Xâ¦„ â¦ƒfâ‚‚ : Yâ‚‚ âŸ¶ Xâ¦„ (hâ‚ : R fâ‚) (hâ‚‚ : R fâ‚‚),
    gâ‚ â‰« fâ‚ = gâ‚‚ â‰« fâ‚‚ â†’ P.map gâ‚.op (x fâ‚ hâ‚) = P.map gâ‚‚.op (x fâ‚‚ hâ‚‚)
def FamilyOfElements.PullbackCompatible (x : FamilyOfElements P R) [R.hasPullbacks] : Prop :=
  âˆ€ â¦ƒYâ‚ Yâ‚‚â¦„ â¦ƒfâ‚ : Yâ‚ âŸ¶ Xâ¦„ â¦ƒfâ‚‚ : Yâ‚‚ âŸ¶ Xâ¦„ (hâ‚ : R fâ‚) (hâ‚‚ : R fâ‚‚),
    haveI := hasPullbacks.has_pullbacks hâ‚ hâ‚‚
    P.map (pullback.fst fâ‚ fâ‚‚).op (x fâ‚ hâ‚) = P.map (pullback.snd fâ‚ fâ‚‚).op (x fâ‚‚ hâ‚‚)
theorem pullbackCompatible_iff (x : FamilyOfElements P R) [R.hasPullbacks] :
    x.Compatible â†” x.PullbackCompatible := by
  constructor
  Â· intro t Yâ‚ Yâ‚‚ fâ‚ fâ‚‚ hfâ‚ hfâ‚‚
    apply t
    haveI := hasPullbacks.has_pullbacks hfâ‚ hfâ‚‚
    apply pullback.condition
  Â· intro t Yâ‚ Yâ‚‚ Z gâ‚ gâ‚‚ fâ‚ fâ‚‚ hfâ‚ hfâ‚‚ comm
    haveI := hasPullbacks.has_pullbacks hfâ‚ hfâ‚‚
    rw [â† pullback.lift_fst _ _ comm, op_comp, FunctorToTypes.map_comp_apply, t hfâ‚ hfâ‚‚,
      â† FunctorToTypes.map_comp_apply, â† op_comp, pullback.lift_snd]
theorem FamilyOfElements.Compatible.restrict {Râ‚ Râ‚‚ : Presieve X} (h : Râ‚ â‰¤ Râ‚‚)
    {x : FamilyOfElements P Râ‚‚} : x.Compatible â†’ (x.restrict h).Compatible :=
  fun q _ _ _ gâ‚ gâ‚‚ _ _ hâ‚ hâ‚‚ comm => q gâ‚ gâ‚‚ (h _ hâ‚) (h _ hâ‚‚) comm
noncomputable def FamilyOfElements.sieveExtend (x : FamilyOfElements P R) :
    FamilyOfElements P (generate R : Presieve X) := fun _ _ hf =>
  P.map hf.choose_spec.choose.op (x _ hf.choose_spec.choose_spec.choose_spec.1)
theorem FamilyOfElements.Compatible.sieveExtend {x : FamilyOfElements P R} (hx : x.Compatible) :
    x.sieveExtend.Compatible := by
  intro _ _ _ _ _ _ _ hâ‚ hâ‚‚ comm
  iterate 2 erw [â† FunctorToTypes.map_comp_apply]; rw [â† op_comp]
  apply hx
  simp [comm, hâ‚.choose_spec.choose_spec.choose_spec.2, hâ‚‚.choose_spec.choose_spec.choose_spec.2]
theorem extend_agrees {x : FamilyOfElements P R} (t : x.Compatible) {f : Y âŸ¶ X} (hf : R f) :
    x.sieveExtend f (le_generate R Y hf) = x f hf := by
  have h := (le_generate R Y hf).choose_spec
  unfold FamilyOfElements.sieveExtend
  rw [t h.choose (ğŸ™ _) _ hf _]
  Â· simp
  Â· rw [id_comp]
    exact h.choose_spec.choose_spec.2
@[simp]
theorem restrict_extend {x : FamilyOfElements P R} (t : x.Compatible) :
    x.sieveExtend.restrict (le_generate R) = x := by
  funext Y f hf
  exact extend_agrees t hf
def FamilyOfElements.SieveCompatible (x : FamilyOfElements P (S : Presieve X)) : Prop :=
  âˆ€ â¦ƒY Zâ¦„ (f : Y âŸ¶ X) (g : Z âŸ¶ Y) (hf), x (g â‰« f) (S.downward_closed hf g) = P.map g.op (x f hf)
theorem compatible_iff_sieveCompatible (x : FamilyOfElements P (S : Presieve X)) :
    x.Compatible â†” x.SieveCompatible := by
  constructor
  Â· intro h Y Z f g hf
    simpa using h (ğŸ™ _) g (S.downward_closed hf g) hf (id_comp _)
  Â· intro h Yâ‚ Yâ‚‚ Z gâ‚ gâ‚‚ fâ‚ fâ‚‚ hâ‚ hâ‚‚ k
    simp_rw [â† h fâ‚ gâ‚ hâ‚, â† h fâ‚‚ gâ‚‚ hâ‚‚]
    congr
theorem FamilyOfElements.Compatible.to_sieveCompatible {x : FamilyOfElements P (S : Presieve X)}
    (t : x.Compatible) : x.SieveCompatible :=
  (compatible_iff_sieveCompatible x).1 t
@[simp]
theorem extend_restrict {x : FamilyOfElements P (generate R).arrows} (t : x.Compatible) :
    (x.restrict (le_generate R)).sieveExtend = x := by
  rw [compatible_iff_sieveCompatible] at t
  funext _ _ h
  apply (t _ _ _).symm.trans
  congr
  exact h.choose_spec.choose_spec.choose_spec.2
theorem restrict_inj {xâ‚ xâ‚‚ : FamilyOfElements P (generate R).arrows} (tâ‚ : xâ‚.Compatible)
    (tâ‚‚ : xâ‚‚.Compatible) : xâ‚.restrict (le_generate R) = xâ‚‚.restrict (le_generate R) â†’ xâ‚ = xâ‚‚ :=
  fun h => by
  rw [â† extend_restrict tâ‚, â† extend_restrict tâ‚‚]
  apply congr_arg
  exact h
@[simps]
noncomputable def compatibleEquivGenerateSieveCompatible :
    { x : FamilyOfElements P R // x.Compatible } â‰ƒ
      { x : FamilyOfElements P (generate R : Presieve X) // x.Compatible } where
  toFun x := âŸ¨x.1.sieveExtend, x.2.sieveExtendâŸ©
  invFun x := âŸ¨x.1.restrict (le_generate R), x.2.restrict _âŸ©
  left_inv x := Subtype.ext (restrict_extend x.2)
  right_inv x := Subtype.ext (extend_restrict x.2)
theorem FamilyOfElements.comp_of_compatible (S : Sieve X) {x : FamilyOfElements P S}
    (t : x.Compatible) {f : Y âŸ¶ X} (hf : S f) {Z} (g : Z âŸ¶ Y) :
    x (g â‰« f) (S.downward_closed hf g) = P.map g.op (x f hf) := by
  simpa using t (ğŸ™ _) g (S.downward_closed hf g) hf (id_comp _)
section FunctorPullback
variable {D : Type uâ‚‚} [Category.{vâ‚‚} D] (F : D â¥¤ C) {Z : D}
variable {T : Presieve (F.obj Z)} {x : FamilyOfElements P T}
def FamilyOfElements.functorPullback (x : FamilyOfElements P T) :
    FamilyOfElements (F.op â‹™ P) (T.functorPullback F) := fun _ f hf => x (F.map f) hf
theorem FamilyOfElements.Compatible.functorPullback (h : x.Compatible) :
    (x.functorPullback F).Compatible := by
  intro Zâ‚ Zâ‚‚ W gâ‚ gâ‚‚ fâ‚ fâ‚‚ hâ‚ hâ‚‚ eq
  exact h (F.map gâ‚) (F.map gâ‚‚) hâ‚ hâ‚‚ (by simp only [â† F.map_comp, eq])
end FunctorPullback
noncomputable def FamilyOfElements.functorPushforward {D : Type uâ‚‚} [Category.{vâ‚‚} D] (F : D â¥¤ C)
    {X : D} {T : Presieve X} (x : FamilyOfElements (F.op â‹™ P) T) :
    FamilyOfElements P (T.functorPushforward F) := fun Y f h => by
  obtain âŸ¨Z, g, h, hâ‚, _âŸ© := getFunctorPushforwardStructure h
  exact P.map h.op (x g hâ‚)
section Pullback
def FamilyOfElements.pullback (f : Y âŸ¶ X) (x : FamilyOfElements P (S : Presieve X)) :
    FamilyOfElements P (S.pullback f : Presieve Y) := fun _ g hg => x (g â‰« f) hg
theorem FamilyOfElements.Compatible.pullback (f : Y âŸ¶ X) {x : FamilyOfElements P S.arrows}
    (h : x.Compatible) : (x.pullback f).Compatible := by
  simp only [compatible_iff_sieveCompatible] at h âŠ¢
  intro W Z fâ‚ fâ‚‚ hf
  unfold FamilyOfElements.pullback
  rw [â† h (fâ‚ â‰« f) fâ‚‚ hf]
  congr 1
  simp only [assoc]
end Pullback
def FamilyOfElements.compPresheafMap (f : P âŸ¶ Q) (x : FamilyOfElements P R) :
    FamilyOfElements Q R := fun Y g hg => f.app (op Y) (x g hg)
@[simp]
theorem FamilyOfElements.compPresheafMap_id (x : FamilyOfElements P R) :
    x.compPresheafMap (ğŸ™ P) = x :=
  rfl
@[simp]
theorem FamilyOfElements.compPresheafMap_comp (x : FamilyOfElements P R) (f : P âŸ¶ Q)
    (g : Q âŸ¶ U) : (x.compPresheafMap f).compPresheafMap g = x.compPresheafMap (f â‰« g) :=
  rfl
theorem FamilyOfElements.Compatible.compPresheafMap (f : P âŸ¶ Q) {x : FamilyOfElements P R}
    (h : x.Compatible) : (x.compPresheafMap f).Compatible := by
  intro Zâ‚ Zâ‚‚ W gâ‚ gâ‚‚ fâ‚ fâ‚‚ hâ‚ hâ‚‚ eq
  unfold FamilyOfElements.compPresheafMap
  rwa [â† FunctorToTypes.naturality, â† FunctorToTypes.naturality, h]
def FamilyOfElements.IsAmalgamation (x : FamilyOfElements P R) (t : P.obj (op X)) : Prop :=
  âˆ€ â¦ƒY : Câ¦„ (f : Y âŸ¶ X) (h : R f), P.map f.op t = x f h
theorem FamilyOfElements.IsAmalgamation.compPresheafMap {x : FamilyOfElements P R} {t} (f : P âŸ¶ Q)
    (h : x.IsAmalgamation t) : (x.compPresheafMap f).IsAmalgamation (f.app (op X) t) := by
  intro Y g hg
  dsimp [FamilyOfElements.compPresheafMap]
  change (f.app _ â‰« Q.map _) _ = _
  rw [â† f.naturality, types_comp_apply, h g hg]
theorem is_compatible_of_exists_amalgamation (x : FamilyOfElements P R)
    (h : âˆƒ t, x.IsAmalgamation t) : x.Compatible := by
  cases' h with t ht
  intro Yâ‚ Yâ‚‚ Z gâ‚ gâ‚‚ fâ‚ fâ‚‚ hâ‚ hâ‚‚ comm
  rw [â† ht _ hâ‚, â† ht _ hâ‚‚, â† FunctorToTypes.map_comp_apply, â† op_comp, comm]
  simp
theorem isAmalgamation_restrict {Râ‚ Râ‚‚ : Presieve X} (h : Râ‚ â‰¤ Râ‚‚) (x : FamilyOfElements P Râ‚‚)
    (t : P.obj (op X)) (ht : x.IsAmalgamation t) : (x.restrict h).IsAmalgamation t := fun Y f hf =>
  ht f (h Y hf)
theorem isAmalgamation_sieveExtend {R : Presieve X} (x : FamilyOfElements P R) (t : P.obj (op X))
    (ht : x.IsAmalgamation t) : x.sieveExtend.IsAmalgamation t := by
  intro Y f hf
  dsimp [FamilyOfElements.sieveExtend]
  rw [â† ht _, â† FunctorToTypes.map_comp_apply, â† op_comp, hf.choose_spec.choose_spec.choose_spec.2]
def IsSeparatedFor (P : Cáµ’áµ– â¥¤ Type w) (R : Presieve X) : Prop :=
  âˆ€ (x : FamilyOfElements P R) (tâ‚ tâ‚‚), x.IsAmalgamation tâ‚ â†’ x.IsAmalgamation tâ‚‚ â†’ tâ‚ = tâ‚‚
theorem IsSeparatedFor.ext {R : Presieve X} (hR : IsSeparatedFor P R) {tâ‚ tâ‚‚ : P.obj (op X)}
    (h : âˆ€ â¦ƒYâ¦„ â¦ƒf : Y âŸ¶ Xâ¦„ (_ : R f), P.map f.op tâ‚ = P.map f.op tâ‚‚) : tâ‚ = tâ‚‚ :=
  hR (fun _ f _ => P.map f.op tâ‚‚) tâ‚ tâ‚‚ (fun _ _ hf => h hf) fun _ _ _ => rfl
theorem isSeparatedFor_iff_generate :
    IsSeparatedFor P R â†” IsSeparatedFor P (generate R : Presieve X) := by
  constructor
  Â· intro h x tâ‚ tâ‚‚ htâ‚ htâ‚‚
    apply h (x.restrict (le_generate R)) tâ‚ tâ‚‚ _ _
    Â· exact isAmalgamation_restrict _ x tâ‚ htâ‚
    Â· exact isAmalgamation_restrict _ x tâ‚‚ htâ‚‚
  Â· intro h x tâ‚ tâ‚‚ htâ‚ htâ‚‚
    apply h x.sieveExtend
    Â· exact isAmalgamation_sieveExtend x tâ‚ htâ‚
    Â· exact isAmalgamation_sieveExtend x tâ‚‚ htâ‚‚
theorem isSeparatedFor_top (P : Cáµ’áµ– â¥¤ Type w) : IsSeparatedFor P (âŠ¤ : Presieve X) :=
  fun x tâ‚ tâ‚‚ hâ‚ hâ‚‚ => by
  have qâ‚ := hâ‚ (ğŸ™ X) (by tauto)
  have qâ‚‚ := hâ‚‚ (ğŸ™ X) (by tauto)
  simp only [op_id, FunctorToTypes.map_id_apply] at qâ‚ qâ‚‚
  rw [qâ‚, qâ‚‚]
def IsSheafFor (P : Cáµ’áµ– â¥¤ Type w) (R : Presieve X) : Prop :=
  âˆ€ x : FamilyOfElements P R, x.Compatible â†’ âˆƒ! t, x.IsAmalgamation t
def YonedaSheafCondition (P : Cáµ’áµ– â¥¤ Type vâ‚) (S : Sieve X) : Prop :=
  âˆ€ f : S.functor âŸ¶ P, âˆƒ! g, S.functorInclusion â‰« g = f
def natTransEquivCompatibleFamily {P : Cáµ’áµ– â¥¤ Type vâ‚} :
    (S.functor âŸ¶ P) â‰ƒ { x : FamilyOfElements P (S : Presieve X) // x.Compatible } where
  toFun Î± := by
    refine âŸ¨fun Y f hf => ?_, ?_âŸ©
    Â· apply Î±.app (op Y) âŸ¨_, hfâŸ©
    Â· rw [compatible_iff_sieveCompatible]
      intro Y Z f g hf
      dsimp
      rw [â† FunctorToTypes.naturality _ _ Î± g.op]
      rfl
  invFun t :=
    { app := fun _ f => t.1 _ f.2
      naturality := fun Y Z g => by
        ext âŸ¨f, hfâŸ©
        apply t.2.to_sieveCompatible _ }
  left_inv Î± := by
    ext X âŸ¨_, _âŸ©
    rfl
  right_inv := by
    rintro âŸ¨x, hxâŸ©
    rfl
theorem extension_iff_amalgamation {P : Cáµ’áµ– â¥¤ Type vâ‚} (x : S.functor âŸ¶ P) (g : yoneda.obj X âŸ¶ P) :
    S.functorInclusion â‰« g = x â†”
      (natTransEquivCompatibleFamily x).1.IsAmalgamation (yonedaEquiv g) := by
  change _ â†” âˆ€ â¦ƒY : Câ¦„ (f : Y âŸ¶ X) (h : S f), P.map f.op (yonedaEquiv g) = x.app (op Y) âŸ¨f, hâŸ©
  constructor
  Â· rintro rfl Y f hf
    rw [yonedaEquiv_naturality]
    dsimp
    simp [yonedaEquiv_apply]
  Â· intro h
    ext Y âŸ¨f, hfâŸ©
    convert h f hf
    rw [yonedaEquiv_naturality]
    dsimp [yonedaEquiv]
    simp
theorem isSheafFor_iff_yonedaSheafCondition {P : Cáµ’áµ– â¥¤ Type vâ‚} :
    IsSheafFor P (S : Presieve X) â†” YonedaSheafCondition P S := by
  rw [IsSheafFor, YonedaSheafCondition]
  simp_rw [extension_iff_amalgamation]
  rw [Equiv.forall_congr_left natTransEquivCompatibleFamily]
  rw [Subtype.forall]
  exact forallâ‚‚_congr fun x hx â†¦ by simp [Equiv.existsUnique_congr_right]
noncomputable def IsSheafFor.extend {P : Cáµ’áµ– â¥¤ Type vâ‚} (h : IsSheafFor P (S : Presieve X))
    (f : S.functor âŸ¶ P) : yoneda.obj X âŸ¶ P :=
  (isSheafFor_iff_yonedaSheafCondition.1 h f).exists.choose
@[reassoc (attr := simp)]
theorem IsSheafFor.functorInclusion_comp_extend {P : Cáµ’áµ– â¥¤ Type vâ‚} (h : IsSheafFor P S.arrows)
    (f : S.functor âŸ¶ P) : S.functorInclusion â‰« h.extend f = f :=
  (isSheafFor_iff_yonedaSheafCondition.1 h f).exists.choose_spec
theorem IsSheafFor.unique_extend {P : Cáµ’áµ– â¥¤ Type vâ‚} (h : IsSheafFor P S.arrows) {f : S.functor âŸ¶ P}
    (t : yoneda.obj X âŸ¶ P) (ht : S.functorInclusion â‰« t = f) : t = h.extend f :=
  (isSheafFor_iff_yonedaSheafCondition.1 h f).unique ht (h.functorInclusion_comp_extend f)
theorem IsSheafFor.hom_ext {P : Cáµ’áµ– â¥¤ Type vâ‚} (h : IsSheafFor P (S : Presieve X))
    (tâ‚ tâ‚‚ : yoneda.obj X âŸ¶ P) (ht : S.functorInclusion â‰« tâ‚ = S.functorInclusion â‰« tâ‚‚) :
    tâ‚ = tâ‚‚ :=
  (h.unique_extend tâ‚ ht).trans (h.unique_extend tâ‚‚ rfl).symm
theorem isSeparatedFor_and_exists_isAmalgamation_iff_isSheafFor :
    (IsSeparatedFor P R âˆ§ âˆ€ x : FamilyOfElements P R, x.Compatible â†’ âˆƒ t, x.IsAmalgamation t) â†”
      IsSheafFor P R := by
  rw [IsSeparatedFor, â† forall_and]
  apply forall_congr'
  intro x
  constructor
  Â· intro z hx
    exact exists_unique_of_exists_of_unique (z.2 hx) z.1
  Â· intro h
    refine âŸ¨?_, ExistsUnique.exists âˆ˜ hâŸ©
    intro tâ‚ tâ‚‚ htâ‚ htâ‚‚
    apply (h _).unique htâ‚ htâ‚‚
    exact is_compatible_of_exists_amalgamation x âŸ¨_, htâ‚‚âŸ©
theorem IsSeparatedFor.isSheafFor (t : IsSeparatedFor P R) :
    (âˆ€ x : FamilyOfElements P R, x.Compatible â†’ âˆƒ t, x.IsAmalgamation t) â†’ IsSheafFor P R := by
  rw [â† isSeparatedFor_and_exists_isAmalgamation_iff_isSheafFor]
  exact And.intro t
theorem IsSheafFor.isSeparatedFor : IsSheafFor P R â†’ IsSeparatedFor P R := fun q =>
  (isSeparatedFor_and_exists_isAmalgamation_iff_isSheafFor.2 q).1
noncomputable def IsSheafFor.amalgamate (t : IsSheafFor P R) (x : FamilyOfElements P R)
    (hx : x.Compatible) : P.obj (op X) :=
  (t x hx).exists.choose
theorem IsSheafFor.isAmalgamation (t : IsSheafFor P R) {x : FamilyOfElements P R}
    (hx : x.Compatible) : x.IsAmalgamation (t.amalgamate x hx) :=
  (t x hx).exists.choose_spec
@[simp]
theorem IsSheafFor.valid_glue (t : IsSheafFor P R) {x : FamilyOfElements P R} (hx : x.Compatible)
    (f : Y âŸ¶ X) (Hf : R f) : P.map f.op (t.amalgamate x hx) = x f Hf :=
  t.isAmalgamation hx f Hf
theorem isSheafFor_iff_generate (R : Presieve X) :
    IsSheafFor P R â†” IsSheafFor P (generate R : Presieve X) := by
  rw [â† isSeparatedFor_and_exists_isAmalgamation_iff_isSheafFor]
  rw [â† isSeparatedFor_and_exists_isAmalgamation_iff_isSheafFor]
  rw [â† isSeparatedFor_iff_generate]
  apply and_congr (Iff.refl _)
  constructor
  Â· intro q x hx
    apply Exists.imp _ (q _ (hx.restrict (le_generate R)))
    intro t ht
    simpa [hx] using isAmalgamation_sieveExtend _ _ ht
  Â· intro q x hx
    apply Exists.imp _ (q _ hx.sieveExtend)
    intro t ht
    simpa [hx] using isAmalgamation_restrict (le_generate R) _ _ ht
theorem isSheafFor_singleton_iso (P : Cáµ’áµ– â¥¤ Type w) : IsSheafFor P (Presieve.singleton (ğŸ™ X)) := by
  intro x _
  refine âŸ¨x _ (Presieve.singleton_self _), ?_, ?_âŸ©
  Â· rintro _ _ âŸ¨rfl, rflâŸ©
    simp
  Â· intro t ht
    simpa using ht _ (Presieve.singleton_self _)
theorem isSheafFor_top_sieve (P : Cáµ’áµ– â¥¤ Type w) : IsSheafFor P ((âŠ¤ : Sieve X) : Presieve X) := by
  rw [â† generate_of_singleton_isSplitEpi (ğŸ™ X)]
  rw [â† isSheafFor_iff_generate]
  apply isSheafFor_singleton_iso
theorem isSheafFor_iso {P' : Cáµ’áµ– â¥¤ Type w} (i : P â‰… P') : IsSheafFor P R â†’ IsSheafFor P' R := by
  intro h x hx
  let x' := x.compPresheafMap i.inv
  have : x'.Compatible := FamilyOfElements.Compatible.compPresheafMap i.inv hx
  obtain âŸ¨t, ht1, ht2âŸ© := h x' this
  use i.hom.app _ t
  fconstructor
  Â· convert FamilyOfElements.IsAmalgamation.compPresheafMap i.hom ht1
    simp [x']
  Â· intro y hy
    rw [show y = (i.inv.app (op X) â‰« i.hom.app (op X)) y by simp]
    simp [ht2 (i.inv.app _ y) (FamilyOfElements.IsAmalgamation.compPresheafMap i.inv hy)]
theorem isSheafFor_subsieve_aux (P : Cáµ’áµ– â¥¤ Type w) {S : Sieve X} {R : Presieve X}
    (h : (S : Presieve X) â‰¤ R) (hS : IsSheafFor P (S : Presieve X))
    (trans : âˆ€ â¦ƒYâ¦„ â¦ƒf : Y âŸ¶ Xâ¦„, R f â†’ IsSeparatedFor P (S.pullback f : Presieve Y)) :
    IsSheafFor P R := by
  rw [â† isSeparatedFor_and_exists_isAmalgamation_iff_isSheafFor]
  constructor
  Â· intro x tâ‚ tâ‚‚ htâ‚ htâ‚‚
    exact
      hS.isSeparatedFor _ _ _ (isAmalgamation_restrict h x tâ‚ htâ‚)
        (isAmalgamation_restrict h x tâ‚‚ htâ‚‚)
  Â· intro x hx
    use hS.amalgamate _ (hx.restrict h)
    intro W j hj
    apply (trans hj).ext
    intro Y f hf
    rw [â† FunctorToTypes.map_comp_apply, â† op_comp, hS.valid_glue (hx.restrict h) _ hf,
      FamilyOfElements.restrict, â† hx (ğŸ™ _) f (h _ hf) _ (id_comp _)]
    simp
theorem isSheafFor_subsieve (P : Cáµ’áµ– â¥¤ Type w) {S : Sieve X} {R : Presieve X}
    (h : (S : Presieve X) â‰¤ R) (trans : âˆ€ â¦ƒYâ¦„ (f : Y âŸ¶ X),
      IsSheafFor P (S.pullback f : Presieve Y)) :
    IsSheafFor P R :=
  isSheafFor_subsieve_aux P h (by simpa using trans (ğŸ™ _)) fun _ f _ => (trans f).isSeparatedFor
section Arrows
variable {B : C} {I : Type*} {X : I â†’ C} (Ï€ : (i : I) â†’ X i âŸ¶ B) (P)
def Arrows.Compatible (x : (i : I) â†’ P.obj (op (X i))) : Prop :=
  âˆ€ i j Z (gi : Z âŸ¶ X i) (gj : Z âŸ¶ X j), gi â‰« Ï€ i = gj â‰« Ï€ j â†’
    P.map gi.op (x i) = P.map gj.op (x j)
lemma FamilyOfElements.isAmalgamation_iff_ofArrows (x : FamilyOfElements P (ofArrows X Ï€))
    (t : P.obj (op B)) :
    x.IsAmalgamation t â†” âˆ€ (i : I), P.map (Ï€ i).op t = x _ (ofArrows.mk i) :=
  âŸ¨fun h i â†¦ h _ (ofArrows.mk i), fun h _ f âŸ¨iâŸ© â†¦ h iâŸ©
namespace Arrows.Compatible
variable {x : (i : I) â†’ P.obj (op (X i))}
variable {P Ï€}
theorem exists_familyOfElements (hx : Compatible P Ï€ x) :
    âˆƒ (x' : FamilyOfElements P (ofArrows X Ï€)), âˆ€ (i : I), x' _ (ofArrows.mk i) = x i := by
  choose i h h' using @ofArrows_surj _ _ _ _ _ Ï€
  exact âŸ¨fun Y f hf â†¦ P.map (eqToHom (h f hf).symm).op (x _),
    fun j â†¦ (hx _ j (X j) _ (ğŸ™ _) <| by rw [â† h', id_comp]).trans <| by simpâŸ©
variable (hx : Compatible P Ï€ x)
noncomputable
def familyOfElements : FamilyOfElements P (ofArrows X Ï€) :=
  (exists_familyOfElements hx).choose
@[simp]
theorem familyOfElements_ofArrows_mk (i : I) :
    hx.familyOfElements _ (ofArrows.mk i) = x i :=
  (exists_familyOfElements hx).choose_spec _
theorem familyOfElements_compatible : hx.familyOfElements.Compatible := by
  rintro Yâ‚ Yâ‚‚ Z gâ‚ gâ‚‚ fâ‚ fâ‚‚ âŸ¨iâŸ© âŸ¨jâŸ© hgf
  simp [hx i j Z gâ‚ gâ‚‚ hgf]
end Arrows.Compatible
theorem isSheafFor_arrows_iff : (ofArrows X Ï€).IsSheafFor P â†”
    (âˆ€ (x : (i : I) â†’ P.obj (op (X i))), Arrows.Compatible P Ï€ x â†’
    âˆƒ! t, âˆ€ i, P.map (Ï€ i).op t = x i) := by
  refine âŸ¨fun h x hx â†¦ ?_, fun h x hx â†¦ ?_âŸ©
  Â· obtain âŸ¨t, htâ‚, htâ‚‚âŸ© := h _ hx.familyOfElements_compatible
    refine âŸ¨t, fun i â†¦ ?_, fun t' ht' â†¦ htâ‚‚ _ fun _ _ âŸ¨iâŸ© â†¦ ?_âŸ©
    Â· rw [htâ‚ _ (ofArrows.mk i), hx.familyOfElements_ofArrows_mk]
    Â· rw [ht', hx.familyOfElements_ofArrows_mk]
  Â· obtain âŸ¨t, hA, htâŸ© := h (fun i â†¦ x (Ï€ i) (ofArrows.mk _))
      (fun i j Z gi gj â†¦ hx gi gj (ofArrows.mk _) (ofArrows.mk _))
    exact âŸ¨t, fun Y f âŸ¨iâŸ© â†¦ hA i, fun y hy â†¦ ht y (fun i â†¦ hy (Ï€ i) (ofArrows.mk _))âŸ©
variable [(ofArrows X Ï€).hasPullbacks]
def Arrows.PullbackCompatible (x : (i : I) â†’ P.obj (op (X i))) : Prop :=
  âˆ€ i j, P.map (pullback.fst (Ï€ i) (Ï€ j)).op (x i) =
    P.map (pullback.snd (Ï€ i) (Ï€ j)).op (x j)
theorem Arrows.pullbackCompatible_iff (x : (i : I) â†’ P.obj (op (X i))) :
    Compatible P Ï€ x â†” PullbackCompatible P Ï€ x := by
  refine âŸ¨fun t i j â†¦ ?_, fun t i j Z gi gj comm â†¦ ?_âŸ©
  Â· apply t
    exact pullback.condition
  Â· rw [â† pullback.lift_fst _ _ comm, op_comp, FunctorToTypes.map_comp_apply, t i j,
      â† FunctorToTypes.map_comp_apply, â† op_comp, pullback.lift_snd]
theorem isSheafFor_arrows_iff_pullbacks : (ofArrows X Ï€).IsSheafFor P â†”
    (âˆ€ (x : (i : I) â†’ P.obj (op (X i))), Arrows.PullbackCompatible P Ï€ x â†’
    âˆƒ! t, âˆ€ i, P.map (Ï€ i).op t = x i) := by
  simp_rw [â† Arrows.pullbackCompatible_iff, isSheafFor_arrows_iff]
end Arrows
end Presieve
end CategoryTheory