import Mathlib.CategoryTheory.Limits.Final
import Mathlib.CategoryTheory.Limits.Shapes.Equalizers
import Mathlib.CategoryTheory.Limits.Shapes.KernelPair
namespace CategoryTheory
universe v v‚ÇÇ u u‚ÇÇ
variable {C : Type u} [Category.{v} C]
variable {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
variable {A B : C} {f g : A ‚ü∂ B}
class IsReflexivePair (f g : A ‚ü∂ B) : Prop where
  common_section' : ‚àÉ s : B ‚ü∂ A, s ‚â´ f = ùüô B ‚àß s ‚â´ g = ùüô B
theorem IsReflexivePair.common_section (f g : A ‚ü∂ B) [IsReflexivePair f g] :
    ‚àÉ s : B ‚ü∂ A, s ‚â´ f = ùüô B ‚àß s ‚â´ g = ùüô B := IsReflexivePair.common_section'
class IsCoreflexivePair (f g : A ‚ü∂ B) : Prop where
  common_retraction' : ‚àÉ s : B ‚ü∂ A, f ‚â´ s = ùüô A ‚àß g ‚â´ s = ùüô A
theorem IsCoreflexivePair.common_retraction (f g : A ‚ü∂ B) [IsCoreflexivePair f g] :
    ‚àÉ s : B ‚ü∂ A, f ‚â´ s = ùüô A ‚àß g ‚â´ s = ùüô A := IsCoreflexivePair.common_retraction'
theorem IsReflexivePair.mk' (s : B ‚ü∂ A) (sf : s ‚â´ f = ùüô B) (sg : s ‚â´ g = ùüô B) :
    IsReflexivePair f g :=
  ‚ü®‚ü®s, sf, sg‚ü©‚ü©
theorem IsCoreflexivePair.mk' (s : B ‚ü∂ A) (fs : f ‚â´ s = ùüô A) (gs : g ‚â´ s = ùüô A) :
    IsCoreflexivePair f g :=
  ‚ü®‚ü®s, fs, gs‚ü©‚ü©
noncomputable def commonSection (f g : A ‚ü∂ B) [IsReflexivePair f g] : B ‚ü∂ A :=
  (IsReflexivePair.common_section f g).choose
@[reassoc (attr := simp)]
theorem section_comp_left (f g : A ‚ü∂ B) [IsReflexivePair f g] : commonSection f g ‚â´ f = ùüô B :=
  (IsReflexivePair.common_section f g).choose_spec.1
@[reassoc (attr := simp)]
theorem section_comp_right (f g : A ‚ü∂ B) [IsReflexivePair f g] : commonSection f g ‚â´ g = ùüô B :=
  (IsReflexivePair.common_section f g).choose_spec.2
noncomputable def commonRetraction (f g : A ‚ü∂ B) [IsCoreflexivePair f g] : B ‚ü∂ A :=
  (IsCoreflexivePair.common_retraction f g).choose
@[reassoc (attr := simp)]
theorem left_comp_retraction (f g : A ‚ü∂ B) [IsCoreflexivePair f g] :
    f ‚â´ commonRetraction f g = ùüô A :=
  (IsCoreflexivePair.common_retraction f g).choose_spec.1
@[reassoc (attr := simp)]
theorem right_comp_retraction (f g : A ‚ü∂ B) [IsCoreflexivePair f g] :
    g ‚â´ commonRetraction f g = ùüô A :=
  (IsCoreflexivePair.common_retraction f g).choose_spec.2
theorem IsKernelPair.isReflexivePair {R : C} {f g : R ‚ü∂ A} {q : A ‚ü∂ B} (h : IsKernelPair q f g) :
    IsReflexivePair f g :=
  IsReflexivePair.mk' _ (h.lift' _ _ rfl).2.1 (h.lift' _ _ _).2.2
theorem IsReflexivePair.swap [IsReflexivePair f g] : IsReflexivePair g f :=
  IsReflexivePair.mk' _ (section_comp_right f g) (section_comp_left f g)
theorem IsCoreflexivePair.swap [IsCoreflexivePair f g] : IsCoreflexivePair g f :=
  IsCoreflexivePair.mk' _ (right_comp_retraction f g) (left_comp_retraction f g)
variable {F : C ‚•§ D} {G : D ‚•§ C} (adj : F ‚ä£ G)
instance (B : D) :
    IsReflexivePair (F.map (G.map (adj.counit.app B))) (adj.counit.app (F.obj (G.obj B))) :=
  IsReflexivePair.mk' (F.map (adj.unit.app (G.obj B)))
    (by
      rw [‚Üê F.map_comp, adj.right_triangle_components]
      apply F.map_id)
    (adj.left_triangle_components _)
namespace Limits
variable (C)
class HasReflexiveCoequalizers : Prop where
  has_coeq : ‚àÄ ‚¶ÉA B : C‚¶Ñ (f g : A ‚ü∂ B) [IsReflexivePair f g], HasCoequalizer f g
class HasCoreflexiveEqualizers : Prop where
  has_eq : ‚àÄ ‚¶ÉA B : C‚¶Ñ (f g : A ‚ü∂ B) [IsCoreflexivePair f g], HasEqualizer f g
attribute [instance 1] HasReflexiveCoequalizers.has_coeq
attribute [instance 1] HasCoreflexiveEqualizers.has_eq
theorem hasCoequalizer_of_common_section [HasReflexiveCoequalizers C] {A B : C} {f g : A ‚ü∂ B}
    (r : B ‚ü∂ A) (rf : r ‚â´ f = ùüô _) (rg : r ‚â´ g = ùüô _) : HasCoequalizer f g := by
  letI := IsReflexivePair.mk' r rf rg
  infer_instance
theorem hasEqualizer_of_common_retraction [HasCoreflexiveEqualizers C] {A B : C} {f g : A ‚ü∂ B}
    (r : B ‚ü∂ A) (fr : f ‚â´ r = ùüô _) (gr : g ‚â´ r = ùüô _) : HasEqualizer f g := by
  letI := IsCoreflexivePair.mk' r fr gr
  infer_instance
instance (priority := 100) hasReflexiveCoequalizers_of_hasCoequalizers [HasCoequalizers C] :
    HasReflexiveCoequalizers C where has_coeq A B f g _ := by infer_instance
instance (priority := 100) hasCoreflexiveEqualizers_of_hasEqualizers [HasEqualizers C] :
    HasCoreflexiveEqualizers C where has_eq A B f g _ := by infer_instance
end Limits
end CategoryTheory
namespace CategoryTheory
universe v v‚ÇÇ u u‚ÇÇ
namespace Limits
inductive WalkingReflexivePair : Type where
  | zero
  | one
  deriving DecidableEq, Inhabited
open WalkingReflexivePair
namespace WalkingReflexivePair
inductive Hom : (WalkingReflexivePair ‚Üí WalkingReflexivePair ‚Üí Type)
  | left : Hom one zero
  | right : Hom one zero
  | reflexion : Hom zero one
  | leftCompReflexion : Hom one one
  | rightCompReflexion : Hom one one
  | id (X : WalkingReflexivePair) : Hom X X
  deriving DecidableEq
attribute [-simp, nolint simpNF] Hom.id.sizeOf_spec
attribute [-simp, nolint simpNF] Hom.leftCompReflexion.sizeOf_spec
attribute [-simp, nolint simpNF] Hom.rightCompReflexion.sizeOf_spec
def Hom.comp :
    ‚àÄ { X Y Z : WalkingReflexivePair } (_ : Hom X Y)
      (_ : Hom Y Z), Hom X Z
  | _, _, _, id _, h => h
  | _, _, _, h, id _ => h
  | _, _, _, reflexion, left => id zero
  | _, _, _, reflexion, right => id zero
  | _, _, _, reflexion, rightCompReflexion => reflexion
  | _, _, _, reflexion, leftCompReflexion => reflexion
  | _, _, _, left, reflexion => leftCompReflexion
  | _, _, _, right, reflexion => rightCompReflexion
  | _, _, _, rightCompReflexion, rightCompReflexion => rightCompReflexion
  | _, _, _, rightCompReflexion, leftCompReflexion => rightCompReflexion
  | _, _, _, rightCompReflexion, right => right
  | _, _, _, rightCompReflexion, left => right
  | _, _, _, leftCompReflexion, left => left
  | _, _, _, leftCompReflexion, right => left
  | _, _, _, leftCompReflexion, rightCompReflexion => leftCompReflexion
  | _, _, _, leftCompReflexion, leftCompReflexion => leftCompReflexion
instance category : SmallCategory WalkingReflexivePair where
  Hom := Hom
  id := Hom.id
  comp := Hom.comp
  comp_id := by intro _ _ f; cases f <;> rfl
  id_comp := by intro _ _ f; cases f <;> rfl
  assoc := by intro _ _ _ _ f g h; cases f <;> cases g <;> cases h <;> rfl
open Hom
@[simp]
lemma Hom.id_eq (X : WalkingReflexivePair) :
    Hom.id X = ùüô X := by rfl
@[reassoc (attr := simp)]
lemma reflexion_comp_left : reflexion ‚â´ left = ùüô zero := rfl
@[reassoc (attr := simp)]
lemma reflexion_comp_right : reflexion ‚â´ right = ùüô zero := rfl
@[simp]
lemma leftCompReflexion_eq : leftCompReflexion = (left ‚â´ reflexion : one ‚ü∂ one) := rfl
@[simp]
lemma rightCompReflexion_eq : rightCompReflexion = (right ‚â´ reflexion : one ‚ü∂ one) := rfl
section FunctorsOutOfWalkingReflexivePair
variable {C : Type u} [Category.{v} C]
@[reassoc (attr := simp)]
lemma map_reflexion_comp_map_left (F : WalkingReflexivePair ‚•§ C) :
    F.map reflexion ‚â´ F.map left = ùüô (F.obj zero) := by
  rw [‚Üê F.map_comp, reflexion_comp_left, F.map_id]
@[reassoc (attr := simp)]
lemma map_reflexion_comp_map_right (F : WalkingReflexivePair ‚•§ C) :
    F.map reflexion ‚â´ F.map right = ùüô (F.obj zero) := by
  rw [‚Üê F.map_comp, reflexion_comp_right, F.map_id]
end FunctorsOutOfWalkingReflexivePair
end WalkingReflexivePair
namespace WalkingParallelPair
@[simps!]
def inclusionWalkingReflexivePair : WalkingParallelPair ‚•§ WalkingReflexivePair where
  obj := fun x => match x with
    | one => WalkingReflexivePair.zero
    | zero => WalkingReflexivePair.one
  map := fun f => match f with
    | .left => WalkingReflexivePair.Hom.left
    | .right => WalkingReflexivePair.Hom.right
    | .id _ => WalkingReflexivePair.Hom.id _
  map_comp := by
    intro _ _ _ f g; cases f <;> cases g <;> rfl
variable {C : Type u} [Category.{v} C]
instance (X : WalkingReflexivePair) :
    Nonempty (StructuredArrow X inclusionWalkingReflexivePair) := by
  cases X with
  | zero => exact ‚ü®StructuredArrow.mk (Y := one) (ùüô _)‚ü©
  | one => exact ‚ü®StructuredArrow.mk (Y := zero) (ùüô _)‚ü©
open WalkingReflexivePair.Hom in
instance (X : WalkingReflexivePair) :
    IsConnected (StructuredArrow X inclusionWalkingReflexivePair) := by
  cases X with
  | zero =>
      refine IsConnected.of_induct  (j‚ÇÄ := StructuredArrow.mk (Y := one) (ùüô _)) ?_
      rintro p h‚ÇÅ h‚ÇÇ ‚ü®‚ü®‚ü®‚ü©‚ü©, (_ | _), ‚ü®_‚ü©‚ü©
      ¬∑ exact (h‚ÇÇ (StructuredArrow.homMk .left)).2 h‚ÇÅ
      ¬∑ exact h‚ÇÅ
  | one =>
      refine IsConnected.of_induct  (j‚ÇÄ := StructuredArrow.mk (Y := zero) (ùüô _))
        (fun p h‚ÇÅ h‚ÇÇ ‚Ü¶ ?_)
      have h‚Çó : StructuredArrow.mk left ‚àà p := (h‚ÇÇ (StructuredArrow.homMk .left)).1 h‚ÇÅ
      have h·µ£ : StructuredArrow.mk right ‚àà p := (h‚ÇÇ (StructuredArrow.homMk .right)).1 h‚ÇÅ
      rintro ‚ü®‚ü®‚ü®‚ü©‚ü©, (_ | _), ‚ü®_‚ü©‚ü©
      ¬∑ exact (h‚ÇÇ (StructuredArrow.homMk .left)).2 h‚Çó
      ¬∑ exact (h‚ÇÇ (StructuredArrow.homMk .right)).2 h·µ£
      all_goals assumption
instance inclusionWalkingReflexivePair_final : Functor.Final inclusionWalkingReflexivePair where
  out := inferInstance
end WalkingParallelPair
end Limits
namespace Limits
open WalkingReflexivePair
variable {C : Type u} [Category.{v} C]
variable {A B : C}
def reflexivePair (f g : A ‚ü∂ B) (s : B ‚ü∂ A)
    (sl : s ‚â´ f = ùüô B := by aesop_cat) (sr : s ‚â´ g = ùüô B := by aesop_cat) :
    (WalkingReflexivePair ‚•§ C) where
  obj x :=
    match x with
    | zero => B
    | one => A
  map h :=
    match h with
    | .id _ => ùüô _
    | .left => f
    | .right => g
    | .reflexion => s
    | .rightCompReflexion => g ‚â´ s
    | .leftCompReflexion => f ‚â´ s
  map_comp := by
    rintro _ _ _ ‚ü®‚ü© g <;> cases g <;>
      simp only [Category.id_comp, Category.comp_id, Category.assoc, sl, sr,
        reassoc_of% sl, reassoc_of% sr] <;> rfl
section
variable {A B : C}
variable (f g : A ‚ü∂ B) (s : B ‚ü∂ A) {sl : s ‚â´ f = ùüô B} {sr : s ‚â´ g = ùüô B}
@[simp] lemma reflexivePair_obj_zero : (reflexivePair f g s sl sr).obj zero = B := rfl
@[simp] lemma reflexivePair_obj_one : (reflexivePair f g s sl sr).obj one = A := rfl
@[simp] lemma reflexivePair_map_right : (reflexivePair f g s sl sr).map .left = f := rfl
@[simp] lemma reflexivePair_map_left : (reflexivePair f g s sl sr).map .right = g := rfl
@[simp] lemma reflexivePair_map_reflexion : (reflexivePair f g s sl sr).map .reflexion = s := rfl
end
noncomputable def ofIsReflexivePair (f g : A ‚ü∂ B) [IsReflexivePair f g] :
    WalkingReflexivePair ‚•§ C := reflexivePair f g (commonSection f g)
@[simp]
lemma ofIsReflexivePair_map_left (f g : A ‚ü∂ B) [IsReflexivePair f g] :
    (ofIsReflexivePair f g).map .left = f := rfl
@[simp]
lemma ofIsReflexivePair_map_right (f g : A ‚ü∂ B) [IsReflexivePair f g] :
    (ofIsReflexivePair f g).map .right = g := rfl
noncomputable def inclusionWalkingReflexivePairOfIsReflexivePairIso
    (f g : A ‚ü∂ B) [IsReflexivePair f g] :
    WalkingParallelPair.inclusionWalkingReflexivePair ‚ãô (ofIsReflexivePair f g) ‚âÖ
      parallelPair f g :=
  diagramIsoParallelPair _
end Limits
namespace Limits
variable {C : Type u} [Category.{v} C]
namespace reflexivePair
open WalkingReflexivePair WalkingReflexivePair.Hom
section
section NatTrans
variable {F G : WalkingReflexivePair ‚•§ C}
  (e‚ÇÄ : F.obj zero ‚ü∂ G.obj zero) (e‚ÇÅ : F.obj one ‚ü∂ G.obj one)
  (h‚ÇÅ : F.map left ‚â´ e‚ÇÄ = e‚ÇÅ ‚â´ G.map left := by aesop_cat)
  (h‚ÇÇ : F.map right ‚â´ e‚ÇÄ = e‚ÇÅ ‚â´ G.map right := by aesop_cat)
  (h‚ÇÉ : F.map reflexion ‚â´ e‚ÇÅ = e‚ÇÄ ‚â´ G.map reflexion := by aesop_cat)
def mkNatTrans : F ‚ü∂ G where
  app := fun x ‚Ü¶ match x with
    | zero => e‚ÇÄ
    | one => e‚ÇÅ
  naturality _ _ f := by
    cases f
    all_goals
      dsimp
      simp only [Functor.map_id, Category.id_comp, Category.comp_id,
        Functor.map_comp, h‚ÇÅ, h‚ÇÇ, h‚ÇÉ, reassoc_of% h‚ÇÅ, reassoc_of% h‚ÇÇ,
        reflexivePair_map_reflexion, reflexivePair_map_left, reflexivePair_map_right,
        Category.assoc]
@[simp]
lemma mkNatTrans_app_zero : (mkNatTrans e‚ÇÄ e‚ÇÅ h‚ÇÅ h‚ÇÇ h‚ÇÉ).app zero = e‚ÇÄ := rfl
@[simp]
lemma mkNatTrans_app_one : (mkNatTrans e‚ÇÄ e‚ÇÅ h‚ÇÅ h‚ÇÇ h‚ÇÉ).app one = e‚ÇÅ := rfl
end NatTrans
section NatIso
variable {F G : WalkingReflexivePair ‚•§ C}
@[simps!]
def mkNatIso (e‚ÇÄ : F.obj zero ‚âÖ G.obj zero) (e‚ÇÅ : F.obj one ‚âÖ G.obj one)
    (h‚ÇÅ : F.map left ‚â´ e‚ÇÄ.hom = e‚ÇÅ.hom ‚â´ G.map left := by aesop_cat)
    (h‚ÇÇ : F.map right ‚â´ e‚ÇÄ.hom = e‚ÇÅ.hom ‚â´ G.map right := by aesop_cat)
    (h‚ÇÉ : F.map reflexion ‚â´ e‚ÇÅ.hom = e‚ÇÄ.hom ‚â´ G.map reflexion := by aesop_cat) :
    F ‚âÖ G where
  hom := mkNatTrans e‚ÇÄ.hom e‚ÇÅ.hom
  inv := mkNatTrans e‚ÇÄ.inv e‚ÇÅ.inv
        (by rw [‚Üê cancel_epi e‚ÇÅ.hom, e‚ÇÅ.hom_inv_id_assoc, ‚Üê reassoc_of% h‚ÇÅ, e‚ÇÄ.hom_inv_id,
            Category.comp_id])
        (by rw [‚Üê cancel_epi e‚ÇÅ.hom, e‚ÇÅ.hom_inv_id_assoc, ‚Üê reassoc_of% h‚ÇÇ, e‚ÇÄ.hom_inv_id,
            Category.comp_id])
        (by rw [‚Üê cancel_epi e‚ÇÄ.hom, e‚ÇÄ.hom_inv_id_assoc, ‚Üê reassoc_of% h‚ÇÉ, e‚ÇÅ.hom_inv_id,
            Category.comp_id])
  hom_inv_id := by ext x; cases x <;> simp
  inv_hom_id := by ext x; cases x <;> simp
variable (F)
@[simps!]
def diagramIsoReflexivePair :
    F ‚âÖ reflexivePair (F.map left) (F.map right) (F.map reflexion) :=
  mkNatIso (Iso.refl _) (Iso.refl _)
end NatIso
@[simps!]
def compRightIso {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] {A B : C}
    (f g : A ‚ü∂ B) (s : B ‚ü∂ A) (sl : s ‚â´ f = ùüô B) (sr : s ‚â´ g = ùüô B) (F : C ‚•§ D) :
    (reflexivePair f g s sl sr) ‚ãô F ‚âÖ reflexivePair (F.map f) (F.map g) (F.map s)
      (by simp only [‚Üê Functor.map_comp, sl, Functor.map_id])
      (by simp only [‚Üê Functor.map_comp, sr, Functor.map_id]) :=
  mkNatIso (Iso.refl _) (Iso.refl _)
lemma whiskerRightMkNatTrans {F G : WalkingReflexivePair ‚•§ C}
    (e‚ÇÄ : F.obj zero ‚ü∂ G.obj zero) (e‚ÇÅ : F.obj one ‚ü∂ G.obj one)
    {h‚ÇÅ : F.map left ‚â´ e‚ÇÄ = e‚ÇÅ ‚â´ G.map left}
    {h‚ÇÇ : F.map right ‚â´ e‚ÇÄ = e‚ÇÅ ‚â´ G.map right}
    {h‚ÇÉ : F.map reflexion ‚â´ e‚ÇÅ = e‚ÇÄ ‚â´ G.map reflexion}
    {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] (H : C ‚•§ D) :
    whiskerRight (mkNatTrans e‚ÇÄ e‚ÇÅ : F ‚ü∂ G) H =
      mkNatTrans (H.map e‚ÇÄ) (H.map e‚ÇÅ)
          (by simp only [Functor.comp_obj, Functor.comp_map, ‚Üê Functor.map_comp, h‚ÇÅ])
          (by simp only [Functor.comp_obj, Functor.comp_map, ‚Üê Functor.map_comp, h‚ÇÇ])
          (by simp only [Functor.comp_obj, Functor.comp_map, ‚Üê Functor.map_comp, h‚ÇÉ]) := by
  ext x; cases x <;> simp
end
instance to_isReflexivePair {F : WalkingReflexivePair ‚•§ C} :
    IsReflexivePair (F.map .left) (F.map .right) :=
  ‚ü®F.map .reflexion, map_reflexion_comp_map_left F, map_reflexion_comp_map_right F‚ü©
end reflexivePair
abbrev ReflexiveCofork (F : WalkingReflexivePair ‚•§ C) := Cocone F
namespace ReflexiveCofork
open WalkingReflexivePair WalkingReflexivePair.Hom
variable {F : WalkingReflexivePair ‚•§ C}
abbrev œÄ (G : ReflexiveCofork F) : F.obj zero ‚ü∂ G.pt := G.Œπ.app zero
@[simps pt]
def mk {X : C} (œÄ : F.obj zero ‚ü∂ X) (h : F.map left ‚â´ œÄ = F.map right ‚â´ œÄ) :
    ReflexiveCofork F where
  pt := X
  Œπ := reflexivePair.mkNatTrans œÄ (F.map left ‚â´ œÄ)
@[simp]
lemma mk_œÄ {X : C} (œÄ : F.obj zero ‚ü∂ X) (h : F.map left ‚â´ œÄ = F.map right ‚â´ œÄ) :
    (mk œÄ h).œÄ = œÄ := rfl
lemma condition (G : ReflexiveCofork F) : F.map left ‚â´ G.œÄ = F.map right ‚â´ G.œÄ := by
  rw [Cocone.w G left, Cocone.w G right]
@[simp]
lemma app_one_eq_œÄ (G : ReflexiveCofork F) : G.Œπ.app zero = G.œÄ := rfl
abbrev toCofork (G : ReflexiveCofork F) : Cofork (F.map left) (F.map right) :=
  Cofork.ofœÄ G.œÄ (by simp)
end ReflexiveCofork
noncomputable section
open WalkingReflexivePair WalkingReflexivePair.Hom
variable (F : WalkingReflexivePair ‚•§ C)
@[simps! functor_obj_pt inverse_obj_pt]
def reflexiveCoforkEquivCofork :
    ReflexiveCofork F ‚âå Cofork (F.map left) (F.map right) :=
  (Functor.Final.coconesEquiv _ F).symm.trans (Cocones.precomposeEquivalence
    (diagramIsoParallelPair (WalkingParallelPair.inclusionWalkingReflexivePair ‚ãô F))).symm
@[simp]
lemma reflexiveCoforkEquivCofork_functor_obj_œÄ (G : ReflexiveCofork F) :
    ((reflexiveCoforkEquivCofork F).functor.obj G).œÄ = G.œÄ := by
  dsimp [reflexiveCoforkEquivCofork]
  rw [ReflexiveCofork.œÄ, Cofork.œÄ]
  aesop_cat
@[simp]
lemma reflexiveCoforkEquivCofork_inverse_obj_œÄ
    (G : Cofork (F.map left) (F.map right)) :
    ((reflexiveCoforkEquivCofork F).inverse.obj G).œÄ = G.œÄ := by
  dsimp only [reflexiveCoforkEquivCofork, Equivalence.symm, Equivalence.trans,
    ReflexiveCofork.œÄ, Cocones.precomposeEquivalence, Cocones.precompose,
    Functor.comp, Functor.Final.coconesEquiv]
  rw [Functor.Final.extendCocone_obj_Œπ_app' (Y := .one) (f := ùüô zero)]
  simp
def reflexiveCoforkEquivCoforkObjIso (G : ReflexiveCofork F) :
    (reflexiveCoforkEquivCofork F).functor.obj G ‚âÖ G.toCofork :=
  Cofork.ext (Iso.refl _)
    (by simp [reflexiveCoforkEquivCofork, Cofork.œÄ])
lemma hasReflexiveCoequalizer_iff_hasCoequalizer :
    HasColimit F ‚Üî HasCoequalizer (F.map left) (F.map right) := by
  simpa only [hasColimit_iff_hasInitial_cocone]
    using Equivalence.hasInitial_iff (reflexiveCoforkEquivCofork F)
instance reflexivePair_hasColimit_of_hasCoequalizer
    [h : HasCoequalizer (F.map left) (F.map right)] : HasColimit F :=
  hasReflexiveCoequalizer_iff_hasCoequalizer _|>.mpr h
def ReflexiveCofork.isColimitEquiv (G : ReflexiveCofork F) :
    IsColimit (G.toCofork) ‚âÉ IsColimit G :=
  IsColimit.equivIsoColimit (reflexiveCoforkEquivCoforkObjIso F G).symm|>.trans <|
    (IsColimit.precomposeHomEquiv (diagramIsoParallelPair _).symm (G.whisker _)).trans <|
      Functor.Final.isColimitWhiskerEquiv _ _
section
variable [HasCoequalizer (F.map left) (F.map right)]
def reflexiveCoequalizerIsoCoequalizer :
    colimit F ‚âÖ coequalizer (F.map left) (F.map right) :=
  ((ReflexiveCofork.isColimitEquiv _ _).symm (colimit.isColimit F)).coconePointUniqueUpToIso
    (colimit.isColimit _)
@[reassoc (attr := simp)]
lemma Œπ_reflexiveCoequalizerIsoCoequalizer_hom :
    colimit.Œπ F zero ‚â´ (reflexiveCoequalizerIsoCoequalizer F).hom =
      coequalizer.œÄ (F.map left) (F.map right) :=
  IsColimit.comp_coconePointUniqueUpToIso_hom
    ((ReflexiveCofork.isColimitEquiv F _).symm _) _ WalkingParallelPair.one
@[reassoc (attr := simp)]
lemma œÄ_reflexiveCoequalizerIsoCoequalizer_inv :
    coequalizer.œÄ _ _ ‚â´ (reflexiveCoequalizerIsoCoequalizer F).inv = colimit.Œπ F _ := by
  rw [reflexiveCoequalizerIsoCoequalizer]
  simp only [colimit.comp_coconePointUniqueUpToIso_inv, Cofork.ofœÄ_pt, colimit.cocone_x,
    Cofork.ofœÄ_Œπ_app, colimit.cocone_Œπ]
end
variable {A B : C} {f g : A ‚ü∂ B} [IsReflexivePair f g] [h : HasCoequalizer f g]
instance ofIsReflexivePair_hasColimit_of_hasCoequalizer :
    HasColimit (ofIsReflexivePair f g) :=
  hasReflexiveCoequalizer_iff_hasCoequalizer _|>.mpr h
def colimitOfIsReflexivePairIsoCoequalizer :
    colimit (ofIsReflexivePair f g) ‚âÖ coequalizer f g :=
  @reflexiveCoequalizerIsoCoequalizer _ _ (ofIsReflexivePair f g) h
@[reassoc (attr := simp)]
lemma Œπ_colimitOfIsReflexivePairIsoCoequalizer_hom :
    colimit.Œπ (ofIsReflexivePair f g) zero ‚â´ colimitOfIsReflexivePairIsoCoequalizer.hom =
      coequalizer.œÄ f g := @Œπ_reflexiveCoequalizerIsoCoequalizer_hom _ _ _ h
@[reassoc (attr := simp)]
lemma œÄ_colimitOfIsReflexivePairIsoCoequalizer_inv :
    coequalizer.œÄ f g ‚â´ colimitOfIsReflexivePairIsoCoequalizer.inv =
      colimit.Œπ (ofIsReflexivePair f g) zero :=
  @œÄ_reflexiveCoequalizerIsoCoequalizer_inv _ _ (ofIsReflexivePair f g) h
end
end Limits
namespace Limits
open WalkingReflexivePair
variable {C : Type u} [Category.{v} C]
theorem hasReflexiveCoequalizers_iff :
    HasColimitsOfShape WalkingReflexivePair C ‚Üî HasReflexiveCoequalizers C :=
  ‚ü®fun _ ‚Ü¶ ‚ü®fun _ _ f g _ ‚Ü¶ (hasReflexiveCoequalizer_iff_hasCoequalizer
      (reflexivePair f g (commonSection f g))).1 inferInstance‚ü©,
    fun _ ‚Ü¶ ‚ü®inferInstance‚ü©‚ü©
end Limits
end CategoryTheory