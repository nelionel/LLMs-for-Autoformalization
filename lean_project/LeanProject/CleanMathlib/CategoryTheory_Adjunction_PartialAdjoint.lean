import Mathlib.CategoryTheory.Adjunction.Basic
import Mathlib.CategoryTheory.Limits.HasLimits
import Mathlib.CategoryTheory.Yoneda
import Mathlib.Order.CompleteLattice
universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ
namespace CategoryTheory
namespace Functor
open Category Opposite Limits
variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] (F : D ‚•§ C)
def LeftAdjointObjIsDefined (X : C) : Prop := IsCorepresentable (F ‚ãô coyoneda.obj (op X))
lemma leftAdjointObjIsDefined_iff (X : C) :
    F.LeftAdjointObjIsDefined X ‚Üî IsCorepresentable (F ‚ãô coyoneda.obj (op X)) := by rfl
variable {F} in
lemma leftAdjointObjIsDefined_of_adjunction {G : C ‚•§ D} (adj : G ‚ä£ F) (X : C) :
    F.LeftAdjointObjIsDefined X :=
  (adj.corepresentableBy X).isCorepresentable
abbrev PartialLeftAdjointSource := FullSubcategory F.LeftAdjointObjIsDefined
instance (X : F.PartialLeftAdjointSource) :
    IsCorepresentable (F ‚ãô coyoneda.obj (op X.obj)) := X.property
noncomputable def partialLeftAdjointObj (X : F.PartialLeftAdjointSource) : D :=
  (F ‚ãô coyoneda.obj (op X.obj)).coreprX
noncomputable def partialLeftAdjointHomEquiv {X : F.PartialLeftAdjointSource} {Y : D} :
    (F.partialLeftAdjointObj X ‚ü∂ Y) ‚âÉ (X.obj ‚ü∂ F.obj Y) :=
  (F ‚ãô coyoneda.obj (op X.obj)).corepresentableBy.homEquiv
lemma partialLeftAdjointHomEquiv_comp {X : F.PartialLeftAdjointSource} {Y Y' : D}
    (f : F.partialLeftAdjointObj X ‚ü∂ Y) (g : Y ‚ü∂ Y') :
    F.partialLeftAdjointHomEquiv (f ‚â´ g) =
      F.partialLeftAdjointHomEquiv f ‚â´ F.map g := by
  apply CorepresentableBy.homEquiv_comp
noncomputable def partialLeftAdjointMap {X Y : F.PartialLeftAdjointSource}
    (f : X ‚ü∂ Y) : F.partialLeftAdjointObj X ‚ü∂ F.partialLeftAdjointObj Y :=
    F.partialLeftAdjointHomEquiv.symm (f ‚â´ F.partialLeftAdjointHomEquiv (ùüô _))
@[simp]
lemma partialLeftAdjointHomEquiv_map {X Y : F.PartialLeftAdjointSource}
    (f : X ‚ü∂ Y) :
    F.partialLeftAdjointHomEquiv (F.partialLeftAdjointMap f) =
      by exact f ‚â´ F.partialLeftAdjointHomEquiv (ùüô _) := by
  simp [partialLeftAdjointMap]
lemma partialLeftAdjointHomEquiv_map_comp {X X' : F.PartialLeftAdjointSource} {Y : D}
    (f : X ‚ü∂ X') (g : F.partialLeftAdjointObj X' ‚ü∂ Y) :
    F.partialLeftAdjointHomEquiv (F.partialLeftAdjointMap f ‚â´ g) =
      by exact f ‚â´ F.partialLeftAdjointHomEquiv g := by
  rw [partialLeftAdjointHomEquiv_comp, partialLeftAdjointHomEquiv_map, assoc,
    ‚Üê partialLeftAdjointHomEquiv_comp, id_comp]
@[simps]
noncomputable def partialLeftAdjoint : F.PartialLeftAdjointSource ‚•§ D where
  obj := F.partialLeftAdjointObj
  map := F.partialLeftAdjointMap
  map_id X := by
    apply F.partialLeftAdjointHomEquiv.injective
    dsimp
    rw [partialLeftAdjointHomEquiv_map]
    erw [id_comp]
  map_comp {X Y Z} f g := by
    apply F.partialLeftAdjointHomEquiv.injective
    dsimp
    rw [partialLeftAdjointHomEquiv_map, partialLeftAdjointHomEquiv_comp,
      partialLeftAdjointHomEquiv_map, assoc]
    erw [assoc]
    rw [‚Üê F.partialLeftAdjointHomEquiv_comp, id_comp,
      partialLeftAdjointHomEquiv_map]
variable {F}
lemma isRightAdjoint_of_leftAdjointObjIsDefined_eq_top
    (h : F.LeftAdjointObjIsDefined = ‚ä§) : F.IsRightAdjoint := by
  replace h : ‚àÄ X, IsCorepresentable (F ‚ãô coyoneda.obj (op X)) := fun X ‚Ü¶ by
    simp only [‚Üê leftAdjointObjIsDefined_iff, h, Pi.top_apply, Prop.top_eq_true]
  exact (Adjunction.adjunctionOfEquivLeft
    (fun X Y ‚Ü¶ (F ‚ãô coyoneda.obj (op X)).corepresentableBy.homEquiv)
    (fun X Y Y' g f ‚Ü¶ by apply CorepresentableBy.homEquiv_comp)).isRightAdjoint
variable (F) in
lemma isRightAdjoint_iff_leftAdjointObjIsDefined_eq_top :
    F.IsRightAdjoint ‚Üî F.LeftAdjointObjIsDefined = ‚ä§ := by
  refine ‚ü®fun h ‚Ü¶ ?_, isRightAdjoint_of_leftAdjointObjIsDefined_eq_top‚ü©
  ext X
  simpa only [Pi.top_apply, Prop.top_eq_true, iff_true]
    using leftAdjointObjIsDefined_of_adjunction (Adjunction.ofIsRightAdjoint F) X
noncomputable def corepresentableByCompCoyonedaObjOfIsColimit {J : Type*} [Category J]
    {R : J ‚•§ F.PartialLeftAdjointSource}
    {c : Cocone (R ‚ãô fullSubcategoryInclusion _)} (hc : IsColimit c)
    {c' : Cocone (R ‚ãô F.partialLeftAdjoint)} (hc' : IsColimit c') :
    (F ‚ãô coyoneda.obj (op c.pt)).CorepresentableBy c'.pt where
  homEquiv {Y} :=
    { toFun := fun f ‚Ü¶ hc.desc (Cocone.mk _
        { app := fun j ‚Ü¶ F.partialLeftAdjointHomEquiv (c'.Œπ.app j ‚â´ f)
          naturality := fun j j' œÜ ‚Ü¶ by
            dsimp
            rw [comp_id, ‚Üê c'.w œÜ, ‚Üê partialLeftAdjointHomEquiv_map_comp, assoc]
            dsimp })
      invFun := fun g ‚Ü¶ hc'.desc (Cocone.mk _
        { app := fun j ‚Ü¶ F.partialLeftAdjointHomEquiv.symm (c.Œπ.app j ‚â´ g)
          naturality := fun j j' œÜ ‚Ü¶ by
            apply F.partialLeftAdjointHomEquiv.injective
            have := c.w œÜ
            dsimp at this ‚ä¢
            rw [comp_id, Equiv.apply_symm_apply, partialLeftAdjointHomEquiv_map_comp,
              Equiv.apply_symm_apply, reassoc_of% this] })
      left_inv := fun f ‚Ü¶ hc'.hom_ext (fun j ‚Ü¶ by simp)
      right_inv := fun g ‚Ü¶ hc.hom_ext (fun j ‚Ü¶ by simp) }
  homEquiv_comp {Y Y'} g f := hc.hom_ext (fun j ‚Ü¶ by
    dsimp
    simp only [IsColimit.fac, IsColimit.fac_assoc, partialLeftAdjointHomEquiv_comp,
      F.map_comp, assoc] )
lemma leftAdjointObjIsDefined_of_isColimit {J : Type*} [Category J] {R : J ‚•§ C} {c : Cocone R}
    (hc : IsColimit c) [HasColimitsOfShape J D]
    (h : ‚àÄ (j : J), F.LeftAdjointObjIsDefined (R.obj j)) :
    F.LeftAdjointObjIsDefined c.pt :=
  (corepresentableByCompCoyonedaObjOfIsColimit
    (R := FullSubcategory.lift _ R h) hc (colimit.isColimit _)).isCorepresentable
lemma leftAdjointObjIsDefined_colimit {J : Type*} [Category J] (R : J ‚•§ C)
    [HasColimit R] [HasColimitsOfShape J D]
    (h : ‚àÄ (j : J), F.LeftAdjointObjIsDefined (R.obj j)) :
    F.LeftAdjointObjIsDefined (colimit R) :=
  leftAdjointObjIsDefined_of_isColimit (colimit.isColimit R) h
end Functor
end CategoryTheory