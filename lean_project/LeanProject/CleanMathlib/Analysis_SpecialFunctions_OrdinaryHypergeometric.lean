import Mathlib.Analysis.Analytic.OfScalars
import Mathlib.Analysis.SpecificLimits.RCLike
open Nat FormalMultilinearSeries
section Field
variable {ğ•‚ : Type*} (ğ”¸ : Type*) [Field ğ•‚] [Ring ğ”¸] [Algebra ğ•‚ ğ”¸] [TopologicalSpace ğ”¸]
  [TopologicalRing ğ”¸]
noncomputable abbrev ordinaryHypergeometricCoefficient (a b c : ğ•‚) (n : â„•) := ((n !â»Â¹ : ğ•‚) *
    (ascPochhammer ğ•‚ n).eval a * (ascPochhammer ğ•‚ n).eval b * ((ascPochhammer ğ•‚ n).eval c)â»Â¹)
noncomputable def ordinaryHypergeometricSeries (a b c : ğ•‚) : FormalMultilinearSeries ğ•‚ ğ”¸ ğ”¸ :=
  ofScalars ğ”¸ (ordinaryHypergeometricCoefficient a b c)
variable {ğ”¸} (a b c : ğ•‚)
noncomputable def ordinaryHypergeometric (x : ğ”¸) : ğ”¸ :=
  (ordinaryHypergeometricSeries ğ”¸ a b c).sum x
@[inherit_doc]
notation "â‚‚Fâ‚" => ordinaryHypergeometric
theorem ordinaryHypergeometricSeries_apply_eq (x : ğ”¸) (n : â„•) :
    (ordinaryHypergeometricSeries ğ”¸ a b c n fun _ => x) =
      ((n !â»Â¹ : ğ•‚) * (ascPochhammer ğ•‚ n).eval a * (ascPochhammer ğ•‚ n).eval b *
        ((ascPochhammer ğ•‚ n).eval c)â»Â¹ ) â€¢ x ^ n := by
  rw [ordinaryHypergeometricSeries, ofScalars_apply_eq]
theorem ordinaryHypergeometricSeries_apply_eq' (x : ğ”¸) :
    (fun n => ordinaryHypergeometricSeries ğ”¸ a b c n fun _ => x) =
      fun n => ((n !â»Â¹ : ğ•‚) * (ascPochhammer ğ•‚ n).eval a * (ascPochhammer ğ•‚ n).eval b *
        ((ascPochhammer ğ•‚ n).eval c)â»Â¹ ) â€¢ x ^ n := by
  rw [ordinaryHypergeometricSeries, ofScalars_apply_eq']
theorem ordinaryHypergeometric_sum_eq (x : ğ”¸) : (ordinaryHypergeometricSeries ğ”¸ a b c).sum x =
    âˆ‘' n : â„•, ((n !â»Â¹ : ğ•‚) * (ascPochhammer ğ•‚ n).eval a * (ascPochhammer ğ•‚ n).eval b *
      ((ascPochhammer ğ•‚ n).eval c)â»Â¹ ) â€¢ x ^ n :=
  tsum_congr fun n => ordinaryHypergeometricSeries_apply_eq a b c x n
theorem ordinaryHypergeometric_eq_tsum : â‚‚Fâ‚ a b c =
    fun (x : ğ”¸) => âˆ‘' n : â„•, ((n !â»Â¹ : ğ•‚) * (ascPochhammer ğ•‚ n).eval a *
      (ascPochhammer ğ•‚ n).eval b * ((ascPochhammer ğ•‚ n).eval c)â»Â¹ ) â€¢ x ^ n :=
  funext (ordinaryHypergeometric_sum_eq a b c)
theorem ordinaryHypergeometricSeries_apply_zero (n : â„•) :
    (ordinaryHypergeometricSeries ğ”¸ a b c n fun _ => 0) = Pi.single (f := fun _ => ğ”¸) 0 1 n := by
  rw [ordinaryHypergeometricSeries, ofScalars_apply_eq, ordinaryHypergeometricCoefficient]
  cases n <;> simp
@[simp]
theorem ordinaryHypergeometric_zero : â‚‚Fâ‚ a b c (0 : ğ”¸) = 1 := by
  simp [ordinaryHypergeometric_eq_tsum, â† ordinaryHypergeometricSeries_apply_eq,
    ordinaryHypergeometricSeries_apply_zero]
theorem ordinaryHypergeometricSeries_symm :
    ordinaryHypergeometricSeries ğ”¸ a b c = ordinaryHypergeometricSeries ğ”¸ b a c := by
  unfold ordinaryHypergeometricSeries ordinaryHypergeometricCoefficient
  simp [mul_assoc, mul_left_comm]
lemma ordinaryHypergeometricSeries_eq_zero_of_neg_nat {n k : â„•} (habc : k = -a âˆ¨ k = -b âˆ¨ k = -c)
    (hk : k < n) : ordinaryHypergeometricSeries ğ”¸ a b c n = 0 := by
  rw [ordinaryHypergeometricSeries, ofScalars]
  rcases habc with h | h | h
  all_goals
    ext
    simp [(ascPochhammer_eval_eq_zero_iff n _).2 âŸ¨k, hk, hâŸ©]
end Field
section RCLike
open Asymptotics Filter Real Set Nat
open scoped Topology
variable {ğ•‚ : Type*} (ğ”¸ : Type*) [RCLike ğ•‚] [NormedDivisionRing ğ”¸] [NormedAlgebra ğ•‚ ğ”¸]
  (a b c : ğ•‚)
theorem ordinaryHypergeometric_radius_top_of_neg_natâ‚ {k : â„•} :
    (ordinaryHypergeometricSeries ğ”¸ (-(k : ğ•‚)) b c).radius = âŠ¤ := by
  refine FormalMultilinearSeries.radius_eq_top_of_forall_image_add_eq_zero _ (1 + k) fun n â†¦ ?_
  exact ordinaryHypergeometricSeries_eq_zero_of_neg_nat (-(k : ğ•‚)) b c (by aesop) (by omega)
theorem ordinaryHypergeometric_radius_top_of_neg_natâ‚‚ {k : â„•} :
    (ordinaryHypergeometricSeries ğ”¸ a (-(k : ğ•‚)) c).radius = âŠ¤ := by
  rw [ordinaryHypergeometricSeries_symm]
  exact ordinaryHypergeometric_radius_top_of_neg_natâ‚ ğ”¸ a c
theorem ordinaryHypergeometric_radius_top_of_neg_natâ‚ƒ {k : â„•} :
    (ordinaryHypergeometricSeries ğ”¸ a b (-(k : ğ•‚))).radius = âŠ¤ := by
  refine FormalMultilinearSeries.radius_eq_top_of_forall_image_add_eq_zero _ (1 + k) fun n â†¦ ?_
  exact ordinaryHypergeometricSeries_eq_zero_of_neg_nat a b (-(k : ğ•‚)) (by aesop) (by omega)
lemma ordinaryHypergeometricSeries_eq_zero_iff (n : â„•) :
    ordinaryHypergeometricSeries ğ”¸ a b c n = 0 â†” âˆƒ k < n, k = -a âˆ¨ k = -b âˆ¨ k = -c := by
  refine âŸ¨fun h â†¦ ?_, fun zero â†¦ ?_âŸ©
  Â· rw [ordinaryHypergeometricSeries, ofScalars_eq_zero] at h
    simp only [_root_.mul_eq_zero, inv_eq_zero] at h
    rcases h with ((hn | h) | h) | h
    Â· simp [Nat.factorial_ne_zero] at hn
    all_goals
      obtain âŸ¨kn, hkn, hnâŸ© := (ascPochhammer_eval_eq_zero_iff _ _).1 h
      exact âŸ¨kn, hkn, by tautoâŸ©
  Â· obtain âŸ¨_, h, hnâŸ© := zero
    exact ordinaryHypergeometricSeries_eq_zero_of_neg_nat a b c hn h
theorem ordinaryHypergeometricSeries_norm_div_succ_norm (n : â„•)
    (habc : âˆ€ kn < n, (â†‘kn â‰  -a âˆ§ â†‘kn â‰  -b âˆ§ â†‘kn â‰  -c)) :
      â€–ordinaryHypergeometricCoefficient a b c nâ€– / â€–ordinaryHypergeometricCoefficient a b c n.succâ€–
      = â€–a + nâ€–â»Â¹ * â€–b + nâ€–â»Â¹ * â€–c + nâ€– * â€–1 + (n : ğ•‚)â€– := by
  simp only [mul_inv_rev, factorial_succ, cast_mul, cast_add,
    cast_one, ascPochhammer_succ_eval, norm_mul, norm_inv]
  calc
    _ = â€–Polynomial.eval a (ascPochhammer ğ•‚ n)â€– * â€–Polynomial.eval a (ascPochhammer ğ•‚ n)â€–â»Â¹ *
        â€–Polynomial.eval b (ascPochhammer ğ•‚ n)â€– * â€–Polynomial.eval b (ascPochhammer ğ•‚ n)â€–â»Â¹ *
        â€–Polynomial.eval c (ascPochhammer ğ•‚ n)â€–â»Â¹â»Â¹ * â€–Polynomial.eval c (ascPochhammer ğ•‚ n)â€–â»Â¹ *
        â€–(n ! : ğ•‚)â€–â»Â¹â»Â¹ * â€–(n ! : ğ•‚)â€–â»Â¹ * â€–a + nâ€–â»Â¹ * â€–b + nâ€–â»Â¹ * â€–c + nâ€–â»Â¹â»Â¹ *
        â€–1 + (n : ğ•‚)â€–â»Â¹â»Â¹ := by ring_nf
    _ = _ := by
      simp only [inv_inv]
      repeat rw [DivisionRing.mul_inv_cancel, one_mul]
      all_goals
        rw [norm_ne_zero_iff]
      any_goals
        apply (ascPochhammer_eval_eq_zero_iff n _).not.2
        push_neg
        exact fun kn hkn â†¦ by simp [habc kn hkn]
      exact cast_ne_zero.2 (factorial_ne_zero n)
theorem ordinaryHypergeometricSeries_radius_eq_one
    (habc : âˆ€ kn : â„•, â†‘kn â‰  -a âˆ§ â†‘kn â‰  -b âˆ§ â†‘kn â‰  -c) :
      (ordinaryHypergeometricSeries ğ”¸ a b c).radius = 1 := by
  convert ofScalars_radius_eq_of_tendsto ğ”¸ _ one_ne_zero ?_
  suffices Tendsto (fun k : â„• â†¦ (a + k)â»Â¹ * (b + k)â»Â¹ * (c + k) * ((1 : ğ•‚) + k)) atTop (ğ“ 1) by
    simp_rw [ordinaryHypergeometricSeries_norm_div_succ_norm a b c _ (fun n _ â†¦ habc n)]
    simp [â† norm_mul, â† norm_inv]
    convert Filter.Tendsto.norm this
    exact norm_one.symm
  have (k : â„•) : (a + k)â»Â¹ * (b + k)â»Â¹ * (c + k) * ((1 : ğ•‚) + k) =
        (c + k) / (a + k) * ((1 + k) / (b + k)) := by field_simp
  simp_rw [this]
  apply (mul_one (1 : ğ•‚)) â–¸ Filter.Tendsto.mul <;>
  convert RCLike.tendsto_add_mul_div_add_mul_atTop_nhds _ _ (1 : ğ•‚) one_ne_zero <;> simp
end RCLike