import Mathlib.Analysis.Calculus.Deriv.Inv
import Mathlib.Analysis.Complex.Circle
import Mathlib.Analysis.NormedSpace.BallAction
import Mathlib.Analysis.SpecialFunctions.ExpDeriv
import Mathlib.Analysis.InnerProductSpace.Calculus
import Mathlib.Analysis.InnerProductSpace.PiL2
import Mathlib.Data.Complex.FiniteDimensional
import Mathlib.Geometry.Manifold.Algebra.LieGroup
import Mathlib.Geometry.Manifold.Instances.Real
import Mathlib.Geometry.Manifold.MFDeriv.Basic
import Mathlib.Tactic.Module
variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace â„ E]
noncomputable section
open Metric Module Function
open scoped Manifold ContDiff
section StereographicProjection
variable (v : E)
def stereoToFun (x : E) : (â„ âˆ™ v)á—® :=
  (2 / ((1 : â„) - innerSL â„ v x)) â€¢ orthogonalProjection (â„ âˆ™ v)á—® x
variable {v}
@[simp]
theorem stereoToFun_apply (x : E) :
    stereoToFun v x = (2 / ((1 : â„) - innerSL â„ v x)) â€¢ orthogonalProjection (â„ âˆ™ v)á—® x :=
  rfl
theorem contDiffOn_stereoToFun :
    ContDiffOn â„ âˆ (stereoToFun v) {x : E | innerSL _ v x â‰  (1 : â„)} := by
  refine ContDiffOn.smul ?_ (orthogonalProjection (â„ âˆ™ v)á—®).contDiff.contDiffOn
  refine contDiff_const.contDiffOn.div ?_ ?_
  Â· exact (contDiff_const.sub (innerSL â„ v).contDiff).contDiffOn
  Â· intro x h h'
    exact h (sub_eq_zero.mp h').symm
theorem continuousOn_stereoToFun :
    ContinuousOn (stereoToFun v) {x : E | innerSL _ v x â‰  (1 : â„)} :=
  contDiffOn_stereoToFun.continuousOn
variable (v)
def stereoInvFunAux (w : E) : E :=
  (â€–wâ€– ^ 2 + 4)â»Â¹ â€¢ ((4 : â„) â€¢ w + (â€–wâ€– ^ 2 - 4) â€¢ v)
variable {v}
@[simp]
theorem stereoInvFunAux_apply (w : E) :
    stereoInvFunAux v w = (â€–wâ€– ^ 2 + 4)â»Â¹ â€¢ ((4 : â„) â€¢ w + (â€–wâ€– ^ 2 - 4) â€¢ v) :=
  rfl
theorem stereoInvFunAux_mem (hv : â€–vâ€– = 1) {w : E} (hw : w âˆˆ (â„ âˆ™ v)á—®) :
    stereoInvFunAux v w âˆˆ sphere (0 : E) 1 := by
  have hâ‚ : (0 : â„) < â€–wâ€– ^ 2 + 4 := by positivity
  suffices â€–(4 : â„) â€¢ w + (â€–wâ€– ^ 2 - 4) â€¢ vâ€– = â€–wâ€– ^ 2 + 4 by
    simp only [mem_sphere_zero_iff_norm, norm_smul, Real.norm_eq_abs, abs_inv, this,
      abs_of_pos hâ‚, stereoInvFunAux_apply, inv_mul_cancelâ‚€ hâ‚.ne']
  suffices â€–(4 : â„) â€¢ w + (â€–wâ€– ^ 2 - 4) â€¢ vâ€– ^ 2 = (â€–wâ€– ^ 2 + 4) ^ 2 by
    simpa only [sq_eq_sq_iff_abs_eq_abs, abs_norm, abs_of_pos hâ‚] using this
  rw [Submodule.mem_orthogonal_singleton_iff_inner_left] at hw
  simp [norm_add_sq_real, norm_smul, inner_smul_left, inner_smul_right, hw, mul_pow,
    Real.norm_eq_abs, hv]
  ring
theorem hasFDerivAt_stereoInvFunAux (v : E) :
    HasFDerivAt (stereoInvFunAux v) (ContinuousLinearMap.id â„ E) 0 := by
  have hâ‚€ : HasFDerivAt (fun w : E => â€–wâ€– ^ 2) (0 : E â†’L[â„] â„) 0 := by
    convert (hasStrictFDerivAt_norm_sq (0 : E)).hasFDerivAt
    simp only [map_zero, smul_zero]
  have hâ‚ : HasFDerivAt (fun w : E => (â€–wâ€– ^ 2 + 4)â»Â¹) (0 : E â†’L[â„] â„) 0 := by
    convert (hasFDerivAt_inv _).comp _ (hâ‚€.add (hasFDerivAt_const 4 0)) <;> simp
  have hâ‚‚ : HasFDerivAt (fun w => (4 : â„) â€¢ w + (â€–wâ€– ^ 2 - 4) â€¢ v)
      ((4 : â„) â€¢ ContinuousLinearMap.id â„ E) 0 := by
    convert ((hasFDerivAt_const (4 : â„) 0).smul (hasFDerivAt_id 0)).add
      ((hâ‚€.sub (hasFDerivAt_const (4 : â„) 0)).smul (hasFDerivAt_const v 0)) using 1
    ext w
    simp
  convert hâ‚.smul hâ‚‚ using 1
  ext w
  simp
theorem hasFDerivAt_stereoInvFunAux_comp_coe (v : E) :
    HasFDerivAt (stereoInvFunAux v âˆ˜ ((â†‘) : (â„ âˆ™ v)á—® â†’ E)) (â„ âˆ™ v)á—®.subtypeL 0 := by
  have : HasFDerivAt (stereoInvFunAux v) (ContinuousLinearMap.id â„ E) ((â„ âˆ™ v)á—®.subtypeL 0) :=
    hasFDerivAt_stereoInvFunAux v
  refine this.comp (0 : (â„ âˆ™ v)á—®) (by apply ContinuousLinearMap.hasFDerivAt)
theorem contDiff_stereoInvFunAux : ContDiff â„ âˆ (stereoInvFunAux v) := by
  have hâ‚€ : ContDiff â„ âˆ fun w : E => â€–wâ€– ^ 2 := contDiff_norm_sq â„
  have hâ‚ : ContDiff â„ âˆ fun w : E => (â€–wâ€– ^ 2 + 4)â»Â¹ := by
    refine (hâ‚€.add contDiff_const).inv ?_
    intro x
    nlinarith
  have hâ‚‚ : ContDiff â„ âˆ fun w => (4 : â„) â€¢ w + (â€–wâ€– ^ 2 - 4) â€¢ v := by
    refine (contDiff_const.smul contDiff_id).add ?_
    exact (hâ‚€.sub contDiff_const).smul contDiff_const
  exact hâ‚.smul hâ‚‚
def stereoInvFun (hv : â€–vâ€– = 1) (w : (â„ âˆ™ v)á—®) : sphere (0 : E) 1 :=
  âŸ¨stereoInvFunAux v (w : E), stereoInvFunAux_mem hv w.2âŸ©
@[simp]
theorem stereoInvFun_apply (hv : â€–vâ€– = 1) (w : (â„ âˆ™ v)á—®) :
    (stereoInvFun hv w : E) = (â€–wâ€– ^ 2 + 4)â»Â¹ â€¢ ((4 : â„) â€¢ w + (â€–wâ€– ^ 2 - 4) â€¢ v) :=
  rfl
open scoped InnerProductSpace in
theorem stereoInvFun_ne_north_pole (hv : â€–vâ€– = 1) (w : (â„ âˆ™ v)á—®) :
    stereoInvFun hv w â‰  (âŸ¨v, by simp [hv]âŸ© : sphere (0 : E) 1) := by
  refine Subtype.coe_ne_coe.1 ?_
  rw [â† inner_lt_one_iff_real_of_norm_one _ hv]
  Â· have hw : âŸªv, wâŸ«_â„ = 0 := Submodule.mem_orthogonal_singleton_iff_inner_right.mp w.2
    have hw' : (â€–(w : E)â€– ^ 2 + 4)â»Â¹ * (â€–(w : E)â€– ^ 2 - 4) < 1 := by
      rw [inv_mul_lt_iffâ‚€']
      Â· linarith
      positivity
    simpa [real_inner_comm, inner_add_right, inner_smul_right, real_inner_self_eq_norm_mul_norm, hw,
      hv] using hw'
  Â· simpa using stereoInvFunAux_mem hv w.2
theorem continuous_stereoInvFun (hv : â€–vâ€– = 1) : Continuous (stereoInvFun hv) :=
  continuous_induced_rng.2 (contDiff_stereoInvFunAux.continuous.comp continuous_subtype_val)
open scoped InnerProductSpace in
attribute [-simp] AddSubgroupClass.coe_norm Submodule.coe_norm in
theorem stereo_left_inv (hv : â€–vâ€– = 1) {x : sphere (0 : E) 1} (hx : (x : E) â‰  v) :
    stereoInvFun hv (stereoToFun v x) = x := by
  ext
  simp only [stereoToFun_apply, stereoInvFun_apply, smul_add]
  set a : â„ := innerSL _ v x
  set y := orthogonalProjection (â„ âˆ™ v)á—® x
  have split : â†‘x = a â€¢ v + â†‘y := by
    convert (orthogonalProjection_add_orthogonalProjection_orthogonal (â„ âˆ™ v) x).symm
    exact (orthogonalProjection_unit_singleton â„ hv x).symm
  have hvy : âŸªv, yâŸ«_â„ = 0 := Submodule.mem_orthogonal_singleton_iff_inner_right.mp y.2
  have pythag : 1 = a ^ 2 + â€–yâ€– ^ 2 := by
    have hvy' : âŸªa â€¢ v, yâŸ«_â„ = 0 := by simp only [inner_smul_left, hvy, mul_zero]
    convert norm_add_sq_eq_norm_sq_add_norm_sq_of_inner_eq_zero _ _ hvy' using 2
    Â· simp [â† split]
    Â· simp [norm_smul, hv, â† sq, sq_abs]
    Â· exact sq _
  have ha : 0 < 1 - a := by
    have : a < 1 := (inner_lt_one_iff_real_of_norm_one hv (by simp)).mpr hx.symm
    linarith
  rw [split, Submodule.coe_smul_of_tower]
  simp only [norm_smul, norm_div, RCLike.norm_ofNat, Real.norm_eq_abs, abs_of_nonneg ha.le]
  match_scalars
  Â· field_simp
    linear_combination 4 * (1 - a) * pythag
  Â· field_simp
    linear_combination 4 * (a - 1) ^ 3 * pythag
theorem stereo_right_inv (hv : â€–vâ€– = 1) (w : (â„ âˆ™ v)á—®) : stereoToFun v (stereoInvFun hv w) = w := by
  simp only [stereoToFun, stereoInvFun, stereoInvFunAux, smul_add, map_add, map_smul, innerSL_apply,
    orthogonalProjection_mem_subspace_eq_self]
  have hâ‚ : orthogonalProjection (â„ âˆ™ v)á—® v = 0 :=
    orthogonalProjection_orthogonalComplement_singleton_eq_zero v
  have hâ‚‚ : inner v w = (0 : â„) := Submodule.mem_orthogonal_singleton_iff_inner_right.mp w.2
  have hâ‚ƒ : inner v v = (1 : â„) := by simp [real_inner_self_eq_norm_mul_norm, hv]
  rw [hâ‚, hâ‚‚, hâ‚ƒ]
  match_scalars
  simp (disch := field_simp_discharge) only [add_div', add_sub_sub_cancel, div_div,
    div_div_eq_mul_div, div_eq_iff, div_mul_eq_mul_div, inv_eq_one_div,
    mul_div_assoc', mul_one, mul_zero, one_mul, smul_eq_mul, sub_div', zero_add, zero_div, zero_mul]
  ring
def stereographic (hv : â€–vâ€– = 1) : PartialHomeomorph (sphere (0 : E) 1) (â„ âˆ™ v)á—® where
  toFun := stereoToFun v âˆ˜ (â†‘)
  invFun := stereoInvFun hv
  source := {âŸ¨v, by simp [hv]âŸ©}á¶œ
  target := Set.univ
  map_source' := by simp
  map_target' {w} _ := fun h => (stereoInvFun_ne_north_pole hv w) (Set.eq_of_mem_singleton h)
  left_inv' x hx := stereo_left_inv hv fun h => hx (by
    rw [â† h] at hv
    apply Subtype.ext
    dsimp
    exact h)
  right_inv' w _ := stereo_right_inv hv w
  open_source := isOpen_compl_singleton
  open_target := isOpen_univ
  continuousOn_toFun :=
    continuousOn_stereoToFun.comp continuous_subtype_val.continuousOn fun w h => by
      dsimp
      exact
        h âˆ˜ Subtype.ext âˆ˜ Eq.symm âˆ˜ (inner_eq_one_iff_of_norm_one hv (by simp)).mp
  continuousOn_invFun := (continuous_stereoInvFun hv).continuousOn
theorem stereographic_apply (hv : â€–vâ€– = 1) (x : sphere (0 : E) 1) :
    stereographic hv x = (2 / ((1 : â„) - inner v x)) â€¢ orthogonalProjection (â„ âˆ™ v)á—® x :=
  rfl
@[simp]
theorem stereographic_source (hv : â€–vâ€– = 1) : (stereographic hv).source = {âŸ¨v, by simp [hv]âŸ©}á¶œ :=
  rfl
@[simp]
theorem stereographic_target (hv : â€–vâ€– = 1) : (stereographic hv).target = Set.univ :=
  rfl
@[simp]
theorem stereographic_apply_neg (v : sphere (0 : E) 1) :
    stereographic (norm_eq_of_mem_sphere v) (-v) = 0 := by
  simp [stereographic_apply, orthogonalProjection_orthogonalComplement_singleton_eq_zero]
@[simp]
theorem stereographic_neg_apply (v : sphere (0 : E) 1) :
    stereographic (norm_eq_of_mem_sphere (-v)) v = 0 := by
  convert stereographic_apply_neg (-v)
  ext1
  simp
end StereographicProjection
section ChartedSpace
private theorem findim (n : â„•) [Fact (finrank â„ E = n + 1)] : FiniteDimensional â„ E :=
  .of_fact_finrank_eq_succ n
def stereographic' (n : â„•) [Fact (finrank â„ E = n + 1)] (v : sphere (0 : E) 1) :
    PartialHomeomorph (sphere (0 : E) 1) (EuclideanSpace â„ (Fin n)) :=
  stereographic (norm_eq_of_mem_sphere v) â‰«â‚•
    (OrthonormalBasis.fromOrthogonalSpanSingleton n
            (ne_zero_of_mem_unit_sphere v)).repr.toHomeomorph.toPartialHomeomorph
@[simp]
theorem stereographic'_source {n : â„•} [Fact (finrank â„ E = n + 1)] (v : sphere (0 : E) 1) :
    (stereographic' n v).source = {v}á¶œ := by simp [stereographic']
@[simp]
theorem stereographic'_target {n : â„•} [Fact (finrank â„ E = n + 1)] (v : sphere (0 : E) 1) :
    (stereographic' n v).target = Set.univ := by simp [stereographic']
instance EuclideanSpace.instChartedSpaceSphere {n : â„•} [Fact (finrank â„ E = n + 1)] :
    ChartedSpace (EuclideanSpace â„ (Fin n)) (sphere (0 : E) 1) where
  atlas := {f | âˆƒ v : sphere (0 : E) 1, f = stereographic' n v}
  chartAt v := stereographic' n (-v)
  mem_chart_source v := by simpa using ne_neg_of_mem_unit_sphere â„ v
  chart_mem_atlas v := âŸ¨-v, rflâŸ©
instance (n : â„•) :
    ChartedSpace (EuclideanSpace â„ (Fin n)) (sphere (0 : EuclideanSpace â„ (Fin (n + 1))) 1) :=
  have := Fact.mk (@finrank_euclideanSpace_fin â„ _ (n + 1))
  EuclideanSpace.instChartedSpaceSphere
end ChartedSpace
section SmoothManifold
open scoped InnerProductSpace
theorem sphere_ext_iff (u v : sphere (0 : E) 1) : u = v â†” âŸª(u : E), vâŸ«_â„ = 1 := by
  simp [Subtype.ext_iff, inner_eq_one_iff_of_norm_one]
theorem stereographic'_symm_apply {n : â„•} [Fact (finrank â„ E = n + 1)] (v : sphere (0 : E) 1)
    (x : EuclideanSpace â„ (Fin n)) :
    ((stereographic' n v).symm x : E) =
      let U : (â„ âˆ™ (v : E))á—® â‰ƒâ‚—áµ¢[â„] EuclideanSpace â„ (Fin n) :=
        (OrthonormalBasis.fromOrthogonalSpanSingleton n (ne_zero_of_mem_unit_sphere v)).repr
      (â€–(U.symm x : E)â€– ^ 2 + 4)â»Â¹ â€¢ (4 : â„) â€¢ (U.symm x : E) +
        (â€–(U.symm x : E)â€– ^ 2 + 4)â»Â¹ â€¢ (â€–(U.symm x : E)â€– ^ 2 - 4) â€¢ v.val := by
  simp [real_inner_comm, stereographic, stereographic', â† Submodule.coe_norm]
instance EuclideanSpace.instSmoothManifoldWithCornersSphere {n : â„•} [Fact (finrank â„ E = n + 1)] :
    SmoothManifoldWithCorners (ğ“¡ n) (sphere (0 : E) 1) :=
  smoothManifoldWithCorners_of_contDiffOn (ğ“¡ n) (sphere (0 : E) 1)
    (by
      rintro _ _ âŸ¨v, rflâŸ© âŸ¨v', rflâŸ©
      let U :=
        (
            OrthonormalBasis.fromOrthogonalSpanSingleton (ğ•œ := â„)
            n (ne_zero_of_mem_unit_sphere v)).repr
      let U' :=
        (
            OrthonormalBasis.fromOrthogonalSpanSingleton (ğ•œ := â„)
            n (ne_zero_of_mem_unit_sphere v')).repr
      have Hâ‚ := U'.contDiff.comp_contDiffOn contDiffOn_stereoToFun
      have Hâ‚‚ := (contDiff_stereoInvFunAux (v := v.val)|>.comp
        (â„ âˆ™ (v : E))á—®.subtypeL.contDiff).comp U.symm.contDiff
      convert Hâ‚.comp_inter (Hâ‚‚.contDiffOn : ContDiffOn â„ âˆ _ Set.univ) using 1
      simp only [PartialHomeomorph.trans_toPartialEquiv, PartialHomeomorph.symm_toPartialEquiv,
        PartialEquiv.trans_source, PartialEquiv.symm_source, stereographic'_target,
        stereographic'_source]
      simp only [modelWithCornersSelf_coe, modelWithCornersSelf_coe_symm, Set.preimage_id,
        Set.range_id, Set.inter_univ, Set.univ_inter, Set.compl_singleton_eq, Set.preimage_setOf_eq]
      simp only [id, comp_apply, Submodule.subtypeL_apply, PartialHomeomorph.coe_coe_symm,
        innerSL_apply, Ne, sphere_ext_iff, real_inner_comm (v' : E)]
      rfl)
instance (n : â„•) :
    SmoothManifoldWithCorners (ğ“¡ n) (sphere (0 :  EuclideanSpace â„ (Fin (n + 1))) 1) :=
  haveI := Fact.mk (@finrank_euclideanSpace_fin â„ _ (n + 1))
  EuclideanSpace.instSmoothManifoldWithCornersSphere
theorem contMDiff_coe_sphere {n : â„•} [Fact (finrank â„ E = n + 1)] :
    ContMDiff (ğ“¡ n) ğ“˜(â„, E) âŠ¤ ((â†‘) : sphere (0 : E) 1 â†’ E) := by
  have := EuclideanSpace.instSmoothManifoldWithCornersSphere (E := E) (n := n)
  rw [contMDiff_iff]
  constructor
  Â· exact continuous_subtype_val
  Â· intro v _
    let U : _ â‰ƒâ‚—áµ¢[â„] _ :=
      (
          OrthonormalBasis.fromOrthogonalSpanSingleton
          n (ne_zero_of_mem_unit_sphere (-v))).repr
    exact
      ((contDiff_stereoInvFunAux.comp (â„ âˆ™ (-v : E))á—®.subtypeL.contDiff).comp
          U.symm.contDiff).contDiffOn
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F]
variable {H : Type*} [TopologicalSpace H] {I : ModelWithCorners â„ F H}
variable {M : Type*} [TopologicalSpace M] [ChartedSpace H M] [SmoothManifoldWithCorners I M]
theorem ContMDiff.codRestrict_sphere {n : â„•} [Fact (finrank â„ E = n + 1)] {m : â„•âˆ} {f : M â†’ E}
    (hf : ContMDiff I ğ“˜(â„, E) m f) (hf' : âˆ€ x, f x âˆˆ sphere (0 : E) 1) :
    ContMDiff I (ğ“¡ n) m (Set.codRestrict _ _ hf' : M â†’ sphere (0 : E) 1) := by
  rw [contMDiff_iff_target]
  refine âŸ¨continuous_induced_rng.2 hf.continuous, ?_âŸ©
  intro v
  let U : _ â‰ƒâ‚—áµ¢[â„] _ :=
    (
        OrthonormalBasis.fromOrthogonalSpanSingleton
        n (ne_zero_of_mem_unit_sphere (-v))).repr
  have h : ContDiffOn â„ âˆ _ Set.univ := U.contDiff.contDiffOn
  have Hâ‚ := (h.comp_inter contDiffOn_stereoToFun).contMDiffOn
  have Hâ‚‚ : ContMDiffOn _ _ _ _ Set.univ := hf.contMDiffOn
  convert (Hâ‚.of_le le_top).comp' Hâ‚‚ using 1
  ext x
  have hfxv : f x = -â†‘v â†” âŸªf x, -â†‘vâŸ«_â„ = 1 := by
    have hfx : â€–f xâ€– = 1 := by simpa using hf' x
    rw [inner_eq_one_iff_of_norm_one hfx]
    exact norm_eq_of_mem_sphere (-v)
  dsimp [chartAt, Set.codRestrict, ChartedSpace.chartAt]
  simp [not_iff_not, Subtype.ext_iff, hfxv, real_inner_comm]
theorem contMDiff_neg_sphere {n : â„•} [Fact (finrank â„ E = n + 1)] :
    ContMDiff (ğ“¡ n) (ğ“¡ n) âŠ¤ fun x : sphere (0 : E) 1 => -x := by
  apply ContMDiff.codRestrict_sphere
  apply contDiff_neg.contMDiff.comp _
  exact contMDiff_coe_sphere
private lemma stereographic'_neg {n : â„•} [Fact (finrank â„ E = n + 1)] (v : sphere (0 : E) 1) :
  stereographic' n (-v) v = 0 := by
    dsimp [stereographic']
    simp only [EmbeddingLike.map_eq_zero_iff]
    apply stereographic_neg_apply
theorem range_mfderiv_coe_sphere {n : â„•} [Fact (finrank â„ E = n + 1)] (v : sphere (0 : E) 1) :
    LinearMap.range (mfderiv (ğ“¡ n) ğ“˜(â„, E) ((â†‘) : sphere (0 : E) 1 â†’ E) v :
    TangentSpace (ğ“¡ n) v â†’L[â„] E) = (â„ âˆ™ (v : E))á—® := by
  rw [((contMDiff_coe_sphere v).mdifferentiableAt le_top).mfderiv]
  dsimp [chartAt]
  simp only [chartAt, stereographic_neg_apply, fderivWithin_univ,
    LinearIsometryEquiv.toHomeomorph_symm, LinearIsometryEquiv.coe_toHomeomorph,
    LinearIsometryEquiv.map_zero, mfld_simps]
  let U := (OrthonormalBasis.fromOrthogonalSpanSingleton (ğ•œ := â„) n
    (ne_zero_of_mem_unit_sphere (-v))).repr
  suffices
      LinearMap.range (fderiv â„ ((stereoInvFunAux (-v : E) âˆ˜ (â†‘)) âˆ˜ U.symm) 0) = (â„ âˆ™ (v : E))á—® by
    convert this using 3
    apply stereographic'_neg
  have :
    HasFDerivAt (stereoInvFunAux (-v : E) âˆ˜ (Subtype.val : (â„ âˆ™ (â†‘(-v) : E))á—® â†’ E))
      (â„ âˆ™ (â†‘(-v) : E))á—®.subtypeL (U.symm 0) := by
    convert hasFDerivAt_stereoInvFunAux_comp_coe (-v : E)
    simp
  convert congrArg LinearMap.range (this.comp 0 U.symm.toContinuousLinearEquiv.hasFDerivAt).fderiv
  symm
  convert
    (U.symm : EuclideanSpace â„ (Fin n) â‰ƒâ‚—áµ¢[â„] (â„ âˆ™ (â†‘(-v) : E))á—®).range_comp
      (â„ âˆ™ (â†‘(-v) : E))á—®.subtype using 1
  simp only [Submodule.range_subtype, coe_neg_sphere]
  congr 1
  apply Submodule.span_eq_span
  Â· simp only [Set.singleton_subset_iff, SetLike.mem_coe]
    rw [â† Submodule.neg_mem_iff]
    exact Submodule.mem_span_singleton_self (-v : E)
  Â· simp only [Set.singleton_subset_iff, SetLike.mem_coe]
    rw [Submodule.neg_mem_iff]
    exact Submodule.mem_span_singleton_self (v : E)
theorem mfderiv_coe_sphere_injective {n : â„•} [Fact (finrank â„ E = n + 1)] (v : sphere (0 : E) 1) :
    Injective (mfderiv (ğ“¡ n) ğ“˜(â„, E) ((â†‘) : sphere (0 : E) 1 â†’ E) v) := by
  rw [((contMDiff_coe_sphere v).mdifferentiableAt le_top).mfderiv]
  simp only [chartAt, stereographic', stereographic_neg_apply, fderivWithin_univ,
    LinearIsometryEquiv.toHomeomorph_symm, LinearIsometryEquiv.coe_toHomeomorph,
    LinearIsometryEquiv.map_zero, mfld_simps]
  let U := (OrthonormalBasis.fromOrthogonalSpanSingleton
      (ğ•œ := â„) n (ne_zero_of_mem_unit_sphere (-v))).repr
  suffices Injective (fderiv â„ ((stereoInvFunAux (-v : E) âˆ˜ (â†‘)) âˆ˜ U.symm) 0) by
    convert this using 3
    apply stereographic'_neg
  have : HasFDerivAt (stereoInvFunAux (-v : E) âˆ˜ (Subtype.val : (â„ âˆ™ (â†‘(-v) : E))á—® â†’ E))
      (â„ âˆ™ (â†‘(-v) : E))á—®.subtypeL (U.symm 0) := by
    convert hasFDerivAt_stereoInvFunAux_comp_coe (-v : E)
    simp
  have := congr_arg DFunLike.coe <| (this.comp 0 U.symm.toContinuousLinearEquiv.hasFDerivAt).fderiv
  refine Eq.subst this.symm ?_
  rw [ContinuousLinearMap.coe_comp', ContinuousLinearEquiv.coe_coe]
  simpa [- Subtype.val_injective] using Subtype.val_injective
end SmoothManifold
section Circle
open Complex
theorem finrank_real_complex_fact' : Fact (finrank â„ â„‚ = 1 + 1) :=
  finrank_real_complex_fact
attribute [local instance] finrank_real_complex_fact'
instance : ChartedSpace (EuclideanSpace â„ (Fin 1)) Circle :=
  EuclideanSpace.instChartedSpaceSphere
instance : SmoothManifoldWithCorners (ğ“¡ 1) Circle :=
  EuclideanSpace.instSmoothManifoldWithCornersSphere (E := â„‚)
instance : LieGroup (ğ“¡ 1) Circle where
  smooth_mul := by
    apply ContMDiff.codRestrict_sphere
    let c : Circle â†’ â„‚ := (â†‘)
    have hâ‚‚ : ContMDiff (ğ“˜(â„, â„‚).prod ğ“˜(â„, â„‚)) ğ“˜(â„, â„‚) âŠ¤ fun z : â„‚ Ã— â„‚ => z.fst * z.snd := by
      rw [contMDiff_iff]
      exact âŸ¨continuous_mul, fun x y => contDiff_mul.contDiffOnâŸ©
    suffices hâ‚ : ContMDiff ((ğ“¡ 1).prod (ğ“¡ 1)) (ğ“˜(â„, â„‚).prod ğ“˜(â„, â„‚)) âŠ¤ (Prod.map c c) from
      hâ‚‚.comp hâ‚
    apply ContMDiff.prod_map <;>
    exact contMDiff_coe_sphere
  smooth_inv := by
    apply ContMDiff.codRestrict_sphere
    simp only [â† Circle.coe_inv, Circle.coe_inv_eq_conj]
    exact Complex.conjCLE.contDiff.contMDiff.comp contMDiff_coe_sphere
theorem contMDiff_circleExp : ContMDiff ğ“˜(â„, â„) (ğ“¡ 1) âŠ¤ Circle.exp :=
  (contDiff_exp.comp (contDiff_id.smul contDiff_const)).contMDiff.codRestrict_sphere _
@[deprecated (since := "2024-07-25")] alias contMDiff_expMapCircle := contMDiff_circleExp
end Circle