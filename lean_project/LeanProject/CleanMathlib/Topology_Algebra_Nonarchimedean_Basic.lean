import Mathlib.Algebra.Group.Subgroup.Basic
import Mathlib.Topology.Algebra.OpenSubgroup
import Mathlib.Topology.Algebra.Ring.Basic
open Topology
open scoped Pointwise
class NonarchimedeanAddGroup (G : Type*) [AddGroup G] [TopologicalSpace G] extends
  TopologicalAddGroup G : Prop where
  is_nonarchimedean : âˆ€ U âˆˆ ğ“ (0 : G), âˆƒ V : OpenAddSubgroup G, (V : Set G) âŠ† U
@[to_additive]
class NonarchimedeanGroup (G : Type*) [Group G] [TopologicalSpace G] extends TopologicalGroup G :
  Prop where
  is_nonarchimedean : âˆ€ U âˆˆ ğ“ (1 : G), âˆƒ V : OpenSubgroup G, (V : Set G) âŠ† U
class NonarchimedeanRing (R : Type*) [Ring R] [TopologicalSpace R] extends TopologicalRing R :
  Prop where
  is_nonarchimedean : âˆ€ U âˆˆ ğ“ (0 : R), âˆƒ V : OpenAddSubgroup R, (V : Set R) âŠ† U
instance (priority := 100) NonarchimedeanRing.to_nonarchimedeanAddGroup (R : Type*) [Ring R]
    [TopologicalSpace R] [t : NonarchimedeanRing R] : NonarchimedeanAddGroup R :=
  { t with }
namespace NonarchimedeanGroup
variable {G : Type*} [Group G] [TopologicalSpace G] [NonarchimedeanGroup G]
variable {H : Type*} [Group H] [TopologicalSpace H] [TopologicalGroup H]
variable {K : Type*} [Group K] [TopologicalSpace K] [NonarchimedeanGroup K]
@[to_additive]
theorem nonarchimedean_of_emb (f : G â†’* H) (emb : IsOpenEmbedding f) : NonarchimedeanGroup H :=
  { is_nonarchimedean := fun U hU =>
      have hâ‚ : f â»Â¹' U âˆˆ ğ“ (1 : G) := by
        apply emb.continuous.tendsto
        rwa [f.map_one]
      let âŸ¨V, hVâŸ© := is_nonarchimedean (f â»Â¹' U) hâ‚
      âŸ¨{ Subgroup.map f V with isOpen' := emb.isOpenMap _ V.isOpen }, Set.image_subset_iff.2 hVâŸ© }
@[to_additive NonarchimedeanAddGroup.prod_subset "An open neighborhood of the identity in
the cartesian product of two nonarchimedean groups contains the cartesian product of
an open neighborhood in each group."]
theorem prod_subset {U} (hU : U âˆˆ ğ“ (1 : G Ã— K)) :
    âˆƒ (V : OpenSubgroup G) (W : OpenSubgroup K), (V : Set G) Ã—Ë¢ (W : Set K) âŠ† U := by
  rw [nhds_prod_eq, Filter.mem_prod_iff] at hU
  rcases hU with âŸ¨Uâ‚, hUâ‚, Uâ‚‚, hUâ‚‚, hâŸ©
  cases' is_nonarchimedean _ hUâ‚ with V hV
  cases' is_nonarchimedean _ hUâ‚‚ with W hW
  use V; use W
  rw [Set.prod_subset_iff]
  intro x hX y hY
  exact Set.Subset.trans (Set.prod_mono hV hW) h (Set.mem_sep hX hY)
@[to_additive NonarchimedeanAddGroup.prod_self_subset "An open neighborhood of the identity in
the cartesian square of a nonarchimedean group contains the cartesian square of
an open neighborhood in the group."]
theorem prod_self_subset {U} (hU : U âˆˆ ğ“ (1 : G Ã— G)) :
    âˆƒ V : OpenSubgroup G, (V : Set G) Ã—Ë¢ (V : Set G) âŠ† U :=
  let âŸ¨V, W, hâŸ© := prod_subset hU
  âŸ¨V âŠ“ W, by refine Set.Subset.trans (Set.prod_mono ?_ ?_) â€¹_â€º <;> simpâŸ©
@[to_additive "The cartesian product of two nonarchimedean groups is nonarchimedean."]
instance : NonarchimedeanGroup (G Ã— K) where
  is_nonarchimedean _ hU :=
    let âŸ¨V, W, hâŸ© := prod_subset hU
    âŸ¨V.prod W, â€¹_â€ºâŸ©
end NonarchimedeanGroup
namespace NonarchimedeanRing
open NonarchimedeanAddGroup
variable {R S : Type*}
variable [Ring R] [TopologicalSpace R] [NonarchimedeanRing R]
variable [Ring S] [TopologicalSpace S] [NonarchimedeanRing S]
instance : NonarchimedeanRing (R Ã— S) where
  is_nonarchimedean := NonarchimedeanAddGroup.is_nonarchimedean
theorem left_mul_subset (U : OpenAddSubgroup R) (r : R) :
    âˆƒ V : OpenAddSubgroup R, r â€¢ (V : Set R) âŠ† U :=
  âŸ¨U.comap (AddMonoidHom.mulLeft r) (continuous_mul_left r), (U : Set R).image_preimage_subset _âŸ©
theorem mul_subset (U : OpenAddSubgroup R) : âˆƒ V : OpenAddSubgroup R, (V : Set R) * V âŠ† U := by
  let âŸ¨V, HâŸ© := prod_self_subset <| (U.isOpen.preimage continuous_mul).mem_nhds <| by
    simpa only [Set.mem_preimage, Prod.snd_zero, mul_zero] using U.zero_mem
  use V
  rintro v âŸ¨a, ha, b, hb, hvâŸ©
  have hy := H (Set.mk_mem_prod ha hb)
  simp only [Set.mem_preimage, SetLike.mem_coe, hv] at hy
  rw [SetLike.mem_coe]
  exact hy
end NonarchimedeanRing