import Mathlib.Analysis.Complex.CauchyIntegral
import Mathlib.Analysis.NormedSpace.Extr
import Mathlib.Data.Complex.FiniteDimensional
import Mathlib.Topology.Order.ExtrClosure
open TopologicalSpace Metric Set Filter Asymptotics Function MeasureTheory AffineMap Bornology
open scoped Topology Filter NNReal Real
universe u v w
variable {E : Type u} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E] {F : Type v} [NormedAddCommGroup F]
  [NormedSpace ‚ÑÇ F]
local postfix:100 "ÃÇ" => UniformSpace.Completion
namespace Complex
theorem norm_max_aux‚ÇÅ [CompleteSpace F] {f : ‚ÑÇ ‚Üí F} {z w : ‚ÑÇ}
    (hd : DiffContOnCl ‚ÑÇ f (ball z (dist w z)))
    (hz : IsMaxOn (norm ‚àò f) (closedBall z (dist w z)) z) : ‚Äñf w‚Äñ = ‚Äñf z‚Äñ := by
  set r : ‚Ñù := dist w z
  have hw : w ‚àà closedBall z r := mem_closedBall.2 le_rfl
  refine (isMaxOn_iff.1 hz _ hw).antisymm (not_lt.1 ?_)
  rintro hw_lt : ‚Äñf w‚Äñ < ‚Äñf z‚Äñ
  have hr : 0 < r := dist_pos.2 (ne_of_apply_ne (norm ‚àò f) hw_lt.ne)
  suffices ‚Äñ‚àÆ Œ∂ in C(z, r), (Œ∂ - z)‚Åª¬π ‚Ä¢ f Œ∂‚Äñ < 2 * œÄ * ‚Äñf z‚Äñ by
    refine this.ne ?_
    have A : (‚àÆ Œ∂ in C(z, r), (Œ∂ - z)‚Åª¬π ‚Ä¢ f Œ∂) = (2 * œÄ * I : ‚ÑÇ) ‚Ä¢ f z :=
      hd.circleIntegral_sub_inv_smul (mem_ball_self hr)
    simp [A, norm_smul, Real.pi_pos.le]
  suffices ‚Äñ‚àÆ Œ∂ in C(z, r), (Œ∂ - z)‚Åª¬π ‚Ä¢ f Œ∂‚Äñ < 2 * œÄ * r * (‚Äñf z‚Äñ / r) by
    rwa [mul_assoc, mul_div_cancel‚ÇÄ _ hr.ne'] at this
  have hsub : sphere z r ‚äÜ closedBall z r := sphere_subset_closedBall
  refine circleIntegral.norm_integral_lt_of_norm_le_const_of_lt hr ?_ ?_ ‚ü®w, rfl, ?_‚ü©
  ¬∑ show ContinuousOn (fun Œ∂ : ‚ÑÇ => (Œ∂ - z)‚Åª¬π ‚Ä¢ f Œ∂) (sphere z r)
    refine ((continuousOn_id.sub continuousOn_const).inv‚ÇÄ ?_).smul (hd.continuousOn_ball.mono hsub)
    exact fun Œ∂ hŒ∂ => sub_ne_zero.2 (ne_of_mem_sphere hŒ∂ hr.ne')
  ¬∑ show ‚àÄ Œ∂ ‚àà sphere z r, ‚Äñ(Œ∂ - z)‚Åª¬π ‚Ä¢ f Œ∂‚Äñ ‚â§ ‚Äñf z‚Äñ / r
    rintro Œ∂ (hŒ∂ : abs (Œ∂ - z) = r)
    rw [le_div_iff‚ÇÄ hr, norm_smul, norm_inv, norm_eq_abs, hŒ∂, mul_comm, mul_inv_cancel_left‚ÇÄ hr.ne']
    exact hz (hsub hŒ∂)
  show ‚Äñ(w - z)‚Åª¬π ‚Ä¢ f w‚Äñ < ‚Äñf z‚Äñ / r
  rw [norm_smul, norm_inv, norm_eq_abs, ‚Üê div_eq_inv_mul]
  exact (div_lt_div_iff_of_pos_right hr).2 hw_lt
theorem norm_max_aux‚ÇÇ {f : ‚ÑÇ ‚Üí F} {z w : ‚ÑÇ} (hd : DiffContOnCl ‚ÑÇ f (ball z (dist w z)))
    (hz : IsMaxOn (norm ‚àò f) (closedBall z (dist w z)) z) : ‚Äñf w‚Äñ = ‚Äñf z‚Äñ := by
  set e : F ‚ÜíL[‚ÑÇ] FÃÇ := UniformSpace.Completion.toComplL
  have he : ‚àÄ x, ‚Äñe x‚Äñ = ‚Äñx‚Äñ := UniformSpace.Completion.norm_coe
  replace hz : IsMaxOn (norm ‚àò e ‚àò f) (closedBall z (dist w z)) z := by
    simpa only [IsMaxOn, Function.comp_def, he] using hz
  simpa only [he, Function.comp_def]
    using norm_max_aux‚ÇÅ (e.differentiable.comp_diffContOnCl hd) hz
theorem norm_max_aux‚ÇÉ {f : ‚ÑÇ ‚Üí F} {z w : ‚ÑÇ} {r : ‚Ñù} (hr : dist w z = r)
    (hd : DiffContOnCl ‚ÑÇ f (ball z r)) (hz : IsMaxOn (norm ‚àò f) (ball z r) z) : ‚Äñf w‚Äñ = ‚Äñf z‚Äñ := by
  subst r
  rcases eq_or_ne w z with (rfl | hne); ¬∑ rfl
  rw [‚Üê dist_ne_zero] at hne
  exact norm_max_aux‚ÇÇ hd (closure_ball z hne ‚ñ∏ hz.closure hd.continuousOn.norm)
theorem norm_eqOn_closedBall_of_isMaxOn {f : E ‚Üí F} {z : E} {r : ‚Ñù}
    (hd : DiffContOnCl ‚ÑÇ f (ball z r)) (hz : IsMaxOn (norm ‚àò f) (ball z r) z) :
    EqOn (norm ‚àò f) (const E ‚Äñf z‚Äñ) (closedBall z r) := by
  intro w hw
  rw [mem_closedBall, dist_comm] at hw
  rcases eq_or_ne z w with (rfl | hne); ¬∑ rfl
  set e := (lineMap z w : ‚ÑÇ ‚Üí E)
  have hde : Differentiable ‚ÑÇ e := (differentiable_id.smul_const (w - z)).add_const z
  suffices ‚Äñ(f ‚àò e) (1 : ‚ÑÇ)‚Äñ = ‚Äñ(f ‚àò e) (0 : ‚ÑÇ)‚Äñ by simpa [e]
  have hr : dist (1 : ‚ÑÇ) 0 = 1 := by simp
  have hball : MapsTo e (ball 0 1) (ball z r) := by
    refine ((lipschitzWith_lineMap z w).mapsTo_ball (mt nndist_eq_zero.1 hne) 0 1).mono
      Subset.rfl ?_
    simpa only [lineMap_apply_zero, mul_one, coe_nndist] using ball_subset_ball hw
  exact norm_max_aux‚ÇÉ hr (hd.comp hde.diffContOnCl hball)
      (hz.comp_mapsTo hball (lineMap_apply_zero z w))
theorem norm_eq_norm_of_isMaxOn_of_ball_subset {f : E ‚Üí F} {s : Set E} {z w : E}
    (hd : DiffContOnCl ‚ÑÇ f s) (hz : IsMaxOn (norm ‚àò f) s z) (hsub : ball z (dist w z) ‚äÜ s) :
    ‚Äñf w‚Äñ = ‚Äñf z‚Äñ :=
  norm_eqOn_closedBall_of_isMaxOn (hd.mono hsub) (hz.on_subset hsub) (mem_closedBall.2 le_rfl)
theorem norm_eventually_eq_of_isLocalMax {f : E ‚Üí F} {c : E}
    (hd : ‚àÄ·∂† z in ùìù c, DifferentiableAt ‚ÑÇ f z) (hc : IsLocalMax (norm ‚àò f) c) :
    ‚àÄ·∂† y in ùìù c, ‚Äñf y‚Äñ = ‚Äñf c‚Äñ := by
  rcases nhds_basis_closedBall.eventually_iff.1 (hd.and hc) with ‚ü®r, hr‚ÇÄ, hr‚ü©
  exact nhds_basis_closedBall.eventually_iff.2
    ‚ü®r, hr‚ÇÄ, norm_eqOn_closedBall_of_isMaxOn (DifferentiableOn.diffContOnCl fun x hx =>
        (hr <| closure_ball_subset_closedBall hx).1.differentiableWithinAt) fun x hx =>
      (hr <| ball_subset_closedBall hx).2‚ü©
theorem isOpen_setOf_mem_nhds_and_isMaxOn_norm {f : E ‚Üí F} {s : Set E}
    (hd : DifferentiableOn ‚ÑÇ f s) : IsOpen {z | s ‚àà ùìù z ‚àß IsMaxOn (norm ‚àò f) s z} := by
  refine isOpen_iff_mem_nhds.2 fun z hz => (eventually_eventually_nhds.2 hz.1).and ?_
  replace hd : ‚àÄ·∂† w in ùìù z, DifferentiableAt ‚ÑÇ f w := hd.eventually_differentiableAt hz.1
  exact (norm_eventually_eq_of_isLocalMax hd <| hz.2.isLocalMax hz.1).mono fun x hx y hy =>
    le_trans (hz.2 hy).out hx.ge
theorem norm_eqOn_of_isPreconnected_of_isMaxOn {f : E ‚Üí F} {U : Set E} {c : E}
    (hc : IsPreconnected U) (ho : IsOpen U) (hd : DifferentiableOn ‚ÑÇ f U) (hcU : c ‚àà U)
    (hm : IsMaxOn (norm ‚àò f) U c) : EqOn (norm ‚àò f) (const E ‚Äñf c‚Äñ) U := by
  set V := U ‚à© {z | IsMaxOn (norm ‚àò f) U z}
  have hV : ‚àÄ x ‚àà V, ‚Äñf x‚Äñ = ‚Äñf c‚Äñ := fun x hx => le_antisymm (hm hx.1) (hx.2 hcU)
  suffices U ‚äÜ V from fun x hx => hV x (this hx)
  have hVo : IsOpen V := by
    simpa only [ho.mem_nhds_iff, setOf_and, setOf_mem_eq]
      using isOpen_setOf_mem_nhds_and_isMaxOn_norm hd
  have hVne : (U ‚à© V).Nonempty := ‚ü®c, hcU, hcU, hm‚ü©
  set W := U ‚à© {z | ‚Äñf z‚Äñ ‚â† ‚Äñf c‚Äñ}
  have hWo : IsOpen W := hd.continuousOn.norm.isOpen_inter_preimage ho isOpen_ne
  have hdVW : Disjoint V W := disjoint_left.mpr fun x hxV hxW => hxW.2 (hV x hxV)
  have hUVW : U ‚äÜ V ‚à™ W := fun x hx =>
    (eq_or_ne ‚Äñf x‚Äñ ‚Äñf c‚Äñ).imp (fun h => ‚ü®hx, fun y hy => (hm hy).out.trans_eq h.symm‚ü©)
      (And.intro hx)
  exact hc.subset_left_of_subset_union hVo hWo hdVW hUVW hVne
theorem norm_eqOn_closure_of_isPreconnected_of_isMaxOn {f : E ‚Üí F} {U : Set E} {c : E}
    (hc : IsPreconnected U) (ho : IsOpen U) (hd : DiffContOnCl ‚ÑÇ f U) (hcU : c ‚àà U)
    (hm : IsMaxOn (norm ‚àò f) U c) : EqOn (norm ‚àò f) (const E ‚Äñf c‚Äñ) (closure U) :=
  (norm_eqOn_of_isPreconnected_of_isMaxOn hc ho hd.differentiableOn hcU hm).of_subset_closure
    hd.continuousOn.norm continuousOn_const subset_closure Subset.rfl
section StrictConvex
variable [StrictConvexSpace ‚Ñù F]
theorem eqOn_of_isPreconnected_of_isMaxOn_norm {f : E ‚Üí F} {U : Set E} {c : E}
    (hc : IsPreconnected U) (ho : IsOpen U) (hd : DifferentiableOn ‚ÑÇ f U) (hcU : c ‚àà U)
    (hm : IsMaxOn (norm ‚àò f) U c) : EqOn f (const E (f c)) U := fun x hx =>
  have H‚ÇÅ : ‚Äñf x‚Äñ = ‚Äñf c‚Äñ := norm_eqOn_of_isPreconnected_of_isMaxOn hc ho hd hcU hm hx
  have H‚ÇÇ : ‚Äñf x + f c‚Äñ = ‚Äñf c + f c‚Äñ :=
    norm_eqOn_of_isPreconnected_of_isMaxOn hc ho (hd.add_const _) hcU hm.norm_add_self hx
  eq_of_norm_eq_of_norm_add_eq H‚ÇÅ <| by simp only [H‚ÇÇ, SameRay.rfl.norm_add, H‚ÇÅ, Function.const]
theorem eqOn_closure_of_isPreconnected_of_isMaxOn_norm {f : E ‚Üí F} {U : Set E} {c : E}
    (hc : IsPreconnected U) (ho : IsOpen U) (hd : DiffContOnCl ‚ÑÇ f U) (hcU : c ‚àà U)
    (hm : IsMaxOn (norm ‚àò f) U c) : EqOn f (const E (f c)) (closure U) :=
  (eqOn_of_isPreconnected_of_isMaxOn_norm hc ho hd.differentiableOn hcU hm).of_subset_closure
    hd.continuousOn continuousOn_const subset_closure Subset.rfl
theorem eq_of_isMaxOn_of_ball_subset {f : E ‚Üí F} {s : Set E} {z w : E} (hd : DiffContOnCl ‚ÑÇ f s)
    (hz : IsMaxOn (norm ‚àò f) s z) (hsub : ball z (dist w z) ‚äÜ s) : f w = f z :=
  have H‚ÇÅ : ‚Äñf w‚Äñ = ‚Äñf z‚Äñ := norm_eq_norm_of_isMaxOn_of_ball_subset hd hz hsub
  have H‚ÇÇ : ‚Äñf w + f z‚Äñ = ‚Äñf z + f z‚Äñ :=
    norm_eq_norm_of_isMaxOn_of_ball_subset (hd.add_const _) hz.norm_add_self hsub
  eq_of_norm_eq_of_norm_add_eq H‚ÇÅ <| by simp only [H‚ÇÇ, SameRay.rfl.norm_add, H‚ÇÅ]
theorem eqOn_closedBall_of_isMaxOn_norm {f : E ‚Üí F} {z : E} {r : ‚Ñù}
    (hd : DiffContOnCl ‚ÑÇ f (ball z r)) (hz : IsMaxOn (norm ‚àò f) (ball z r) z) :
    EqOn f (const E (f z)) (closedBall z r) := fun _x hx =>
  eq_of_isMaxOn_of_ball_subset hd hz <| ball_subset_ball hx
lemma eq_const_of_exists_max {f : E ‚Üí F} {b : ‚Ñù} (h_an : DifferentiableOn ‚ÑÇ f (ball 0 b))
    {v : E} (hv : v ‚àà ball 0 b) (hv_max : IsMaxOn (norm ‚àò f) (ball 0 b) v) :
    Set.EqOn f (Function.const E (f v)) (ball 0 b) :=
  Complex.eqOn_of_isPreconnected_of_isMaxOn_norm (convex_ball 0 b).isPreconnected
    isOpen_ball h_an hv hv_max
lemma eq_const_of_exists_le [ProperSpace E] {f : E ‚Üí F} {r b : ‚Ñù}
    (h_an : DifferentiableOn ‚ÑÇ f (ball 0 b)) (hr_nn : 0 ‚â§ r) (hr_lt : r < b)
    (hr : ‚àÄ z, z ‚àà (ball 0 b) ‚Üí ‚àÉ w, w ‚àà closedBall 0 r ‚àß ‚Äñf z‚Äñ ‚â§ ‚Äñf w‚Äñ) :
    Set.EqOn f (Function.const E (f 0)) (ball 0 b) := by
  obtain ‚ü®x, hx_mem, hx_max‚ü© := isCompact_closedBall (0 : E) r |>.exists_isMaxOn
    (nonempty_closedBall.mpr hr_nn)
    (h_an.continuousOn.mono <| closedBall_subset_ball hr_lt).norm
  suffices Set.EqOn f (Function.const E (f x)) (ball 0 b) by
    rwa [this (mem_ball_self (hr_nn.trans_lt hr_lt))]
  apply eq_const_of_exists_max h_an (closedBall_subset_ball hr_lt hx_mem) (fun z hz ‚Ü¶ ?_)
  obtain ‚ü®w, hw, hw'‚ü© := hr z hz
  exact hw'.trans (hx_max hw)
theorem eventually_eq_of_isLocalMax_norm {f : E ‚Üí F} {c : E}
    (hd : ‚àÄ·∂† z in ùìù c, DifferentiableAt ‚ÑÇ f z) (hc : IsLocalMax (norm ‚àò f) c) :
    ‚àÄ·∂† y in ùìù c, f y = f c := by
  rcases nhds_basis_closedBall.eventually_iff.1 (hd.and hc) with ‚ü®r, hr‚ÇÄ, hr‚ü©
  exact nhds_basis_closedBall.eventually_iff.2
    ‚ü®r, hr‚ÇÄ, eqOn_closedBall_of_isMaxOn_norm (DifferentiableOn.diffContOnCl fun x hx =>
        (hr <| closure_ball_subset_closedBall hx).1.differentiableWithinAt) fun x hx =>
      (hr <| ball_subset_closedBall hx).2‚ü©
theorem eventually_eq_or_eq_zero_of_isLocalMin_norm {f : E ‚Üí ‚ÑÇ} {c : E}
    (hf : ‚àÄ·∂† z in ùìù c, DifferentiableAt ‚ÑÇ f z) (hc : IsLocalMin (norm ‚àò f) c) :
    (‚àÄ·∂† z in ùìù c, f z = f c) ‚à® f c = 0 := by
  refine or_iff_not_imp_right.mpr fun h => ?_
  have h1 : ‚àÄ·∂† z in ùìù c, f z ‚â† 0 := hf.self_of_nhds.continuousAt.eventually_ne h
  have h2 : IsLocalMax (norm ‚àò f)‚Åª¬π c := hc.inv (h1.mono fun z => norm_pos_iff.mpr)
  have h3 : IsLocalMax (norm ‚àò f‚Åª¬π) c := by refine h2.congr (Eventually.of_forall ?_); simp
  have h4 : ‚àÄ·∂† z in ùìù c, DifferentiableAt ‚ÑÇ f‚Åª¬π z := by filter_upwards [hf, h1] with z h using h.inv
  filter_upwards [eventually_eq_of_isLocalMax_norm h4 h3] with z using inv_inj.mp
end StrictConvex
variable [Nontrivial E]
theorem exists_mem_frontier_isMaxOn_norm [FiniteDimensional ‚ÑÇ E] {f : E ‚Üí F} {U : Set E}
    (hb : IsBounded U) (hne : U.Nonempty) (hd : DiffContOnCl ‚ÑÇ f U) :
    ‚àÉ z ‚àà frontier U, IsMaxOn (norm ‚àò f) (closure U) z := by
  have hc : IsCompact (closure U) := hb.isCompact_closure
  obtain ‚ü®w, hwU, hle‚ü© : ‚àÉ w ‚àà closure U, IsMaxOn (norm ‚àò f) (closure U) w :=
    hc.exists_isMaxOn hne.closure hd.continuousOn.norm
  rw [closure_eq_interior_union_frontier, mem_union] at hwU
  cases' hwU with hwU hwU; rotate_left; ¬∑ exact ‚ü®w, hwU, hle‚ü©
  have : interior U ‚â† univ := ne_top_of_le_ne_top hc.ne_univ interior_subset_closure
  rcases exists_mem_frontier_infDist_compl_eq_dist hwU this with ‚ü®z, hzU, hzw‚ü©
  refine ‚ü®z, frontier_interior_subset hzU, fun x hx => (hle hx).out.trans_eq ?_‚ü©
  refine (norm_eq_norm_of_isMaxOn_of_ball_subset hd (hle.on_subset subset_closure) ?_).symm
  rw [dist_comm, ‚Üê hzw]
  exact ball_infDist_compl_subset.trans interior_subset
theorem norm_le_of_forall_mem_frontier_norm_le {f : E ‚Üí F} {U : Set E} (hU : IsBounded U)
    (hd : DiffContOnCl ‚ÑÇ f U) {C : ‚Ñù} (hC : ‚àÄ z ‚àà frontier U, ‚Äñf z‚Äñ ‚â§ C) {z : E}
    (hz : z ‚àà closure U) : ‚Äñf z‚Äñ ‚â§ C := by
  rw [closure_eq_self_union_frontier, union_comm, mem_union] at hz
  cases' hz with hz hz; ¬∑ exact hC z hz
  rcases exists_ne z with ‚ü®w, hne‚ü©
  set e := (lineMap z w : ‚ÑÇ ‚Üí E)
  have hde : Differentiable ‚ÑÇ e := (differentiable_id.smul_const (w - z)).add_const z
  have hL : AntilipschitzWith (nndist z w)‚Åª¬π e := antilipschitzWith_lineMap hne.symm
  replace hd : DiffContOnCl ‚ÑÇ (f ‚àò e) (e ‚Åª¬π' U) :=
    hd.comp hde.diffContOnCl (mapsTo_preimage _ _)
  have h‚ÇÄ : (0 : ‚ÑÇ) ‚àà e ‚Åª¬π' U := by simpa only [e, mem_preimage, lineMap_apply_zero]
  rcases exists_mem_frontier_isMaxOn_norm (hL.isBounded_preimage hU) ‚ü®0, h‚ÇÄ‚ü© hd with ‚ü®Œ∂, hŒ∂U, hŒ∂‚ü©
  calc
    ‚Äñf z‚Äñ = ‚Äñf (e 0)‚Äñ := by simp only [e, lineMap_apply_zero]
    _ ‚â§ ‚Äñf (e Œ∂)‚Äñ := hŒ∂ (subset_closure h‚ÇÄ)
    _ ‚â§ C := hC _ (hde.continuous.frontier_preimage_subset _ hŒ∂U)
theorem eqOn_closure_of_eqOn_frontier {f g : E ‚Üí F} {U : Set E} (hU : IsBounded U)
    (hf : DiffContOnCl ‚ÑÇ f U) (hg : DiffContOnCl ‚ÑÇ g U) (hfg : EqOn f g (frontier U)) :
    EqOn f g (closure U) := by
  suffices H : ‚àÄ z ‚àà closure U, ‚Äñ(f - g) z‚Äñ ‚â§ 0 by simpa [sub_eq_zero] using H
  refine fun z hz => norm_le_of_forall_mem_frontier_norm_le hU (hf.sub hg) (fun w hw => ?_) hz
  simp [hfg hw]
theorem eqOn_of_eqOn_frontier {f g : E ‚Üí F} {U : Set E} (hU : IsBounded U) (hf : DiffContOnCl ‚ÑÇ f U)
    (hg : DiffContOnCl ‚ÑÇ g U) (hfg : EqOn f g (frontier U)) : EqOn f g U :=
  (eqOn_closure_of_eqOn_frontier hU hf hg hfg).mono subset_closure
end Complex