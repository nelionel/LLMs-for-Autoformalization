import Mathlib.Analysis.SpecialFunctions.Complex.LogBounds
import Mathlib.NumberTheory.Harmonic.ZetaAsymp
import Mathlib.NumberTheory.LSeries.Dirichlet
import Mathlib.NumberTheory.LSeries.DirichletContinuation
import Mathlib.NumberTheory.LSeries.Positivity
open Complex Asymptotics Topology Filter
open ArithmeticFunction hiding log
open scoped ComplexOrder
variable {N : â„•}
namespace DirichletCharacter
section quadratic
def zetaMul (Ï‡ : DirichletCharacter â„‚ N) : ArithmeticFunction â„‚ :=
  .zeta * toArithmeticFunction (Ï‡ Â·)
lemma isMultiplicative_zetaMul (Ï‡ : DirichletCharacter â„‚ N) : Ï‡.zetaMul.IsMultiplicative :=
  isMultiplicative_zeta.natCast.mul <| isMultiplicative_toArithmeticFunction Ï‡
lemma LSeriesSummable_zetaMul (Ï‡ : DirichletCharacter â„‚ N) {s : â„‚} (hs : 1 < s.re) :
    LSeriesSummable Ï‡.zetaMul s := by
  refine ArithmeticFunction.LSeriesSummable_mul (LSeriesSummable_zeta_iff.mpr hs) <|
    LSeriesSummable_of_bounded_of_one_lt_re (m := 1) (fun n hn â†¦ ?_) hs
  simpa only [toArithmeticFunction, coe_mk, hn, â†“reduceIte, â† Complex.norm_eq_abs]
  using norm_le_one Ï‡ _
lemma zetaMul_prime_pow_nonneg {Ï‡ : DirichletCharacter â„‚ N} (hÏ‡ : Ï‡ ^ 2 = 1) {p : â„•}
    (hp : p.Prime) (k : â„•) :
    0 â‰¤ zetaMul Ï‡ (p ^ k) := by
  simp only [zetaMul, toArithmeticFunction, coe_zeta_mul_apply, coe_mk,
    Nat.sum_divisors_prime_pow hp, pow_eq_zero_iff', hp.ne_zero, ne_eq, false_and, â†“reduceIte,
    Nat.cast_pow, map_pow]
  rcases MulChar.isQuadratic_iff_sq_eq_one.mpr hÏ‡ p with h | h | h
  Â· refine Finset.sum_nonneg fun i _ â†¦ ?_
    simp only [h, le_refl, pow_nonneg]
  Â· refine Finset.sum_nonneg fun i _ â†¦ ?_
    simp only [h, one_pow, zero_le_one]
  Â· simp only [h, neg_one_geom_sum]
    split_ifs
    exacts [le_rfl, zero_le_one]
lemma zetaMul_nonneg {Ï‡ : DirichletCharacter â„‚ N} (hÏ‡ : Ï‡ ^ 2 = 1) (n : â„•) :
    0 â‰¤ zetaMul Ï‡ n := by
  rcases eq_or_ne n 0 with rfl | hn
  Â· simp only [ArithmeticFunction.map_zero, le_refl]
  Â· simpa only [Ï‡.isMultiplicative_zetaMul.multiplicative_factorization _ hn] using
      Finset.prod_nonneg
        fun p hp â†¦ zetaMul_prime_pow_nonneg hÏ‡ (Nat.prime_of_mem_primeFactors hp) _
private structure BadChar (N : â„•) [NeZero N] where
  Ï‡ : DirichletCharacter â„‚ N
  Ï‡_ne : Ï‡ â‰  1
  Ï‡_sq : Ï‡ ^ 2 = 1
  hÏ‡ : Ï‡.LFunction 1 = 0
variable [NeZero N]
namespace BadChar
private noncomputable
def F (B : BadChar N) : â„‚ â†’ â„‚ :=
  Function.update (fun s : â„‚ â†¦ riemannZeta s * LFunction B.Ï‡ s) 1 (deriv (LFunction B.Ï‡) 1)
private lemma F_differentiableAt_of_ne (B : BadChar N) {s : â„‚} (hs : s â‰  1) :
    DifferentiableAt â„‚ B.F s := by
  apply DifferentiableAt.congr_of_eventuallyEq
  Â· exact (differentiableAt_riemannZeta hs).mul <| differentiableAt_LFunction B.Ï‡ s (.inl hs)
  Â· filter_upwards [eventually_ne_nhds hs] with t ht using Function.update_noteq ht ..
private lemma F_eq_LSeries (B : BadChar N) {s : â„‚} (hs : 1 < s.re) :
    B.F s = LSeries B.Ï‡.zetaMul s := by
  rw [F, zetaMul, â† coe_mul, LSeries_convolution']
  Â· have hs' : s â‰  1 := fun h â†¦ by simp only [h, one_re, lt_self_iff_false] at hs
    simp only [ne_eq, hs', not_false_eq_true, Function.update_noteq, B.Ï‡.LFunction_eq_LSeries hs]
    congr 1
    Â· simp_rw [â† LSeries_zeta_eq_riemannZeta hs, â† natCoe_apply]
    Â· exact LSeries_congr s B.Ï‡.apply_eq_toArithmeticFunction_apply
  Â· exact LSeriesSummable_zeta_iff.mpr hs
  Â· exact (LSeriesSummable_congr _ fun h â†¦ (B.Ï‡.apply_eq_toArithmeticFunction_apply h).symm).mpr <|
      ZMod.LSeriesSummable_of_one_lt_re B.Ï‡ hs
private lemma F_differentiable (B : BadChar N) : Differentiable â„‚ B.F := by
  intro s
  rcases ne_or_eq s 1 with hs | rfl
  Â· exact B.F_differentiableAt_of_ne hs
  refine (analyticAt_of_differentiable_on_punctured_nhds_of_continuousAt ?_ ?_).differentiableAt
  Â· filter_upwards [self_mem_nhdsWithin] with t ht
    exact B.F_differentiableAt_of_ne ht
  let G := Function.update (fun s â†¦ (s - 1) * riemannZeta s) 1 1
  let H := Function.update (fun s â†¦ (B.Ï‡.LFunction s - B.Ï‡.LFunction 1) / (s - 1)) 1
    (deriv B.Ï‡.LFunction 1)
  have : B.F = G * H := by
    ext1 t
    rcases eq_or_ne t 1 with rfl | ht
    Â· simp only [F, G, H, Pi.mul_apply, one_mul, Function.update_same]
    Â· simp only [F, G, H, Function.update_noteq ht, mul_comm _ (riemannZeta _), B.hÏ‡, sub_zero,
      Pi.mul_apply, mul_assoc, mul_div_cancelâ‚€ _ (sub_ne_zero.mpr ht)]
  rw [this]
  apply ContinuousAt.mul
  Â· simpa only [G, continuousAt_update_same] using riemannZeta_residue_one
  Â· exact (B.Ï‡.differentiableAt_LFunction 1 (.inr B.Ï‡_ne)).hasDerivAt.continuousAt_div
private lemma F_neg_two (B : BadChar N) : B.F (-2 : â„) = 0 := by
  have := riemannZeta_neg_two_mul_nat_add_one 0
  rw [Nat.cast_zero, zero_add, mul_one] at this
  rw [F, ofReal_neg, ofReal_ofNat, Function.update_noteq (mod_cast (by omega : (-2 : â„¤) â‰  1)),
    this, zero_mul]
end BadChar
private theorem LFunction_apply_one_ne_zero_of_quadratic {Ï‡ : DirichletCharacter â„‚ N}
    (hÏ‡ : Ï‡ ^ 2 = 1) (Ï‡_ne : Ï‡ â‰  1) :
    Ï‡.LFunction 1 â‰  0 := by
  intro hL
  let B : BadChar N := {Ï‡ := Ï‡, Ï‡_sq := hÏ‡, hÏ‡ := hL, Ï‡_ne := Ï‡_ne}
  refine B.F_neg_two.not_gt ?_
  refine ArithmeticFunction.LSeries_positive_of_differentiable_of_eqOn (zetaMul_nonneg hÏ‡)
    (Ï‡.isMultiplicative_zetaMul.map_one â–¸ zero_lt_one) B.F_differentiable ?_
    (fun _ â†¦ B.F_eq_LSeries) _
  exact LSeries.abscissaOfAbsConv_le_of_forall_lt_LSeriesSummable
    fun _ a â†¦ Ï‡.LSeriesSummable_zetaMul a
end quadratic
section nonvanishing
variable (Ï‡ : DirichletCharacter â„‚ N)
private lemma re_log_comb_nonneg' {a : â„} (haâ‚€ : 0 â‰¤ a) (haâ‚ : a < 1) {z : â„‚} (hz : â€–zâ€– = 1) :
      0 â‰¤ 3 * (-log (1 - a)).re + 4 * (-log (1 - a * z)).re + (-log (1 - a * z ^ 2)).re := by
  have hacâ‚€ : â€–(a : â„‚)â€– < 1 := by
    simp only [norm_eq_abs, abs_ofReal, _root_.abs_of_nonneg haâ‚€, haâ‚]
  have hacâ‚ : â€–a * zâ€– < 1 := by rwa [norm_mul, hz, mul_one]
  have hacâ‚‚ : â€–a * z ^ 2â€– < 1 := by rwa [norm_mul, norm_pow, hz, one_pow, mul_one]
  rw [â† ((hasSum_re <| hasSum_taylorSeries_neg_log hacâ‚€).mul_left 3).add
    ((hasSum_re <| hasSum_taylorSeries_neg_log hacâ‚).mul_left 4) |>.add
    (hasSum_re <| hasSum_taylorSeries_neg_log hacâ‚‚) |>.tsum_eq]
  refine tsum_nonneg fun n â†¦ ?_
  simp only [â† ofReal_pow, div_natCast_re, ofReal_re, mul_pow, mul_re, ofReal_im, zero_mul,
    sub_zero]
  rcases n.eq_zero_or_pos with rfl | hn
  Â· simp only [pow_zero, Nat.cast_zero, div_zero, mul_zero, one_re, mul_one, add_zero, le_refl]
  Â· simp only [â† mul_div_assoc, â† add_div]
    refine div_nonneg ?_ n.cast_nonneg
    rw [â† pow_mul, pow_mul', sq, mul_re, â† sq, â† sq, â† sq_abs_sub_sq_re, â† norm_eq_abs, norm_pow,
      hz]
    convert (show 0 â‰¤ 2 * a ^ n * ((z ^ n).re + 1) ^ 2 by positivity) using 1
    ring
private lemma re_log_comb_nonneg {n : â„•} (hn : 2 â‰¤ n) {x : â„} (hx : 1 < x) (y : â„) :
    0 â‰¤ 3 * (-log (1 - (1 : DirichletCharacter â„‚ N) n * n ^ (-x : â„‚))).re +
          4 * (-log (1 - Ï‡ n * n ^ (-(x + I * y)))).re +
          (-log (1 - (Ï‡ n ^ 2) * n ^ (-(x + 2 * I * y)))).re := by
  by_cases hn' : IsUnit (n : ZMod N)
  Â· have haâ‚€ : 0 â‰¤ (n : â„) ^ (-x) := Real.rpow_nonneg n.cast_nonneg _
    have haâ‚ : (n : â„) ^ (-x) < 1 := by
      rw [Real.rpow_neg (Nat.cast_nonneg n), inv_lt_one_iffâ‚€]
      exact .inr <| Real.one_lt_rpow (mod_cast one_lt_two.trans_le hn) <| zero_lt_one.trans hx
    have hz : â€–Ï‡ n * (n : â„‚) ^ (-(I * y))â€– = 1 := by
      rw [norm_mul, â† hn'.unit_spec, DirichletCharacter.unit_norm_eq_one Ï‡ hn'.unit,
        norm_eq_abs, â† ofReal_natCast, abs_cpow_eq_rpow_re_of_pos (mod_cast by omega)]
      simp only [neg_re, mul_re, I_re, ofReal_re, zero_mul, I_im, ofReal_im, mul_zero, sub_self,
        neg_zero, Real.rpow_zero, one_mul]
    rw [MulChar.one_apply hn', one_mul]
    convert re_log_comb_nonneg' haâ‚€ haâ‚ hz using 6
    Â· simp only [ofReal_cpow n.cast_nonneg (-x), ofReal_natCast, ofReal_neg]
    Â· congr 2
      rw [neg_add, cpow_add _ _ <| mod_cast by omega, â† ofReal_neg, ofReal_cpow n.cast_nonneg (-x),
        ofReal_natCast, mul_left_comm]
    Â· rw [neg_add, cpow_add _ _ <| mod_cast by omega, â† ofReal_neg, ofReal_cpow n.cast_nonneg (-x),
        ofReal_natCast, show -(2 * I * y) = (2 : â„•) * -(I * y) by ring, cpow_nat_mul, mul_pow,
        mul_left_comm]
  Â· simp only [MulChar.map_nonunit _ hn', zero_mul, sub_zero, log_one, neg_zero, zero_re, mul_zero,
      neg_add_rev, add_zero, pow_two, le_refl]
lemma summable_neg_log_one_sub_mul_prime_cpow {s : â„‚} (hs : 1 < s.re) :
    Summable fun p : Nat.Primes â†¦ -log (1 - Ï‡ p * (p : â„‚) ^ (-s)) := by
  have (p : Nat.Primes) : â€–Ï‡ p * (p : â„‚) ^ (-s)â€– â‰¤ (p : â„) ^ (-s).re := by
    simpa only [norm_mul, norm_natCast_cpow_of_re_ne_zero _ <| re_neg_ne_zero_of_one_lt_re hs]
      using mul_le_of_le_one_left (by positivity) (Ï‡.norm_le_one _)
  refine (Nat.Primes.summable_rpow.mpr ?_).of_nonneg_of_le (fun _ â†¦ norm_nonneg _) this
    |>.of_norm.clog_one_sub.neg
  simp only [neg_re, neg_lt_neg_iff, hs]
private lemma one_lt_re_one_add {x : â„} (hx : 0 < x) (y : â„) :
    1 < (1 + x : â„‚).re âˆ§ 1 < (1 + x + I * y).re âˆ§ 1 < (1 + x + 2 * I * y).re := by
  simp only [add_re, one_re, ofReal_re, lt_add_iff_pos_right, hx, mul_re, I_re, zero_mul, I_im,
    ofReal_im, mul_zero, sub_self, add_zero, re_ofNat, im_ofNat, mul_one, mul_im, and_self]
open scoped LSeries.notation in
lemma norm_LSeries_product_ge_one {x : â„} (hx : 0 < x) (y : â„) :
    â€–L â†—(1 : DirichletCharacter â„‚ N) (1 + x) ^ 3 * L â†—Ï‡ (1 + x + I * y) ^ 4 *
      L â†—(Ï‡ ^ 2 :) (1 + x + 2 * I * y)â€– â‰¥ 1 := by
  have âŸ¨hâ‚€, hâ‚, hâ‚‚âŸ© := one_lt_re_one_add hx y
  have Hâ‚€ := summable_neg_log_one_sub_mul_prime_cpow (N := N) 1 hâ‚€
  have Hâ‚ := summable_neg_log_one_sub_mul_prime_cpow Ï‡ hâ‚
  have Hâ‚‚ := summable_neg_log_one_sub_mul_prime_cpow (Ï‡ ^ 2) hâ‚‚
  have hsumâ‚€ := (hasSum_re Hâ‚€.hasSum).summable.mul_left 3
  have hsumâ‚ := (hasSum_re Hâ‚.hasSum).summable.mul_left 4
  have hsumâ‚‚ := (hasSum_re Hâ‚‚.hasSum).summable
  rw [â† LSeries_eulerProduct_exp_log _ hâ‚€, â† LSeries_eulerProduct_exp_log Ï‡ hâ‚,
    â† LSeries_eulerProduct_exp_log _ hâ‚‚]
  simp only [â† exp_nat_mul, Nat.cast_ofNat, â† exp_add, norm_eq_abs, abs_exp, add_re, mul_re,
    re_ofNat, im_ofNat, zero_mul, sub_zero, Real.one_le_exp_iff]
  rw [re_tsum Hâ‚€, re_tsum Hâ‚, re_tsum Hâ‚‚, â† tsum_mul_left, â† tsum_mul_left,
    â† tsum_add hsumâ‚€ hsumâ‚, â† tsum_add (hsumâ‚€.add hsumâ‚) hsumâ‚‚]
  simpa only [neg_add_rev, neg_re, mul_neg, Ï‡.pow_apply' two_ne_zero, ge_iff_le, add_re, one_re,
    ofReal_re, ofReal_add, ofReal_one] using
      tsum_nonneg fun (p : Nat.Primes) â†¦ Ï‡.re_log_comb_nonneg p.prop.two_le hâ‚€ y
variable [NeZero N]
lemma norm_LFunction_product_ge_one {x : â„} (hx : 0 < x) (y : â„) :
    â€–LFunctionTrivChar N (1 + x) ^ 3 * LFunction Ï‡ (1 + x + I * y) ^ 4 *
      LFunction (Ï‡ ^ 2) (1 + x + 2 * I * y)â€– â‰¥ 1 := by
  have âŸ¨hâ‚€, hâ‚, hâ‚‚âŸ© := one_lt_re_one_add hx y
  rw [LFunctionTrivChar, DirichletCharacter.LFunction_eq_LSeries 1 hâ‚€,
    Ï‡.LFunction_eq_LSeries hâ‚, (Ï‡ ^ 2).LFunction_eq_LSeries hâ‚‚]
  exact norm_LSeries_product_ge_one Ï‡ hx y
lemma LFunctionTrivChar_isBigO_near_one_horizontal :
    (fun x : â„ â†¦ LFunctionTrivChar N (1 + x)) =O[ğ“[>] 0] fun x â†¦ (1 : â„‚) / x := by
  have : (fun w : â„‚ â†¦ LFunctionTrivChar N (1 + w)) =O[ğ“[â‰ ] 0] (1 / Â·) := by
    have H : Tendsto (fun w â†¦ w * LFunctionTrivChar N (1 + w)) (ğ“[â‰ ] 0)
        (ğ“ <| âˆ p âˆˆ N.primeFactors, (1 - (p : â„‚)â»Â¹)) := by
      convert (LFunctionTrivChar_residue_one (N := N)).comp (f := fun w â†¦ 1 + w) ?_ using 1
      Â· simp only [Function.comp_def, add_sub_cancel_left]
      Â· simpa only [tendsto_iff_comap, Homeomorph.coe_addLeft, add_zero, map_le_iff_le_comap] using
          ((Homeomorph.addLeft (1 : â„‚)).map_punctured_nhds_eq 0).le
    exact (isBigO_mul_iff_isBigO_div eventually_mem_nhdsWithin).mp <| H.isBigO_one â„‚
  exact (isBigO_comp_ofReal_nhds_ne this).mono <| nhds_right'_le_nhds_ne 0
omit [NeZero N] in
private lemma one_add_I_mul_ne_one_or {y : â„} (hy : y â‰  0 âˆ¨ Ï‡ â‰  1) :
    1 + I * y â‰  1 âˆ¨ Ï‡ â‰  1:= by
  simpa only [ne_eq, add_right_eq_self, _root_.mul_eq_zero, I_ne_zero, ofReal_eq_zero, false_or]
    using hy
lemma LFunction_isBigO_horizontal {y : â„} (hy : y â‰  0 âˆ¨ Ï‡ â‰  1) :
    (fun x : â„ â†¦ LFunction Ï‡ (1 + x + I * y)) =O[ğ“[>] 0] fun _ â†¦ (1 : â„‚) := by
  refine IsBigO.mono ?_ nhdsWithin_le_nhds
  simp_rw [add_comm (1 : â„‚), add_assoc]
  have := (Ï‡.differentiableAt_LFunction _ <| one_add_I_mul_ne_one_or Ï‡ hy).continuousAt
  rw [â† zero_add (1 + _)] at this
  exact this.comp (f := fun x : â„ â†¦ x + (1 + I * y)) (x := 0) (by fun_prop) |>.tendsto.isBigO_one â„‚
private lemma LFunction_isBigO_horizontal_of_eq_zero {y : â„} (hy : y â‰  0 âˆ¨ Ï‡ â‰  1)
    (h : LFunction Ï‡ (1 + I * y) = 0) :
    (fun x : â„ â†¦ LFunction Ï‡ (1 + x + I * y)) =O[ğ“[>] 0] fun x : â„ â†¦ (x : â„‚) := by
  simp_rw [add_comm (1 : â„‚), add_assoc]
  have := (Ï‡.differentiableAt_LFunction _ <| one_add_I_mul_ne_one_or Ï‡ hy).hasDerivAt
  rw [â† zero_add (1 + _)] at this
  simpa only [zero_add, h, sub_zero]
    using (Complex.isBigO_comp_ofReal_nhds
      (this.comp_add_const 0 _).differentiableAt.isBigO_sub) |>.mono nhdsWithin_le_nhds
private lemma LFunction_ne_zero_of_not_quadratic_or_ne_one {t : â„} (h : Ï‡ ^ 2 â‰  1 âˆ¨ t â‰  0) :
    LFunction Ï‡ (1 + I * t) â‰  0 := by
  intro Hz
  have hzâ‚ : t â‰  0 âˆ¨ Ï‡ â‰  1 := by
    refine h.symm.imp_right (fun h H â†¦ ?_)
    simp only [H, one_pow, ne_eq, not_true_eq_false] at h
  have hzâ‚‚ : 2 * t â‰  0 âˆ¨ Ï‡ ^ 2 â‰  1 :=
    h.symm.imp_left <| mul_ne_zero two_ne_zero
  have help (x : â„) : ((1 / x) ^ 3 * x ^ 4 * 1 : â„‚) = x := by
    rcases eq_or_ne x 0 with rfl | h
    Â· rw [ofReal_zero, zero_pow (by omega), mul_zero, mul_one]
    Â· rw [one_div, inv_pow, pow_succ _ 3, â† mul_assoc,
        inv_mul_cancelâ‚€ <| pow_ne_zero 3 (ofReal_ne_zero.mpr h), one_mul, mul_one]
  have Hâ‚€ : (fun _ : â„ â†¦ (1 : â„)) =O[ğ“[>] 0]
      fun x â†¦ LFunctionTrivChar N (1 + x) ^ 3 * LFunction Ï‡ (1 + x + I * t) ^ 4 *
                   LFunction (Ï‡ ^ 2) (1 + x + 2 * I * t) :=
    IsBigO.of_bound' <| eventually_nhdsWithin_of_forall
      fun _ hx â†¦ (norm_one (Î± := â„)).symm â–¸ (Ï‡.norm_LFunction_product_ge_one hx t).le
  have H := (LFunctionTrivChar_isBigO_near_one_horizontal (N := N)).pow 3 |>.mul <|
    (Ï‡.LFunction_isBigO_horizontal_of_eq_zero hzâ‚ Hz).pow 4 |>.mul <|
    LFunction_isBigO_horizontal _ hzâ‚‚
  simp only [ofReal_mul, ofReal_ofNat, mul_left_comm I, â† mul_assoc, help] at H
  replace H := (Hâ‚€.trans H).norm_right
  simp only [norm_eq_abs, abs_ofReal] at H
  #adaptation_note
  exact isLittleO_irrefl (.of_forall (fun _ â†¦ one_ne_zero)) <|
    (H.of_norm_right (F' := â„)).trans_isLittleO <| isLittleO_id_one.mono nhdsWithin_le_nhds
theorem LFunction_ne_zero_of_re_eq_one {s : â„‚} (hs : s.re = 1) (hÏ‡s : Ï‡ â‰  1 âˆ¨ s â‰  1) :
    LFunction Ï‡ s â‰  0 := by
  by_cases h : Ï‡ ^ 2 = 1 âˆ§ s = 1
  Â· exact h.2 â–¸ LFunction_apply_one_ne_zero_of_quadratic h.1 <| hÏ‡s.neg_resolve_right h.2
  Â· have hs' : s = 1 + I * s.im := by
      conv_lhs => rw [â† re_add_im s, hs, ofReal_one, mul_comm]
    rw [not_and_or, â† ne_eq, â† ne_eq, hs', add_right_ne_self] at h
    replace h : Ï‡ ^ 2 â‰  1 âˆ¨ s.im â‰  0 :=
      h.imp_right (fun H â†¦ by exact_mod_cast right_ne_zero_of_mul H)
    exact hs'.symm â–¸ Ï‡.LFunction_ne_zero_of_not_quadratic_or_ne_one h
theorem LFunction_ne_zero_of_one_le_re â¦ƒs : â„‚â¦„ (hÏ‡s : Ï‡ â‰  1 âˆ¨ s â‰  1) (hs : 1 â‰¤ s.re) :
    LFunction Ï‡ s â‰  0 :=
  hs.eq_or_lt.casesOn (fun hs â†¦ LFunction_ne_zero_of_re_eq_one Ï‡ hs.symm hÏ‡s)
    fun hs â†¦ LFunction_eq_LSeries Ï‡ hs â–¸ LSeries_ne_zero_of_one_lt_re Ï‡ hs
variable {Ï‡} in
theorem LFunction_apply_one_ne_zero (hÏ‡ : Ï‡ â‰  1) : LFunction Ï‡ 1 â‰  0 :=
  LFunction_ne_zero_of_one_le_re Ï‡ (.inl hÏ‡) <| one_re â–¸ le_rfl
lemma _root_.riemannZeta_ne_zero_of_one_le_re â¦ƒs : â„‚â¦„ (hs : 1 â‰¤ s.re) :
    riemannZeta s â‰  0 := by
  rcases eq_or_ne s 1 with rfl | hsâ‚€
  Â· exact riemannZeta_one_ne_zero
  Â· exact LFunction_modOne_eq (Ï‡ := 1) â–¸ LFunction_ne_zero_of_one_le_re _ (.inr hsâ‚€) hs
end nonvanishing
end DirichletCharacter