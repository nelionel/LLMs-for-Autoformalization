import Mathlib.CategoryTheory.Closed.Monoidal
import Mathlib.CategoryTheory.Functor.Currying
import Mathlib.CategoryTheory.Monoidal.FunctorCategory
noncomputable section
open CategoryTheory CategoryTheory.MonoidalCategory CategoryTheory.MonoidalClosed
namespace CategoryTheory.Functor
variable {D C : Type*} [Groupoid D] [Category C] [MonoidalCategory C] [MonoidalClosed C]
@[simps!]
def closedIhom (F : D ‚•§ C) : (D ‚•§ C) ‚•§ D ‚•§ C :=
  ((whiskeringRight‚ÇÇ D C·µí·µñ C C).obj internalHom).obj (Groupoid.invFunctor D ‚ãô F.op)
@[simps]
def closedUnit (F : D ‚•§ C) : ùü≠ (D ‚•§ C) ‚ü∂ tensorLeft F ‚ãô closedIhom F where
  app G :=
  { app := fun X => (ihom.coev (F.obj X)).app (G.obj X)
    naturality := by
      intro X Y f
      dsimp
      simp only [ihom.coev_naturality, closedIhom_obj_map, Monoidal.tensorObj_map]
      dsimp
      rw [coev_app_comp_pre_app_assoc, ‚Üê Functor.map_comp, tensorHom_def]
      simp }
@[simps]
def closedCounit (F : D ‚•§ C) : closedIhom F ‚ãô tensorLeft F ‚ü∂ ùü≠ (D ‚•§ C) where
  app G :=
  { app := fun X => (ihom.ev (F.obj X)).app (G.obj X)
    naturality := by
      intro X Y f
      dsimp
      simp only [closedIhom_obj_map, pre_comm_ihom_map]
      rw [tensorHom_def]
      simp }
instance closed (F : D ‚•§ C) : Closed F where
  rightAdj := closedIhom F
  adj :=
    { unit := closedUnit F
      counit := closedCounit F }
@[simps! closed_adj]
instance monoidalClosed : MonoidalClosed (D ‚•§ C) where
theorem ihom_map (F : D ‚•§ C) {G H : D ‚•§ C} (f : G ‚ü∂ H) : (ihom F).map f = (closedIhom F).map f :=
  rfl
theorem ihom_ev_app (F G : D ‚•§ C) : (ihom.ev F).app G = (closedCounit F).app G :=
  rfl
theorem ihom_coev_app (F G : D ‚•§ C) : (ihom.coev F).app G = (closedUnit F).app G :=
  rfl
end CategoryTheory.Functor