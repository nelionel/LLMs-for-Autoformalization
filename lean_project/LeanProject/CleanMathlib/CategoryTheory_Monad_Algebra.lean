import Mathlib.CategoryTheory.Monad.Basic
import Mathlib.CategoryTheory.Adjunction.Basic
import Mathlib.CategoryTheory.Functor.EpiMono
namespace CategoryTheory
open Category
universe v‚ÇÅ u‚ÇÅ
variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]
namespace Monad
structure Algebra (T : Monad C) : Type max u‚ÇÅ v‚ÇÅ where
  A : C
  a : (T : C ‚•§ C).obj A ‚ü∂ A
  unit : T.Œ∑.app A ‚â´ a = ùüô A := by aesop_cat
  assoc : T.Œº.app A ‚â´ a = (T : C ‚•§ C).map a ‚â´ a := by aesop_cat
attribute [reassoc] Algebra.unit Algebra.assoc
namespace Algebra
variable {T : Monad C}
@[ext]
structure Hom (A B : Algebra T) where
  f : A.A ‚ü∂ B.A
  h : (T : C ‚•§ C).map f ‚â´ B.a = A.a ‚â´ f := by aesop_cat
attribute [reassoc (attr := simp)] Hom.h
namespace Hom
def id (A : Algebra T) : Hom A A where f := ùüô A.A
instance (A : Algebra T) : Inhabited (Hom A A) :=
  ‚ü®{ f := ùüô _ }‚ü©
def comp {P Q R : Algebra T} (f : Hom P Q) (g : Hom Q R) : Hom P R where f := f.f ‚â´ g.f
end Hom
instance : CategoryStruct (Algebra T) where
  Hom := Hom
  id := Hom.id
  comp := @Hom.comp _ _ _
@[ext]
lemma Hom.ext' (X Y : Algebra T) (f g : X ‚ü∂ Y) (h : f.f = g.f) : f = g := Hom.ext h
@[simp]
theorem comp_eq_comp {A A' A'' : Algebra T} (f : A ‚ü∂ A') (g : A' ‚ü∂ A'') :
    Algebra.Hom.comp f g = f ‚â´ g :=
  rfl
@[simp]
theorem id_eq_id (A : Algebra T) : Algebra.Hom.id A = ùüô A :=
  rfl
@[simp]
theorem id_f (A : Algebra T) : (ùüô A : A ‚ü∂ A).f = ùüô A.A :=
  rfl
@[simp]
theorem comp_f {A A' A'' : Algebra T} (f : A ‚ü∂ A') (g : A' ‚ü∂ A'') : (f ‚â´ g).f = f.f ‚â´ g.f :=
  rfl
instance eilenbergMoore : Category (Algebra T) where
@[simps]
def isoMk {A B : Algebra T} (h : A.A ‚âÖ B.A)
    (w : (T : C ‚•§ C).map h.hom ‚â´ B.a = A.a ‚â´ h.hom := by aesop_cat) : A ‚âÖ B where
  hom := { f := h.hom }
  inv :=
    { f := h.inv
      h := by
        rw [h.eq_comp_inv, Category.assoc, ‚Üê w, ‚Üê Functor.map_comp_assoc]
        simp }
end Algebra
variable (T : Monad C)
@[simps]
def forget : Algebra T ‚•§ C where
  obj A := A.A
  map f := f.f
@[simps]
def free : C ‚•§ Algebra T where
  obj X :=
    { A := T.obj X
      a := T.Œº.app X
      assoc := (T.assoc _).symm }
  map f :=
    { f := T.map f
      h := T.Œº.naturality _ }
instance [Inhabited C] : Inhabited (Algebra T) :=
  ‚ü®(free T).obj default‚ü©
@[simps! unit counit]
def adj : T.free ‚ä£ T.forget :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun X Y =>
        { toFun := fun f => T.Œ∑.app X ‚â´ f.f
          invFun := fun f =>
            { f := T.map f ‚â´ Y.a
              h := by
                dsimp
                simp [‚Üê Y.assoc, ‚Üê T.Œº.naturality_assoc] }
          left_inv := fun f => by
            ext
            dsimp
            simp
          right_inv := fun f => by
            dsimp only [forget_obj]
            rw [‚Üê T.Œ∑.naturality_assoc, Y.unit]
            apply Category.comp_id } }
theorem algebra_iso_of_iso {A B : Algebra T} (f : A ‚ü∂ B) [IsIso f.f] : IsIso f :=
  ‚ü®‚ü®{   f := inv f.f
        h := by
          rw [IsIso.eq_comp_inv f.f, Category.assoc, ‚Üê f.h]
          simp },
      by aesop_cat‚ü©‚ü©
instance forget_reflects_iso : T.forget.ReflectsIsomorphisms where
  reflects {_ _} f := fun [IsIso f.f] => algebra_iso_of_iso T f
instance forget_faithful : T.forget.Faithful where
theorem algebra_epi_of_epi {X Y : Algebra T} (f : X ‚ü∂ Y) [h : Epi f.f] : Epi f :=
  (forget T).epi_of_epi_map h
theorem algebra_mono_of_mono {X Y : Algebra T} (f : X ‚ü∂ Y) [h : Mono f.f] : Mono f :=
  (forget T).mono_of_mono_map h
instance : T.forget.IsRightAdjoint  :=
  ‚ü®T.free, ‚ü®T.adj‚ü©‚ü©
@[simps]
def algebraFunctorOfMonadHom {T‚ÇÅ T‚ÇÇ : Monad C} (h : T‚ÇÇ ‚ü∂ T‚ÇÅ) : Algebra T‚ÇÅ ‚•§ Algebra T‚ÇÇ where
  obj A :=
    { A := A.A
      a := h.app A.A ‚â´ A.a
      unit := by
        dsimp
        simp [A.unit]
      assoc := by
        dsimp
        simp [A.assoc] }
  map f := { f := f.f }
@[simps (config := { rhsMd := .default })]
def algebraFunctorOfMonadHomId {T‚ÇÅ : Monad C} : algebraFunctorOfMonadHom (ùüô T‚ÇÅ) ‚âÖ ùü≠ _ :=
  NatIso.ofComponents fun X => Algebra.isoMk (Iso.refl _)
@[simps (config := { rhsMd := .default })]
def algebraFunctorOfMonadHomComp {T‚ÇÅ T‚ÇÇ T‚ÇÉ : Monad C} (f : T‚ÇÅ ‚ü∂ T‚ÇÇ) (g : T‚ÇÇ ‚ü∂ T‚ÇÉ) :
    algebraFunctorOfMonadHom (f ‚â´ g) ‚âÖ algebraFunctorOfMonadHom g ‚ãô algebraFunctorOfMonadHom f :=
  NatIso.ofComponents fun X => Algebra.isoMk (Iso.refl _)
@[simps (config := { rhsMd := .default })]
def algebraFunctorOfMonadHomEq {T‚ÇÅ T‚ÇÇ : Monad C} {f g : T‚ÇÅ ‚ü∂ T‚ÇÇ} (h : f = g) :
    algebraFunctorOfMonadHom f ‚âÖ algebraFunctorOfMonadHom g :=
  NatIso.ofComponents fun X => Algebra.isoMk (Iso.refl _)
@[simps]
def algebraEquivOfIsoMonads {T‚ÇÅ T‚ÇÇ : Monad C} (h : T‚ÇÅ ‚âÖ T‚ÇÇ) : Algebra T‚ÇÅ ‚âå Algebra T‚ÇÇ where
  functor := algebraFunctorOfMonadHom h.inv
  inverse := algebraFunctorOfMonadHom h.hom
  unitIso :=
    algebraFunctorOfMonadHomId.symm ‚â™‚â´
      algebraFunctorOfMonadHomEq (by simp) ‚â™‚â´ algebraFunctorOfMonadHomComp _ _
  counitIso :=
    (algebraFunctorOfMonadHomComp _ _).symm ‚â™‚â´
      algebraFunctorOfMonadHomEq (by simp) ‚â™‚â´ algebraFunctorOfMonadHomId
@[simp]
theorem algebra_equiv_of_iso_monads_comp_forget {T‚ÇÅ T‚ÇÇ : Monad C} (h : T‚ÇÅ ‚ü∂ T‚ÇÇ) :
    algebraFunctorOfMonadHom h ‚ãô forget _ = forget _ :=
  rfl
end Monad
namespace Comonad
structure Coalgebra (G : Comonad C) : Type max u‚ÇÅ v‚ÇÅ where
  A : C
  a : A ‚ü∂ (G : C ‚•§ C).obj A
  counit : a ‚â´ G.Œµ.app A = ùüô A := by aesop_cat
  coassoc : a ‚â´ G.Œ¥.app A = a ‚â´ G.map a := by aesop_cat
attribute [reassoc] Coalgebra.counit Coalgebra.coassoc
namespace Coalgebra
variable {G : Comonad C}
@[ext]
structure Hom (A B : Coalgebra G) where
  f : A.A ‚ü∂ B.A
  h : A.a ‚â´ (G : C ‚•§ C).map f = f ‚â´ B.a := by aesop_cat
attribute [reassoc (attr := simp)] Hom.h
namespace Hom
def id (A : Coalgebra G) : Hom A A where f := ùüô A.A
def comp {P Q R : Coalgebra G} (f : Hom P Q) (g : Hom Q R) : Hom P R where f := f.f ‚â´ g.f
end Hom
instance : CategoryStruct (Coalgebra G) where
  Hom := Hom
  id := Hom.id
  comp := @Hom.comp _ _ _
@[ext]
lemma Hom.ext' (X Y : Coalgebra G) (f g : X ‚ü∂ Y) (h : f.f = g.f) : f = g := Hom.ext h
@[simp]
theorem comp_eq_comp {A A' A'' : Coalgebra G} (f : A ‚ü∂ A') (g : A' ‚ü∂ A'') :
    Coalgebra.Hom.comp f g = f ‚â´ g :=
  rfl
@[simp]
theorem id_eq_id (A : Coalgebra G) : Coalgebra.Hom.id A = ùüô A :=
  rfl
@[simp]
theorem id_f (A : Coalgebra G) : (ùüô A : A ‚ü∂ A).f = ùüô A.A :=
  rfl
@[simp]
theorem comp_f {A A' A'' : Coalgebra G} (f : A ‚ü∂ A') (g : A' ‚ü∂ A'') : (f ‚â´ g).f = f.f ‚â´ g.f :=
  rfl
instance eilenbergMoore : Category (Coalgebra G) where
@[simps]
def isoMk {A B : Coalgebra G} (h : A.A ‚âÖ B.A)
    (w : A.a ‚â´ (G : C ‚•§ C).map h.hom = h.hom ‚â´ B.a := by aesop_cat) : A ‚âÖ B where
  hom := { f := h.hom }
  inv :=
    { f := h.inv
      h := by
        rw [h.eq_inv_comp, ‚Üê reassoc_of% w, ‚Üê Functor.map_comp]
        simp }
end Coalgebra
variable (G : Comonad C)
@[simps]
def forget : Coalgebra G ‚•§ C where
  obj A := A.A
  map f := f.f
@[simps]
def cofree : C ‚•§ Coalgebra G where
  obj X :=
    { A := G.obj X
      a := G.Œ¥.app X
      coassoc := (G.coassoc _).symm }
  map f :=
    { f := G.map f
      h := (G.Œ¥.naturality _).symm }
@[simps! unit counit]
def adj : G.forget ‚ä£ G.cofree :=
  Adjunction.mkOfHomEquiv
    { homEquiv := fun X Y =>
        { toFun := fun f =>
            { f := X.a ‚â´ G.map f
              h := by
                dsimp
                simp [‚Üê Coalgebra.coassoc_assoc] }
          invFun := fun g => g.f ‚â´ G.Œµ.app Y
          left_inv := fun f => by
            dsimp
            rw [Category.assoc, G.Œµ.naturality, Functor.id_map, X.counit_assoc]
          right_inv := fun g => by
            ext1; dsimp
            rw [Functor.map_comp, g.h_assoc, cofree_obj_a, Comonad.right_counit]
            apply comp_id } }
theorem coalgebra_iso_of_iso {A B : Coalgebra G} (f : A ‚ü∂ B) [IsIso f.f] : IsIso f :=
  ‚ü®‚ü®{   f := inv f.f
        h := by
          rw [IsIso.eq_inv_comp f.f, ‚Üê f.h_assoc]
          simp },
      by aesop_cat‚ü©‚ü©
instance forget_reflects_iso : G.forget.ReflectsIsomorphisms where
  reflects {_ _} f := fun [IsIso f.f] => coalgebra_iso_of_iso G f
instance forget_faithful : (forget G).Faithful where
theorem algebra_epi_of_epi {X Y : Coalgebra G} (f : X ‚ü∂ Y) [h : Epi f.f] : Epi f :=
  (forget G).epi_of_epi_map h
theorem algebra_mono_of_mono {X Y : Coalgebra G} (f : X ‚ü∂ Y) [h : Mono f.f] : Mono f :=
  (forget G).mono_of_mono_map h
instance : G.forget.IsLeftAdjoint  :=
  ‚ü®_, ‚ü®G.adj‚ü©‚ü©
end Comonad
end CategoryTheory